mapscripts CityClara_CannabisClub_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setweather (WEATHER_FOG_DIAGONAL)
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_CITYCLARA_STATE, 0: CityClar_CannabisCenter_ScottTrigger
	]
}

script CityClar_CannabisCenter_ScottTrigger {
	setvar (VAR_CITYCLARA_STATE, 1)
	lockall
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
	load_field_pic (4, 190, 98)
	msgbox (CityClara_CannabisCenter_Text_Leaf1) 
	applymovement (OBJ_EVENT_ID_PLAYER, CityClara_CannabisCenter_Movement_PlayerToScott)
	waitmovement (0)
	destroy_field_pic (4)	
	load_field_pic (5, 190, 98)
	msgbox (CityClara_CannabisCenter_Text_Scott1)	
	destroy_field_pic (5)	
	load_field_pic (4, 190, 98)
	msgbox (CityClara_CannabisCenter_Text_Leaf2) 
	destroy_field_pic (4)
	applymovement (OBJ_EVENT_ID_PLAYER, CityClara_CannabisCenter_Movement_PlayerDown) 
	waitmovement (0)	
	applymovement (3, CityClara_CannabisCenter_Movement_LeafExit)
	waitmovement (0)
	removeobject (3)
	clearflag (FLAG_CLARACITY_HIDELEAF)
	releaseall
	end
}

script CityClara_CannabisCenter_Clerk {
	lock
	faceplayer
	msgbox (CityClara_CannabisCenter_Text_Intro)
	closemessage	
	call (Common_EventScript_OutOfCenterPartyHeal)
	msgbox (CityClara_CannabisCenter_Text_Outtro)
	release
	end
}

script CityClara_CannabisCenter_Scott {
	lock
	faceplayer
	load_field_pic (5, 190, 98)
	msgbox (CityClara_CannabisCenter_Text_Scott)
	closemessage
	destroy_field_pic (5)
	applymovement (2, Common_Movement_FaceRight)
	release
	end
}

movement CityClara_CannabisCenter_Movement_PlayerDown {
	walk_down
	face_up
}

movement CityClara_CannabisCenter_Movement_PlayerToScott {
	walk_up * 3
	walk_right * 5
	face_up
}

movement CityClara_CannabisCenter_Movement_LeafExit {
	walk_down
	walk_left * 5
	walk_down * 3
}

text CityClara_CannabisCenter_Text_Intro {
	format("Hey, good to see you. Here man hit the bong!")
}

text CityClara_CannabisCenter_Text_Outtro {
	format("Just let me know if you want to smoke more.")
}

text CityClara_CannabisCenter_Text_Leaf1 {
	format("Oh {PLAYER} there you are! What took you so long? While we were waiting my dad got too high but I've grown impatient and must see mew.")
}

text CityClara_CannabisCenter_Text_Scott1 {
	format("Mew's like....a Pokemon...man...a LEDGEND!")
}

text CityClara_CannabisCenter_Text_Leaf2 {
	format("See what I mean? Dad is totally blitzed. I'll wait for you down by the church I guess but make sure to smoke up if you're tired.")
}

text CityClara_CannabisCenter_Text_Scott {
	format("Bro....\pDude! It's like why do the cows eat grass? And isn't life all connected as one straight through our moms right up the umbral cord...You will hear birds singing through tuesday's heat...")
}





