
const OBJ_EVENT_TEACHER = 1

mapscripts Ardor_Uni_ArtClass_MapScripts {

	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_CANVAS_STEP_COUNTER,  17: Ardor_Uni_ArtClass_EventScript_FacePlayerLeft
		VAR_CANVAS_STEP_COUNTER, 117: Ardor_Uni_ArtClass_EventScript_FacePlayerRight
	]

	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_CANVAS_STEP_COUNTER,  17: Ardor_Uni_ArtClass_EventScript_Test
		VAR_CANVAS_STEP_COUNTER, 117: Ardor_Uni_ArtClass_EventScript_Test
	]

}

script Ardor_Uni_ArtClass_EventScript_FacePlayerLeft {
	turnobject (OBJ_EVENT_ID_PLAYER, DIR_WEST)
	turnobject (OBJ_EVENT_TEACHER, DIR_EAST)
	end
}

script Ardor_Uni_ArtClass_EventScript_FacePlayerRight {
	turnobject (OBJ_EVENT_ID_PLAYER, DIR_EAST)
	turnobject (OBJ_EVENT_TEACHER, DIR_WEST)
	end
}

script Ardor_Uni_ArtClass_EventScript_Teacher {
	lock
	faceplayer
	if (defeated(TRAINER_UNI_ART)) {
		msgbox(Ardor_Uni_ArtClass_Text_ClassFinished)
		closemessage
	} else {
		msgbox(Ardor_Uni_ArtClass_Text_BeginArt, MSGBOX_YESNO)
		if (var(VAR_RESULT) == TRUE) {
			call (Ardor_Uni_ArtClass_EventScript_DoArt)
		}
		msgbox(Ardor_Uni_ArtClass_Text_ReturnSoon)
		closemessage
	}
	applymovement (OBJ_EVENT_TEACHER, Common_Movement_FaceDown)
	waitmovement (0)
	release
	end
}

script Ardor_Uni_ArtClass_EventScript_DoArt {
	
	specialvar (VAR_RESULT, GetPlayerFacingDirection)
	if (var(VAR_RESULT) == DIR_EAST) {
		setvar (VAR_CANVAS_STEP_COUNTER, 100)
	}
	warpsilent (MAP_ART_CANVAS, 32, 32)
	waitstate
	end
}

script Ardor_Uni_ArtClass_EventScript_Test {
	setvar (VAR_CANVAS_STEP_COUNTER, 420)
	msgbox(Ardor_Uni_Text_TestIntro)
	closemessage
	trainerbattle_no_intro(TRAINER_UNI_ART, Ardor_Uni_ArtClass_Text_TeacherBattleLost)
	call (Ardor_Uni_TermState_IncrementVar)
	msgbox(Ardor_Uni_ArtClass_Text_ClassFinished)
	closemessage
	return
}

text Ardor_Uni_ArtClass_Text_BeginArt {
	format("Salve puer. Magistra Gina sum. Necesse est pingere. Praestasne pingere?")
}

text Ardor_Uni_ArtClass_Text_ReturnSoon {
	format("Reveni cum amore artis.")
}

text Ardor_Uni_ArtClass_Text_ClassFinished {
	format("Congratulatio! Iam studuisti artem.")
}

text Ardor_Uni_ArtClass_Text_TeacherBattleLost {
	format("Pictor es.")
}




			
