
//DYNAMIC MAP
//VAR_TEMP_5
//1: ISLA PINA
//2: MARE OCCIDENS
//3: MARE TROPICUM
//4: Acta Echona

//Ocean Map Transisiton A: Transition_IslaPina
//Ocean Map Transisiton B: Transition_MareOccidens

//FLAG_TEMP_F used in Ulysses Fisherman scene for "player began scene talking to fisherman
//FLAG_TEMP_3 is set during interact with special Moly Tree

mapscripts MareS6_MapScripts {

	MAP_SCRIPT_ON_TRANSITION {
		//Handle Dynamic Map
		specialvar (VAR_RESULT, GetDynamicMapSec_MareS6_F)
		switch (var(VAR_RESULT)) {
			case 1:
				setvar (VAR_TEMP_5, 1)
			case 2:
				setvar (VAR_TEMP_5, 2)
			case 3:
				setvar (VAR_TEMP_5, 3)
			case 4:
				setvar (VAR_TEMP_5, 4)
        }
        //Setup Ulysses with fisherman
        if (var(VAR_ULYSSES_STATE) == 4) {
            call (MareS6_Script_ShowUlyssesWithFisherman)
        }
        //Setup Ulysses in Woods
        if (var(VAR_ULYSSES_STATE) == 5) {
            call (MareS6_Script_ShowUlyssesInForest)
        }
        //Setup Ulysses ante portam
        if (var(VAR_ULYSSES_STATE) == 6) {
            call (MareS6_Script_ShowUlyssesAntePortam)
        }
        //Hide pigges
	}

	MAP_SCRIPT_ON_LOAD {
        //Show / Hide Moly fruit 
		if (var(VAR_ULYSSES_STATE) == 8) {
			call (MareS6_Script_ShowMolyFruit)
		}
		//Lock Circe Door
		if (var(VAR_ULYSSES_STATE) < 7) {
			call (MareS6_Script_LockCirceDoor)
		}
	}
}

//Silent in this case as island transitions are handled by Start Stop surf task
script MareS6_Script_Transition_IslaPina {
	setvar (VAR_TEMP_5, 1)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script MareS6_Script_Transition_MareOccidens {
	setvar (VAR_TEMP_5, 2)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script MareS6_Script_MareTropicumTransition {
	setvar (VAR_TEMP_5, 3)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

//Playa Echona
script MareS6_Script_PlayaEchonaTransition {
	setvar (VAR_TEMP_5, 4)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script MareS6_Script_ShowUlyssesWithFisherman {
    setobjectxyperm (LOCALID_MARES6_ULYSSES, 72, 66)
    setobjectmovementtype (LOCALID_MARES6_ULYSSES, MOVEMENT_TYPE_FACE_RIGHT)

    setobjectmovementtype (LOCALID_MARES6_FISHERMAN, MOVEMENT_TYPE_FACE_LEFT)
    return
}

script MareS6_Script_ShowUlyssesInForest {
    setobjectxyperm (LOCALID_MARES6_ULYSSES, 34, 55)
    setobjectmovementtype (LOCALID_MARES6_ULYSSES, MOVEMENT_TYPE_LOOK_AROUND)
    return
}

script MareS6_Script_ShowUlyssesAntePortam {
    setobjectxyperm (LOCALID_MARES6_ULYSSES, 63, 44)
    setobjectmovementtype (LOCALID_MARES6_ULYSSES, MOVEMENT_TYPE_LOOK_AROUND)
    return
}

script MareS6_Script_ShowMolyFruit {
	setmetatile (16, 60, METATILE_BigIsland_SandPalmFruitL, FALSE)
	setmetatile (17, 60, METATILE_BigIsland_SandPalmFruitC, FALSE)
	setmetatile (18, 60, METATILE_BigIsland_SandPalmFruitR, FALSE)
	return
}

script MareS6_Script_LockCirceDoor {
	setmetatile (63, 42, METATILE_General_Door, TRUE)
	return
}

script MareS6_Script_Fisherman {
    load_field_pic (FIELD_PIC_FISHERMAN, 190, 98)
    //before Ulysses arrives
    if (var(VAR_ULYSSES_STATE) < 4) {
        faceplayer
        msgbox (MareS6_Text_FishermanBeforeUlysses)
    }
    //during Ulysses et Fisherman scene
    elif (var(VAR_ULYSSES_STATE) == 4) {
        setflag (FLAG_TEMP_F)
        call (MareS6_Script_UlyssesEtFishermanScene)
    }
    //during Ulysses et Circe scene
    elif (var(VAR_ULYSSES_STATE) < 12) {
        faceplayer
        msgbox (MareS6_Text_FishermanDuringUlysses)
    }
    //after Circe scene
    else {
        faceplayer
        msgbox (MareS6_Text_FishermanAfterUlysses)
    }
    destroy_field_pic (FIELD_PIC_FISHERMAN)
    closemessage
    unfaceplayer
    end
}

script MareS6_Script_Ulysses {
    //during Ulysses et fisherman scene
    if (var(VAR_ULYSSES_STATE) == 4) {
        call (MareS6_Script_UlyssesEtFishermanScene)
    }
    //Ulysses in the pen during Molly piggies
    //Ulysses on the western shores
}

script MareS6_Script_UlyssesEtFishermanScene {
    //F: exposition
    msgbox (MareS6_Text_FishermanExposition)
    destroy_field_pic (FIELD_PIC_FISHERMAN)
    //U: response
    load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
    msgbox (MareS6_Text_UlyssesResponseToFisherman)
    destroy_field_pic (FIELD_PIC_ULYSSES)
    closemessage
    //Alert U
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_ExclamationMark)
    waitmovement (0)
    //P to U if needed
    if (flag(FLAG_TEMP_F)) {
        applymovement (LOCALID_PLAYER, Common_Movement_WalkLeft)
        waitmovement (0)
        applymovement (LOCALID_PLAYER, Common_Movement_FaceUp)
        waitmovement (0)
    }
    //U face P
    specialvar (VAR_RESULT, GetPlayerFacingDirection)
    if (var(VAR_RESULT) == DIR_NORTH) {
        applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_FaceDown)
    } elif (var(VAR_RESULT) == DIR_EAST) {
        applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_FaceLeft)
    } else {
        msgbox ("ERROR, \nPlayerFacingDirection OOB")
        closemessage
        end
    }
    waitmovement (0)
    //U: call to action
    load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
    msgbox (MareS6_Text_UlyssesCallToAction)
    destroy_field_pic (FIELD_PIC_ULYSSES)
    closemessage
    //exit U
    specialvar (VAR_RESULT, GetPlayerFacingDirection)
    if (var(VAR_RESULT) == DIR_EAST) {
        applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_FaceDown)
        waitmovement (0)
    }
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_WalkLeft3)
    waitmovement (0)
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_WalkDown7)
    waitmovement (0)
    //update U position
    removeobject (LOCALID_MARES6_ULYSSES)
    call (MareS6_Script_ShowUlyssesInForest)
    addobject (LOCALID_MARES6_ULYSSES)
    //face fisherman
    applymovement (LOCALID_MARES6_FISHERMAN, Common_Movement_FaceUp)
    waitmovement (0)
    //update vars
    setvar (VAR_ULYSSES_STATE, 5)
    //fin
    end
}

script MareS6_Script_UlyssesJungleB {
    applymovement (LOCALID_PLAYER, Common_Movement_WalkUp)
    waitmovement (0)
    call (MareS6_Script_UlyssesJungle)
}

script MareS6_Script_UlyssesJungleC {
    applymovement (LOCALID_PLAYER, Common_Movement_WalkUp2)
    waitmovement (0)
    call (MareS6_Script_UlyssesJungle)
}

script MareS6_Script_UlyssesJungleD {
    applymovement (LOCALID_PLAYER, Common_Movement_WalkUp3)
    waitmovement (0)
    call (MareS6_Script_UlyssesJungle)
}

script MareS6_Script_UlyssesJungle {
    applymovement (LOCALID_PLAYER, Common_Movement_WalkRight)
    waitmovement (0)
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_ExclamationMark)
    waitmovement (0)
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_FaceLeft)
    waitmovement (0)
    load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
    msgbox (MareS6_Text_UlyssesJungle)
    destroy_field_pic (FIELD_PIC_ULYSSES)
    closemessage
    //U exit
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_WalkUp6)
    waitmovement (0)
    //Reposition U
    removeobject (LOCALID_MARES6_ULYSSES)
    call (MareS6_Script_ShowUlyssesAntePortam)
    addobject (LOCALID_MARES6_ULYSSES)
    //update var
    setvar (VAR_ULYSSES_STATE, 6)
    //fin
    end
}

script MareS6_Script_UlyssesAntePortamA {
    applymovement (LOCALID_PLAYER, Common_Movement_WalkRight)
    waitmovement (0)
    call (MareS6_Script_UlyssesAntePortam)
}

script MareS6_Script_UlyssesAntePortamC {
    applymovement (LOCALID_PLAYER, Common_Movement_WalkLeft)
    waitmovement (0)
    call (MareS6_Script_UlyssesAntePortam)
}

script MareS6_Script_UlyssesAntePortam {
    applymovement (LOCALID_PLAYER, Common_Movement_WalkUp)
    waitmovement (0)
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_FaceDown)
    waitmovement (0)
    load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
    msgbox (MareS6_Text_UlyssesAntePortam)
    destroy_field_pic (FIELD_PIC_ULYSSES)
    closemessage
    //U exit
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_WalkUp2)
    waitmovement (0)
    //Door stuff
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_WalkInPlaceFasterUp)
    waitmovement (0)
    opendoor (63, 42)
    waitdooranim
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_WalkInPlaceFasterUp)
    waitmovement (0)
    applymovement (LOCALID_MARES6_ULYSSES, Common_Movement_WalkUp)
    waitmovement (0)
    removeobject (LOCALID_MARES6_ULYSSES)
    setobjectxyperm (LOCALID_MARES6_ULYSSES, 0, 0)
    setobjectmovementtype (LOCALID_MARES6_ULYSSES, MOVEMENT_TYPE_INVISIBLE)
    addobject (LOCALID_MARES6_ULYSSES)
    closedoor (63, 42)
    waitdooranim
    //update var
    setvar (VAR_ULYSSES_STATE, 7)
    //fin
    end
}

//Transformed Prisoners as animals

script MareS6_Script_Shinx {
    //face player
    faceplayer
    //alert animal
    applymovement (LOCALID_MARES6_SHINX, Common_Movement_ExclamationMark)
    waitmovement (0)
    //do animal sound
	playmoncry (SPECIES_SHINX, CRY_MODE_ENCOUNTER)
	waitmoncry
    //msg the (animal) is looking at you knowingly
    bufferspeciesname (STR_VAR_1, SPECIES_SHINX)
    msgbox (MareS6_Text_MeetsGaze)
    //check for Moly
    checkitem (ITEM_PINA_FRUIT)
    if (var(VAR_RESULT) == YES) {
        msgbox (MareS6_Text_UsePinaFruit, MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO) {
            closemessage
            unfaceplayer
            end
        //if so YesNo give Moly
        } else {
            //M: Player gave V moly
            //fade screen
            //sound effect?
            //remove V
            //add sailor to correct location
            //face sailor to correct direction
            //setvar
        }
    }
}

script MareS6_Script_Turtwig {

}

script MareS6_Script_Torchic {

}

script MareS6_Script_Snubbull {

}

script MareS6_Script_Poochyena {

}

//This is the special tree
script MareS6_Script_PinaFruitTree {
    //setup special Moly Tree
    setflag (FLAG_TEMP_3)
    //generic Pina Tree
    call (MareS6_Script_PinaTree)
}

//These are the normal trees
script MareS6_Script_PinaTree {
    //Check if player mons have headbutt
    specialvar (VAR_RESULT, CheckHeadbutt)
    if (var(VAR_RESULT) == YES) {
        //Player Can Headbutt
        msgbox (MareS6_Text_WantToHeadbutt, MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
            call (MareS6_Script_HeadbuttTree)
        }
    } else {
        //player cannot headbutt
        msgbox (MareS6_Text_TreeIsHeadbuttable)
    }
    closemessage
    end
}

//The guy next to the fruit tree
script MareS6_Script_FruitFan {
    faceplayer
    //before fruit grows
    if (var(VAR_ULYSSES_STATE) < 8) {
        msgbox (MareS6_Text_FruitFan1)
    }
    //while fruit is growing
    elif (var(VAR_ULYSSES_STATE) == 8) {
        msgbox (MareS6_Text_FruitFan2)
    }
    //after you took fruit (battle?)
    else {
        msgbox (MareS6_Text_FruitFan3)
    }
    closemessage
    unfaceplayer
}


//NOTE Headbutt trees have not been added yet to expansion. I expect they will be but until then I just won't have actual normal encounters here just the special case BUT in the finished product all the trees on this island should be headbuttable, normally Execute but other stuffs sometimes yknow
script MareS6_Script_HeadbuttTree {
    //setup headbutt message
    bufferpartymonnick (STR_VAR_1, VAR_0x8004)
    buffermovename (STR_VAR_2, MOVE_HEADBUTT)
    //M: mon used headbutt
    msgbox (Text_MonUsedFieldMove)
	closemessage
    //check is follower
	specialvar (VAR_RESULT, IsMonFollower)
    ////setfieldeffectargument (3, VAR_0x8004) //skip pose if so
    if (var(VAR_RESULT) == TRUE) {
//        call (EventScript_FollowerFieldMove)
		msgbox ("FOLLOWER")
    } else {
		msgbox ("NOFOLLOWER")
	}
	closemessage
    //do headbutt
    playse (SE_M_HEADBUTT)
    waitse
    //check if it's Moly Fruit or regular
    if (flag(FLAG_TEMP_3) && var(VAR_ULYSSES_STATE) == 8) {
        call (MareS6_Script_PinaFruit)
    } else {
        //just placeholder battle - make it better of course
        setwildbattle (SPECIES_EXEGGCUTE, 7)
		dowildbattle
    }
    //reset moly tree flag
    clearflag (FLAG_TEMP_3)
    //fin
    end
}

//When the player gets the pina fruit
script MareS6_Script_PinaFruit {
    //alert player
    applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement (0)
    //falling sound
    playse (SE_FALL)
    waitse
    //metatiles
	setmetatile (16, 60, METATILE_BigIsland_SandPalmNoFruitL, FALSE)
	setmetatile (17, 60, METATILE_BigIsland_SandPalmNoFruitC, FALSE)
	setmetatile (18, 60, METATILE_BigIsland_SandPalmNoFruitR, FALSE)
    special (DrawWholeMapView)
    //fruit fall message
    msgbox (MareS6_Text_PinaFruitFell)
    closemessage
    //give item
    giveitem (ITEM_PINA_FRUIT)
    closemessage
    //setvar
    setvar (VAR_ULYSSES_STATE, 9)
    //fin
    return
}


text MareS6_Text_FishermanBeforeUlysses { 
    format("There's been some strange happenings on our sleepy little island. People have been going missing. I'm doing my best to stay out of it and focus on my fishing.")
}

text MareS6_Text_FishermanDuringUlysses {
    format("Well, I'll tell you what, that Ulysses character sure is a confident walker and a smooth talker, but I don't think anyone can stop that witch in the woods. No sir, I think he'd be better off staying here with me and the fishes.")
}

text MareS6_Text_FishermanAfterUlysses {
    format("This is my only joy in life, out here just me and the sea. I keep my rod clean and stay out of trouble, that's all I do.")
}

text MareS6_Text_FishermanExposition {
    format("Now losing people is an unfortunate part of life out here on Isla Pina, but nothing like this. We must have lost half a dozen just this month. We sent out a search party, all strong men like yourself, but we haven't seen them since. We all know it must be that crazy old witch who lives out in the woods but the way I see it there's nothing we can do about it.")
}

text MareS6_Text_UlyssesResponseToFisherman {
    format("That's terrible! Someone must do something!")
}

text MareS6_Text_UlyssesCallToAction {
    format("My goodness, {PLAYER}, am I glad to see you!
            I washed ashore and these good people of
            the island gave me food, water, and clothes.
            I was about to leave when I just learned
            an evil witch, who lives out in the woods,
            is kidnapping the people who live here.
            After the hospitality they showed
            to this stranger in his time of great need,
            it wouldn't be right for me to leave now
            without paying them back by finding this
            murderous witch and saving the island.
            Let's head for the jungle and search for the
            secret lair of the witch. I'll meet you there!")
}

text MareS6_Text_UlyssesJungle {
    format("This jungle more dangerous than I
            had expected but we cannot turn back.
            Lets muscle our way to the witches lair!")
}

text MareS6_Text_UlyssesAntePortam {
    format("This must be it, the evil witch's lair.
            Be cautious, we don't know what kind of tricks
            she has waiting for us once we're inside.
            Now follow me forward. Let's save the town!")
}

text MareS6_Text_MeetsGaze {
    format("{STR_VAR_1} meets your gaze with quiet understanding.")
}

text MareS6_Text_UsePinaFruit {
    format("Would you like to give {STR_VAR_1} some of the Piña fruit?")
}

text MareS6_Text_WantToHeadbutt {
    format("It's a large, formidable tree. Would you like to use Headbutt?")
}

text MareS6_Text_TreeIsHeadbuttable {
    format("There's a large, formidable tree that looks like it can be headbutted!")
}

text MareS6_Text_PinaFruitFell {
    format("The fruit fell down from the tree!")
}

text MareS6_Text_FruitFan1 {
    format("This piña tree is preparing to bloom!
            How do I know? I can talk to the trees.")
}

text MareS6_Text_FruitFan2 {
    format("The piña tree has borne fruit! Do you know
            how rare that is? Sometimes not even one
            tree on the whole island will fruit all year!")
}

text MareS6_Text_FruitFan3 {
    format("The piña fruit! What have you done with it?
            That should belong to the men and women
            of the island in common, not for some
            foreign child to steal! You'll pay for this!")
}

text MareS6_Text_PlayerGaveMoly {
    format("{PLAYER} gave {STR_VAR_1} some of the piña fruit.")
}

