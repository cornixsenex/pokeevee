

//DYNAMIC MAP TEMP_VAR_5

//1: Mare Mortuorum
//2: Mare Oriens
//3: Mare Subtropicum
//4: Isla Ignifera

const OBJ_EVENT_ARGO = 1
const OBJ_EVENT_CASTOR = 2
const OBJ_EVENT_POLLUX = 3
const OBJ_EVENT_ORPHEUS = 4
const OBJ_EVENT_THESEUS = 5
const OBJ_EVENT_PHILOCTETES = 6
const OBJ_EVENT_JASON = 7
const OBJ_EVENT_AMBUSH1 = 8
const OBJ_EVENT_AMBUSH2 = 9
const OBJ_EVENT_AMBUSH3 = 10
const OBJ_EVENT_AMBUSH4 = 11
const OBJ_EVENT_MEDEA = 12


const FIELD_PIC_JASON = 21
const FIELD_PIC_MEDEA = 24

mapscripts BigIslandN_MapScripts {

	MAP_SCRIPT_ON_TRANSITION {

        //Handle Dynamic Map
		specialvar (VAR_TEMP_5, GetDynamicMapSec_BigIslandN_F)
        //Setup for story based object placement - BELOW
		//2 = Boat has landed, Jason waits for journey to Colchis script
		if (var(VAR_ARGO_STATE) == 2) {
			call (BigIslandN_Script_ShowArgo2)
		//3 = Jason at the Cave Entrance, Phil abducted, Argonauts getting bored
		//7 = Colchis open -> Jason hidden inside colchis
		//Need to ammend if you can return from Colchis before it's time to Hide Argo
		} elif (var(VAR_ARGO_STATE) < 7 && var(VAR_ARGO_STATE) != 1) {
			call (BigIslandN_Script_ShowArgo3)
		} elif (var(VAR_ARGO_STATE) == 7) {
			call (BigIslandN_Script_ShowArgo7)
		} elif (var(VAR_ARGO_STATE) == 14) {
			call (BigIslandN_Script_ShowArgoExit)
		} else {
			call (BigIslandN_Script_HideArgo)
		}
	}

	MAP_SCRIPT_ON_LOAD {
		if (var(VAR_ARGO_STATE) < 7 || var(VAR_ARGO_STATE) > 18) {
			call (BigIslandN_Script_HideColchisCave)
		}
		end
	}

	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_ARGO_STATE, 14: BigIslandN_Script_ArgoExit
		VAR_ARGO_STATE, 18: BigIslandN_Script_EscapeColchis
	]

}

//Dynamic Map transitions

script BigIslandN_Script_Transition_MareMortuorum {
    setvar (VAR_TEMP_5, 1)
    special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script BigIslandN_Script_Transition_MareOriens {
    setvar (VAR_TEMP_5, 2)
    special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script BigIslandN_Script_Transition_MareSubtropicum {
    setvar (VAR_TEMP_5, 3)
    special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script BigIslandN_Script_Transition_IslaIgnifera {
    setvar (VAR_TEMP_5, 4)
    special (TransitionMapMusic)
    special (ShowMapNamePopup)
}


script BigIslandN_Script_ShowColchisCave {
	setmetatile (31, 35, METATILE_General_CaveEntrance_Top, FALSE) 
	setmetatile (31, 36, METATILE_General_CaveEntrance_Bottom, FALSE) 
	return
}

script BigIslandN_Script_HideColchisCave {
	setmetatile (31, 35, METATILE_General_RockWall_RockBase, TRUE) 
	setmetatile (31, 36, METATILE_General_RockWall_GrassBase, TRUE) 
	return
}

script BigIslandN_Script_HideArgo {

	setobjectxyperm (OBJ_EVENT_ARGO, 57, 32)
	setobjectmovementtype (OBJ_EVENT_ARGO, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (OBJ_EVENT_CASTOR, 58, 32)
	setobjectmovementtype (OBJ_EVENT_CASTOR, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (OBJ_EVENT_POLLUX, 59, 32)
	setobjectmovementtype (OBJ_EVENT_POLLUX, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (OBJ_EVENT_ORPHEUS, 60, 32)
	setobjectmovementtype (OBJ_EVENT_ORPHEUS, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (OBJ_EVENT_THESEUS, 61, 32)
	setobjectmovementtype (OBJ_EVENT_THESEUS, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (OBJ_EVENT_PHILOCTETES, 62, 32)
	setobjectmovementtype (OBJ_EVENT_PHILOCTETES, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (OBJ_EVENT_JASON, 63, 32)
	setobjectmovementtype (OBJ_EVENT_JASON, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (OBJ_EVENT_MEDEA, 64, 32)
	setobjectmovementtype (OBJ_EVENT_MEDEA, MOVEMENT_TYPE_INVISIBLE)

	hideobjectat (OBJ_EVENT_ARGO, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_ARGO)

	hideobjectat (OBJ_EVENT_CASTOR, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_CASTOR)

	hideobjectat (OBJ_EVENT_POLLUX, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_POLLUX)

	hideobjectat (OBJ_EVENT_THESEUS, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_THESEUS)

	hideobjectat (OBJ_EVENT_ORPHEUS, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_ORPHEUS)

	hideobjectat (OBJ_EVENT_PHILOCTETES, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_PHILOCTETES)

	hideobjectat (OBJ_EVENT_JASON, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_JASON)

	hideobjectat (OBJ_EVENT_MEDEA, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_MEDEA)

	return
}

script BigIslandN_Script_ShowArgo2 {
	setobjectxyperm (OBJ_EVENT_ARGO, 80, 17)
	setobjectmovementtype (OBJ_EVENT_ARGO, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_ARGO)
	showobjectat (OBJ_EVENT_ARGO, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_CASTOR, 81, 18)
	setobjectmovementtype (OBJ_EVENT_CASTOR, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_CASTOR)
	showobjectat (OBJ_EVENT_CASTOR, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_POLLUX, 82, 18)
	setobjectmovementtype (OBJ_EVENT_POLLUX, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_POLLUX)
	showobjectat (OBJ_EVENT_POLLUX, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_ORPHEUS, 111, 14)
	setobjectmovementtype (OBJ_EVENT_ORPHEUS, MOVEMENT_TYPE_FACE_UP)
	addobject (OBJ_EVENT_ORPHEUS)
	showobjectat (OBJ_EVENT_ORPHEUS, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_THESEUS, 60, 21)
	setobjectmovementtype (OBJ_EVENT_THESEUS, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_THESEUS)
	showobjectat (OBJ_EVENT_THESEUS, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_JASON, 50, 30)
	setobjectmovementtype (OBJ_EVENT_JASON, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_JASON)
	showobjectat (OBJ_EVENT_JASON, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_PHILOCTETES, 51, 30)
	setobjectmovementtype (OBJ_EVENT_PHILOCTETES, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_PHILOCTETES)
	showobjectat (OBJ_EVENT_PHILOCTETES, MAP_BIG_ISLAND_N)

	return
}

//Jason at Cave entrance, Phil gone, Theseus in town, Dioscuri at boat, Orpheus on the beach
script BigIslandN_Script_ShowArgo3 {
	setobjectxyperm (OBJ_EVENT_ARGO, 80, 17)
	setobjectmovementtype (OBJ_EVENT_ARGO, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_ARGO)
	showobjectat (OBJ_EVENT_ARGO, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_CASTOR, 41, 12)
	setobjectmovementtype(OBJ_EVENT_CASTOR, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_CASTOR)
	showobjectat (OBJ_EVENT_CASTOR, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_POLLUX, 42, 12)
	setobjectmovementtype (OBJ_EVENT_POLLUX, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_POLLUX)
	showobjectat (OBJ_EVENT_POLLUX, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_ORPHEUS, 111, 14)
	setobjectmovementtype (OBJ_EVENT_ORPHEUS, MOVEMENT_TYPE_FACE_UP)
	addobject (OBJ_EVENT_ORPHEUS)
	showobjectat (OBJ_EVENT_ORPHEUS, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_THESEUS, 68, 26)
	setobjectmovementtype (OBJ_EVENT_THESEUS, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_THESEUS)
	showobjectat (OBJ_EVENT_THESEUS, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_JASON, 31, 37)
	setobjectmovementtype (OBJ_EVENT_JASON, MOVEMENT_TYPE_FACE_UP)
	addobject (OBJ_EVENT_JASON)
	showobjectat (OBJ_EVENT_JASON, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_PHILOCTETES, 62, 32)
	setobjectmovementtype (OBJ_EVENT_PHILOCTETES, MOVEMENT_TYPE_INVISIBLE)
	hideobjectat (OBJ_EVENT_PHILOCTETES, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_PHILOCTETES)

	end	
}

script BigIslandN_Script_ShowArgo7 {
	setobjectxyperm (OBJ_EVENT_ARGO, 80, 17)
	setobjectmovementtype (OBJ_EVENT_ARGO, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_ARGO)
	showobjectat (OBJ_EVENT_ARGO, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_CASTOR, 81, 18)
	setobjectmovementtype (OBJ_EVENT_CASTOR, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_CASTOR)
	showobjectat (OBJ_EVENT_CASTOR, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_POLLUX, 82, 18)
	setobjectmovementtype (OBJ_EVENT_POLLUX, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_POLLUX)
	showobjectat (OBJ_EVENT_POLLUX, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_ORPHEUS, 111, 14)
	setobjectmovementtype (OBJ_EVENT_ORPHEUS, MOVEMENT_TYPE_FACE_UP)
	addobject (OBJ_EVENT_ORPHEUS)
	showobjectat (OBJ_EVENT_ORPHEUS, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_THESEUS, 91, 26)
	setobjectmovementtype (OBJ_EVENT_THESEUS, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_THESEUS)
	showobjectat (OBJ_EVENT_THESEUS, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_JASON, 63, 32)
	setobjectmovementtype (OBJ_EVENT_JASON, MOVEMENT_TYPE_INVISIBLE)
	hideobjectat (OBJ_EVENT_JASON, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_JASON)

	setobjectxyperm (OBJ_EVENT_PHILOCTETES, 62, 32)
	setobjectmovementtype (OBJ_EVENT_PHILOCTETES, MOVEMENT_TYPE_INVISIBLE)
	hideobjectat (OBJ_EVENT_PHILOCTETES, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_PHILOCTETES)

	end
}

script BigIslandN_Script_ShowArgoExit {
	setobjectxyperm (OBJ_EVENT_ARGO, 80, 17)
	setobjectmovementtype (OBJ_EVENT_ARGO, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_ARGO)
	showobjectat (OBJ_EVENT_ARGO, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_CASTOR, 81, 21)
	setobjectmovementtype (OBJ_EVENT_CASTOR, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_CASTOR)
	showobjectat (OBJ_EVENT_CASTOR, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_POLLUX, 82, 21)
	setobjectmovementtype (OBJ_EVENT_POLLUX, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_POLLUX)
	showobjectat (OBJ_EVENT_POLLUX, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_ORPHEUS, 83, 21)
	setobjectmovementtype (OBJ_EVENT_ORPHEUS, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_ORPHEUS)
	showobjectat (OBJ_EVENT_ORPHEUS, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_THESEUS, 80, 21)
	setobjectmovementtype (OBJ_EVENT_THESEUS, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_THESEUS)
	showobjectat (OBJ_EVENT_THESEUS, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_JASON, 31, 39)
	setobjectmovementtype (OBJ_EVENT_JASON, MOVEMENT_TYPE_FACE_LEFT)
	addobject (OBJ_EVENT_JASON, MAP_BIG_ISLAND_N)
	showobjectat (OBJ_EVENT_JASON, MAP_BIG_ISLAND_N)

	setobjectxyperm (OBJ_EVENT_MEDEA, 30, 39)
	setobjectmovementtype (OBJ_EVENT_MEDEA, MOVEMENT_TYPE_FACE_RIGHT)
	addobject (OBJ_EVENT_MEDEA, MAP_BIG_ISLAND_N)
	showobjectat (OBJ_EVENT_MEDEA, MAP_BIG_ISLAND_N)

	end
}

script BigIslandN_Script_ArgoExit {
	lockall
	
	//Player down to Jason and Medea
	applymovement (OBJ_EVENT_ID_PLAYER, BigIslandN_Movement_PlayerExitColchis)
	waitmovement (0)

	//! POINTS
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)

	//Hide Cave
	
	//Set vars for ShakeCamera
	setvar (VAR_0x8004, 1)
	setvar (VAR_0x8005, 1)
	setvar (VAR_0x8006, 16)
	setvar (VAR_0x8007, 3)
	
	special (ShakeCamera)
	waitstate
	playse (SE_EFFECTIVE)
	waitse
	call (BigIslandN_Script_HideColchisCave)
	special (DrawWholeMapView)
	delay (30)
	
	//M: The way is forever shut
	load_field_pic(FIELD_PIC_MEDEA, 190, 98)
	msgbox (BigIslandN_Text_MedeaExitColchis, MSGBOX_DEFAULT)
	destroy_field_pic(FIELD_PIC_MEDEA)

	//Jason: Let us be off
	load_field_pic (FIELD_PIC_JASON, 190, 99)
	msgbox (BigIslandN_Text_JasonExitColchis, MSGBOX_DEFAULT)
	closemessage
	destroy_field_pic (FIELD_PIC_JASON)
	//Walk to Argo
	applymovement (OBJ_EVENT_ID_PLAYER, BigIslandN_Movement_ExeuntArgo1)
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_ExeuntArgo1)
	applymovement (OBJ_EVENT_MEDEA, BigIslandN_Movement_ExeuntArgo1)
	waitmovement (0)
	applymovement (OBJ_EVENT_JASON, Common_Movement_WalkRight)
	applymovement (OBJ_EVENT_MEDEA, Common_Movement_WalkRight)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
	applymovement (OBJ_EVENT_JASON, Common_Movement_WalkRight)
	applymovement (OBJ_EVENT_MEDEA, Common_Movement_WalkRight)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, BigIslandN_Movement_ExeuntArgo2)
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_ExeuntArgo2)
	applymovement (OBJ_EVENT_MEDEA, BigIslandN_Movement_ExeuntArgo2)
	waitmovement (0)

	//Jason speech to the crew: Our quest is won, meet Medea, honor Jason
	load_field_pic (FIELD_PIC_JASON, 190, 99)
	msgbox (BigIslandN_Text_JasonExeunt1, MSGBOX_DEFAULT)
	destroy_field_pic (FIELD_PIC_JASON)

	msgbox (BigIslandN_Text_ThreeCheers, MSGBOX_DEFAULT)

	load_field_pic (FIELD_PIC_JASON, 190, 99)
	msgbox (BigIslandN_Text_JasonExeunt2, MSGBOX_DEFAULT)
	closemessage
	destroy_field_pic (FIELD_PIC_JASON)
	
	//Crew exit Jason and Medea into position
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_JasonMedeaExeunt)
	applymovement (OBJ_EVENT_MEDEA, BigIslandN_Movement_JasonMedeaExeunt)
	applymovement (OBJ_EVENT_CASTOR, BigIslandN_Movement_DioscuriExeunt)
	applymovement (OBJ_EVENT_POLLUX, BigIslandN_Movement_DioscuriExeunt)
	applymovement (OBJ_EVENT_THESEUS, BigIslandN_Movement_TheseusExeunt)
	applymovement (OBJ_EVENT_ORPHEUS, BigIslandN_Movement_OrpheusExeunt)
	waitmovement (0)

	//Jason turns back
	applymovement (OBJ_EVENT_JASON, Common_Movement_FaceDown)
	waitmovement (0)
	
	//J: visit us in Bulbus
	load_field_pic (FIELD_PIC_JASON, 190, 99)
	msgbox (BigIslandN_Text_JasonExeunt3, MSGBOX_DEFAULT)
	closemessage
	destroy_field_pic (FIELD_PIC_JASON)
	
	//They exit
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_DioscuriExeunt)
	applymovement (OBJ_EVENT_MEDEA, BigIslandN_Movement_DioscuriExeunt)
	waitmovement (0)

	//Set vars and flags
	setvar (VAR_ARGO_STATE, 20)
	call (BigIslandN_Script_HideArgo)

	releaseall
	end
}

script BigIslandN_Script_EscapeColchis {
	lockall

	//Player walks down	
	applymovement (OBJ_EVENT_ID_PLAYER, BigIslandN_Movement_PlayerExitColchis)
	waitmovement (0)

	//! point
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
	
	//Hide Cave
	//Set vars for ShakeCamera
	setvar (VAR_0x8004, 1)
	setvar (VAR_0x8005, 1)
	setvar (VAR_0x8006, 16)
	setvar (VAR_0x8007, 3)
	
	special (ShakeCamera)
	waitstate
	playse (SE_EFFECTIVE)
	waitse
	call (BigIslandN_Script_HideColchisCave)
	special (DrawWholeMapView)
	delay (30)

	//Set vars and flags
	setvar (VAR_ARGO_STATE, 20)
	
	//end
	releaseall
	end
}

script BigIslandN_Script_ArgoArrival {

	delay (50)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
	
	//ETC 
	setflag (FLAG_MARES5_HIDE_ARGO)
	setvar (VAR_ARGO_STATE, 2)


	//Clear Argonauts from map and set them on the boat

	//We've landed...west side of the island
	msgbox (BigIslandN_Text_ArrivedAtColchis1, MSGBOX_DEFAULT)
	closemessage

	//Jason and Player disembark first
	special (DrawWholeMapView)
	removeobject (OBJ_EVENT_JASON)
	setobjectxyperm (OBJ_EVENT_JASON, 80, 17)
	setobjectmovementtype (OBJ_EVENT_JASON, MOVEMENT_TYPE_FACE_RIGHT)
	resetobjectsubpriority (OBJ_EVENT_ID_PLAYER, MAP_MARE_S5)
	call (Common_EventScript_StopBrineysBoatMusic)
	addobject (OBJ_EVENT_JASON)
	showobjectat (OBJ_EVENT_JASON, MAP_BIG_ISLAND_N)
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_JasonDisembark)
	delay (25)
	applymovement (OBJ_EVENT_ID_PLAYER, MareS5_Movement_PlayerDisembark)
	showobjectat (OBJ_EVENT_ID_PLAYER, MAP_BIG_ISLAND_N)
	waitmovement (0)

	//Set the boat up
	showobjectat (OBJ_EVENT_ARGO, MAP_BIG_ISLAND_N)
	addobject (OBJ_EVENT_ARGO)
	setobjectxyperm (OBJ_EVENT_ARGO, 80, 17)
	setobjectmovementtype (OBJ_EVENT_ARGO, MOVEMENT_TYPE_FACE_DOWN)
	hideobjectat (OBJ_EVENT_ARGO, MAP_MARE_S5)

	//Orpheus
	removeobject (OBJ_EVENT_ORPHEUS)
	setobjectxyperm (OBJ_EVENT_ORPHEUS, 80, 17)
	setobjectmovementtype (OBJ_EVENT_ORPHEUS, MOVEMENT_TYPE_FACE_RIGHT)
	msgbox (BigIslandN_Text_ArrivedAtColchis2, MSGBOX_DEFAULT)
	closemessage
	addobject (OBJ_EVENT_ORPHEUS)
	showobjectat (OBJ_EVENT_ORPHEUS, MAP_BIG_ISLAND_N)
	applymovement (OBJ_EVENT_ORPHEUS, BigIslandN_Movement_OrpheusDisembark)
	waitmovement (0)

	//Theseus
	removeobject (OBJ_EVENT_THESEUS)
	setobjectxyperm (OBJ_EVENT_THESEUS, 80, 17)
	setobjectmovementtype (OBJ_EVENT_THESEUS, MOVEMENT_TYPE_FACE_RIGHT)
	msgbox (BigIslandN_Text_ArrivedAtColchis3, MSGBOX_DEFAULT)
	closemessage
	addobject (OBJ_EVENT_THESEUS)
	showobjectat (OBJ_EVENT_THESEUS, MAP_BIG_ISLAND_N)
	applymovement (OBJ_EVENT_THESEUS, BigIslandN_Movement_TheseusDisembark)
	waitmovement (0)

	//Castor, Pollux, et Philoctetes
	removeobject (OBJ_EVENT_CASTOR)
	removeobject (OBJ_EVENT_POLLUX)
	setobjectmovementtype (OBJ_EVENT_CASTOR, MOVEMENT_TYPE_FACE_RIGHT)
	setobjectmovementtype (OBJ_EVENT_POLLUX, MOVEMENT_TYPE_FACE_RIGHT)
	setobjectxyperm (OBJ_EVENT_CASTOR, 80, 17)
	setobjectxyperm (OBJ_EVENT_POLLUX, 80, 17)
	removeobject (OBJ_EVENT_PHILOCTETES)
	setobjectxyperm (OBJ_EVENT_PHILOCTETES, 80, 17)
	setobjectmovementtype (OBJ_EVENT_PHILOCTETES, MOVEMENT_TYPE_FACE_RIGHT)
	msgbox (BigIslandN_Text_ArrivedAtColchis4, MSGBOX_DEFAULT)
	closemessage
	addobject (OBJ_EVENT_PHILOCTETES)
	showobjectat (OBJ_EVENT_PHILOCTETES, MAP_BIG_ISLAND_N)
	applymovement (OBJ_EVENT_PHILOCTETES, BigIslandN_Movement_PhiloctetesDisembark)
	waitmovement (0)
	addobject (OBJ_EVENT_POLLUX)
	showobjectat (OBJ_EVENT_POLLUX, MAP_BIG_ISLAND_N)
	applymovement (OBJ_EVENT_POLLUX, BigIslandN_Movement_PolluxDisembark)
	waitmovement (0)
	addobject (OBJ_EVENT_CASTOR)
	showobjectat (OBJ_EVENT_CASTOR, MAP_BIG_ISLAND_N)
	applymovement (OBJ_EVENT_CASTOR, BigIslandN_Movement_CastorDisembark)
	waitmovement (0)

	//Jason - Meet me then exit
	applymovement (OBJ_EVENT_JASON, Common_Movement_FaceUp)
	waitmovement (0)
	msgbox (BigIslandN_Text_ArrivedAtColchis5, MSGBOX_DEFAULT)
	closemessage

	//warpsilent(MAP_BIG_ISLAND_N, 81, 22)
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_JasonExitArgo)
	applymovement (OBJ_EVENT_PHILOCTETES, BigIslandN_Movement_JasonExitArgo)
	waitmovement (0)

	//Argonauts now in position / save position for resets

	hideobjectat (OBJ_EVENT_JASON, MAP_BIG_ISLAND_N)
	hideobjectat (OBJ_EVENT_PHILOCTETES, MAP_BIG_ISLAND_N)
	hideobjectat (OBJ_EVENT_THESEUS, MAP_BIG_ISLAND_N)
	hideobjectat (OBJ_EVENT_ORPHEUS, MAP_BIG_ISLAND_N)
	hideobjectat (OBJ_EVENT_ARGO, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_JASON)
	removeobject (OBJ_EVENT_PHILOCTETES)
	removeobject (OBJ_EVENT_THESEUS)
	removeobject (OBJ_EVENT_ORPHEUS)
	removeobject (OBJ_EVENT_ARGO)
	call (BigIslandN_Script_ShowArgo2)
	special (DrawWholeMapView)

	releaseall

	end
}

script BigIslandN_Script_Philoctetes {
	lock
	faceplayer
	msgbox (BigIslandN_Text_Philoctetes, MSGBOX_DEFAULT)
	closemessage
	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_FaceDown)
	waitmovement (0)
	release
	end
}

script BigIslandN_Script_Jason {
	lockall
	if (var(VAR_ARGO_STATE) == 2) {
		faceplayer
		msgbox (BigIslandN_Text_Jason, MSGBOX_DEFAULT)
		closemessage
		specialvar (VAR_RESULT, GetPlayerFacingDirection)
		if (var(VAR_RESULT) == DIR_NORTH) {
			call (BigIslandN_Script_ExploreA)
		} else {
			call (BigIslandN_Script_ExploreB)
		}
	} elif (var(VAR_ARGO_STATE) <= 6) {
		call (BigIslandN_Script_JasonAtColchisCave)
	} else {
		msgbox (format("BAD VAR in BigIslandN_Script_Jason"), MSGBOX_DEFAULT)
		closemessage
	}
		
	releaseall
	end
}

//Player faces up
script BigIslandN_Script_ExploreA {
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_JasonExploreA1)
	applymovement (OBJ_EVENT_PHILOCTETES, BigIslandN_Movement_PhiloctetesExploreA1)
	waitmovement (0)
	applymovement (OBJ_EVENT_JASON, Common_Movement_WalkLeft)
	waitmovement (0)
	applymovement (OBJ_EVENT_JASON, Common_Movement_WalkLeft)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_WalkLeft)
	waitmovement (0)
	call (BigIslandN_Script_Ambush)
}

script BigIslandN_Script_ExploreB {
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_WalkDown)
	waitmovement (0)
	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_WalkLeft)
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_JasonExploreB1)
	waitmovement (0)
	call (BigIslandN_Script_Ambush)
}

script BigIslandN_Script_Ambush {
	//walk to the meadow
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_AmbushAll)
	applymovement (OBJ_EVENT_PHILOCTETES, BigIslandN_Movement_AmbushAll)
	applymovement (OBJ_EVENT_ID_PLAYER, BigIslandN_Movement_AmbushAll)
	waitmovement (0)
	applymovement (OBJ_EVENT_JASON, Common_Movement_ExclamationMark)
	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_ExclamationMark)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
	playmoncry (SPECIES_CHANDELURE, CRY_MODE_NORMAL)
	waitmoncry 
	msgbox (BigIslandN_Text_Ambush, MSGBOX_DEFAULT)
	closemessage

	//Dudes appear
	removeobject (OBJ_EVENT_AMBUSH1)
	setobjectxyperm (OBJ_EVENT_AMBUSH1, 45, 36)
	setobjectmovementtype (OBJ_EVENT_AMBUSH1, MOVEMENT_TYPE_FACE_UP)
	addobject (OBJ_EVENT_AMBUSH1)
	showobjectat (OBJ_EVENT_AMBUSH1, MAP_BIG_ISLAND_N)
	applymovement (OBJ_EVENT_AMBUSH1, BigIslandN_Movement_Ambusher1)
	waitmovement (0)

	removeobject (OBJ_EVENT_AMBUSH2)
	setobjectxyperm (OBJ_EVENT_AMBUSH2, 45, 37)
	setobjectmovementtype (OBJ_EVENT_AMBUSH2, MOVEMENT_TYPE_FACE_LEFT)
	addobject (OBJ_EVENT_AMBUSH2)
	showobjectat (OBJ_EVENT_AMBUSH2, MAP_BIG_ISLAND_N)
	applymovement (OBJ_EVENT_AMBUSH2, Common_Movement_WalkLeft)
	waitmovement (0)

	removeobject (OBJ_EVENT_AMBUSH3)
	setobjectxyperm (OBJ_EVENT_AMBUSH3, 39, 37)
	setobjectmovementtype (OBJ_EVENT_AMBUSH3, MOVEMENT_TYPE_FACE_RIGHT)
	addobject (OBJ_EVENT_AMBUSH3)
	showobjectat (OBJ_EVENT_AMBUSH3, MAP_BIG_ISLAND_N)
	applymovement (OBJ_EVENT_AMBUSH3, Common_Movement_WalkRight)
	waitmovement (0)

	removeobject (OBJ_EVENT_AMBUSH4)
	setobjectxyperm (OBJ_EVENT_AMBUSH4, 39, 35)
	setobjectmovementtype (OBJ_EVENT_AMBUSH4, MOVEMENT_TYPE_FACE_RIGHT)
	addobject (OBJ_EVENT_AMBUSH4)
	showobjectat (OBJ_EVENT_AMBUSH4, MAP_BIG_ISLAND_N)
	applymovement (OBJ_EVENT_AMBUSH4, Common_Movement_WalkRight)
	waitmovement (0)


	//Defend the party
	msgbox (BigIslandN_Text_Ambush2, MSGBOX_DEFAULT)
	closemessage
	applymovement (OBJ_EVENT_ID_PLAYER, BigIslandN_Movement_PlayerAmbush2)
	waitmovement (0)
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_JasonAmbush2)
	waitmovement (0)
	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_WalkRight)
	waitmovement (0)
	applymovement (OBJ_EVENT_AMBUSH2, Common_Movement_FaceUp)
	applymovement (OBJ_EVENT_AMBUSH1, Common_Movement_FaceDown)
	waitmovement (0)
	applymovement (OBJ_EVENT_JASON, Common_Movement_FaceLeft)
	waitmovement (0)

	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_FaceUp)
	waitmovement (0)
	delay (10)
	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_FaceDown)
	waitmovement (0)
	delay (10)
	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_FaceRight)
	waitmovement (0)
	delay (10)
	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_ExclamationMark)
	waitmovement (0)


	msgbox (BigIslandN_Text_ColchisGrunt1Pre, MSGBOX_DEFAULT)
	closemessage
	//battle
	trainerbattle_no_intro (TRAINER_IGNIS_GRUNT_1, BigIslandN_Text_ColchisGrunt1Lost)

	//Open Cave for next shot

	call (BigIslandN_Script_ShowColchisCave)
	
	
	msgbox (BigIslandN_Text_ColchisGrunt1Post, MSGBOX_DEFAULT)
	closemessage
	//Defeated Flee
	applymovement (OBJ_EVENT_AMBUSH3, BigIslandN_Movement_AmbusherExit3)
	applymovement (OBJ_EVENT_AMBUSH4, BigIslandN_Movement_AmbusherExit4)
	waitmovement (OBJ_EVENT_AMBUSH4)


    //Victors steal Philoctetes

	//Jason approaches player
	applymovement (OBJ_EVENT_JASON, Common_Movement_WalkDown)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	waitmovement (OBJ_EVENT_JASON)
	//Thanks for defending me
	msgbox (BigIslandN_Text_JasonPostAmbush, MSGBOX_DEFAULT)
	closemessage

	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_ExclamationMark)
	waitmovement (0)
	applymovement (OBJ_EVENT_JASON, Common_Movement_FaceRight)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	applymovement (OBJ_EVENT_PHILOCTETES, Common_Movement_FaceLeft)
	waitmovement (0)

	//Help me please I'm beaten
	msgbox (BigIslandN_Text_PhiloctetesAbducted, MSGBOX_DEFAULT)
	closemessage
	//Carried off
	applymovement (OBJ_EVENT_AMBUSH1, BigIslandN_Movement_AmbushPhiloctetesExit)
	applymovement (OBJ_EVENT_AMBUSH2, BigIslandN_Movement_AmbushPhiloctetesExit)
	applymovement (OBJ_EVENT_PHILOCTETES, BigIslandN_Movement_AmbushPhiloctetesExit)
	msgbox (format("NOOOOOOOO!"), MSGBOX_DEFAULT)
	waitmovement (OBJ_EVENT_AMBUSH2)
	closemessage

	applymovement (OBJ_EVENT_JASON, Common_Movement_FaceDown)
	waitmovement (0)
	//Leave him we must be about our mission
	msgbox (BigIslandN_Text_JasonLeaveHim, MSGBOX_DEFAULT)
	closemessage
	//Walk to cave entrance

	//Move Cultists to location
	removeobject (OBJ_EVENT_AMBUSH3)
	setobjectxyperm (OBJ_EVENT_AMBUSH3, 30, 37)
	setobjectmovementtype (OBJ_EVENT_AMBUSH3, MOVEMENT_TYPE_FACE_RIGHT)
	addobject (OBJ_EVENT_AMBUSH3)
	showobjectat (OBJ_EVENT_AMBUSH3, MAP_BIG_ISLAND_N)

	removeobject (OBJ_EVENT_AMBUSH4)
	setobjectxyperm (OBJ_EVENT_AMBUSH4, 31, 37)
	setobjectmovementtype (OBJ_EVENT_AMBUSH4, MOVEMENT_TYPE_FACE_LEFT)
	addobject (OBJ_EVENT_AMBUSH4)
	showobjectat (OBJ_EVENT_AMBUSH4, MAP_BIG_ISLAND_N)

	//Setup Jason spot
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_JasonSetupFollowAmbush)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
	waitmovement (OBJ_EVENT_JASON)
	//Walk together to the Cave
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_JasonFollowAmbush)
	applymovement (OBJ_EVENT_ID_PLAYER, BigIslandN_Movement_JasonFollowAmbush)
	waitmovement (0)

	applymovement (OBJ_EVENT_JASON, Common_Movement_ExclamationMark)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)

	//Grunts Open, enter, and seal cave (hint about Mountain Top?)

	//Hurry in before they catch up we need to warn Aeetes there are intruders on on the isle for Mareep
	msgbox (BigIslandN_Text_CultistsEnterCave, MSGBOX_DEFAULT)
	closemessage


	//Exeunt Cultists
	applymovement (OBJ_EVENT_AMBUSH3, Common_Movement_WalkRight)
	applymovement (OBJ_EVENT_AMBUSH4, Common_Movement_WalkUp)
	waitmovement (OBJ_EVENT_AMBUSH3)
	hideobjectat (OBJ_EVENT_AMBUSH4, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_AMBUSH4)

	applymovement (OBJ_EVENT_AMBUSH3, Common_Movement_WalkUp)
	waitmovement (OBJ_EVENT_AMBUSH3)
	hideobjectat (OBJ_EVENT_AMBUSH3, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_AMBUSH3)

	//Hide Cultists
	setobjectxyperm (OBJ_EVENT_AMBUSH3, 28, 33)
	setobjectmovementtype (OBJ_EVENT_AMBUSH3, MOVEMENT_TYPE_INVISIBLE)
	setobjectxyperm (OBJ_EVENT_AMBUSH4, 28, 33)
	setobjectmovementtype (OBJ_EVENT_AMBUSH4, MOVEMENT_TYPE_INVISIBLE)
	setobjectxyperm (OBJ_EVENT_AMBUSH1, 28, 33)
	setobjectmovementtype (OBJ_EVENT_AMBUSH1, MOVEMENT_TYPE_INVISIBLE)
	setobjectxyperm (OBJ_EVENT_AMBUSH2, 28, 33)
	setobjectmovementtype (OBJ_EVENT_AMBUSH2, MOVEMENT_TYPE_INVISIBLE)

	//Set vars for ShakeCamera
	setvar (VAR_0x8004, 1)
	setvar (VAR_0x8005, 1)
	setvar (VAR_0x8006, 16)
	setvar (VAR_0x8007, 3)

	//Rock Cave Entrance hides again
	special (ShakeCamera)
	waitstate
	playse (SE_EFFECTIVE)
	waitse
	call (BigIslandN_Script_HideColchisCave)
	special (DrawWholeMapView)
	delay (30)

	//You and Jason approach the cave wall
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
	applymovement (OBJ_EVENT_JASON, BigIslandN_Movement_JasonApproachColchisCave)
	waitmovement (0)
	applymovement (OBJ_EVENT_JASON, Common_Movement_ExclamationMark)
	waitmovement (0)
	
	setvar (VAR_ARGO_STATE, 3)

	call (BigIslandN_Script_JasonAtColchisCave)
	//Jason turns to you and says 'they've sealed it shut we must find a way inside

	hideobjectat (OBJ_EVENT_PHILOCTETES, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_PHILOCTETES)
	hideobjectat (OBJ_EVENT_JASON, MAP_BIG_ISLAND_N)
	removeobject (OBJ_EVENT_JASON)


	call (BigIslandN_Script_ShowArgo3)

	//New state of BigIsland: Jason at Colchis Cave, Phil abducted, C&P bored somewhere (hint), Orpheus new song, Theseus new girl 

	return
}	

script BigIslandN_Script_JasonAtColchisCave {
	//FLAG Here
	if (!flag(FLAG_BIGISLAND_N_JASONCOLCHIS_INTRO)) {
		setflag (FLAG_BIGISLAND_N_JASONCOLCHIS_INTRO)
		faceplayer
		msgbox (BigIslandN_Text_OpenColchisCave1, MSGBOX_DEFAULT)
		closemessage
		applymovement (OBJ_EVENT_JASON, Common_Movement_FaceUp)
		waitmovement (0)
	}
	msgbox (BigIslandN_Text_OpenColchisCave2, MSGBOX_DEFAULT)
	closemessage
	specialvar (VAR_RESULT, GetPlayerFacingDirection)
	if (var(VAR_RESULT) == DIR_EAST) {
		applymovement (OBJ_EVENT_JASON, Common_Movement_FaceLeft)
	} else {
		applymovement (OBJ_EVENT_JASON, Common_Movement_FaceDown)
	}
	waitmovement (0)
	msgbox (BigIslandN_Text_OpenColchisCave3, MSGBOX_DEFAULT)
	closemessage

	//Ready to release Spearow
	if (var(VAR_ARGO_STATE) == 6) {
		//First have Jason react to the news - if it's news
		if (!flag(FLAG_BIGISLAND_N_JASON_OSTRACON)) {
			setflag (FLAG_BIGISLAND_N_JASON_OSTRACON)
			applymovement (OBJ_EVENT_JASON, Common_Movement_ExclamationMark)
			waitmovement (0)
			msgbox (BigIslandN_Text_Ostracon, MSGBOX_DEFAULT)
			closemessage

		}
		specialvar (VAR_RESULT, IsSpearowInParty)
		if (var(VAR_RESULT) == TRUE) {
			msgbox (BigIslandN_Text_ReleaseSpearow, MSGBOX_YESNO)
			if (var(VAR_RESULT) == YES) {
				special (ChoosePartyMon)
				waitstate
				if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
					msgbox (BigIslandN_Text_DeclineReleaseSpearow, MSGBOX_DEFAULT)
					closemessage
				} else {
					specialvar (VAR_RESULT, ScriptGetPartyMonSpecies)
					if (var(VAR_RESULT) != SPECIES_SPEAROW) {
						msgbox (BigIslandN_Text_DeclineReleaseSpearow, MSGBOX_DEFAULT)
						closemessage
					} else {
						special (BufferMonNickname)
						msgbox (BigIslandN_Text_ReleasedSpearow, MSGBOX_DEFAULT)
						closemessage
						special (ReleaseChosenMon)
						call (BigIslandN_Script_EnterColchis)
					}
				}
			} else {
				msgbox (BigIslandN_Text_DeclineReleaseSpearow, MSGBOX_DEFAULT)
				closemessage
			}
		}
	}
	applymovement (OBJ_EVENT_JASON, Common_Movement_FaceUp)
	waitmovement (0)
	releaseall
	end
}

script BigIslandN_Script_EnterColchis {
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	setvar (VAR_0x8004, 1)  // vertical pan
	setvar (VAR_0x8005, 1)  // horizontal pan
	setvar (VAR_0x8006, 8)  // num shakes
	setvar (VAR_0x8007, 5)  // shake delay
	special (ShakeCamera)
	waitstate
	playse (SE_EFFECTIVE)
	call (BigIslandN_Script_ShowColchisCave)
	special (DrawWholeMapView)
	delay (30)
	setvar (VAR_ARGO_STATE, 7)
	waitse
	//Jason says you've done it hurry lets get the fleece
	applymovement (OBJ_EVENT_JASON, Common_Movement_ExclamationMark)
	waitmovement (0)
	msgbox (BigIslandN_Text_OpenColchisCave4, MSGBOX_DEFAULT)
	closemessage
	//Exits
	applymovement (OBJ_EVENT_JASON, Common_Movement_WalkUp)
	waitmovement (0)

	clearflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	releaseall
	call (BigIslandN_Script_ShowArgo7)
	
	//Player follows
	//Story continues in next map :/
	end
}

script BigIslandN_Script_TestNicknamer {
	special (ChoosePartyMon)
	if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
		msgbox ("DECLINE")
		closemessage
	} else {
		specialvar (VAR_RESULT, ScriptGetPartyMonSpecies)
		if (var(VAR_RESULT) == SPECIES_EGG) {
			msgbox ("CANT NAME EGG")
			closemessage
		} else {
			call (Common_EventScript_NameReceivedPartyMon)
		}
	}
	end
}

script BigIslandN_Script_Argo {
	lock
	msgbox (BigIslandN_Text_Argo, MSGBOX_DEFAULT)
	closemessage
	release
	end
}

//VAR = 2 They block acess to the ship and say something like "why'd we have to wait back here at the ship this sucks"
//VAR = 3-6 IE Jason at Colchis Cave entrance Brothers on the beach with some islander talking about how they're imbibing and heard a story about some cult that abducts people and sacrifices them to the mountain or they go missing or something old religion
script BigIslandN_Script_Dioscuri {
	lock
	faceplayer
	if (var(VAR_ARGO_STATE) == 2) {
		msgbox (BigIslandN_Text_Dioscuri1, MSGBOX_DEFAULT)
	} elif (var(VAR_ARGO_STATE) < 7 && var(VAR_ARGO_STATE) != 1) {
		msgbox (BigIslandN_Text_Dioscuri2, MSGBOX_DEFAULT)
	} elif (var(VAR_ARGO_STATE) == 7) {
		if (flag(FLAG_BIGISLANDN_DIOSCURIFOUND)) {
			clearflag (FLAG_BIGISLANDN_DIOSCURIFOUND)
			msgbox (BigIslandN_Text_Dioscuri3, MSGBOX_DEFAULT)
		} else {
			msgbox (BigIslandN_Text_Dioscuri1, MSGBOX_DEFAULT)
		}
	} else {
		msgbox (format("ERROR: BigIslandN_Script_Dioscuri VAR_ARGO_STATE index not accounted for. Please report bugs to CornixSenex"))
	}
	closemessage
	release
	end
}

script BigIslandN_Script_Orpheus {
	lock
	faceplayer
	switch (var(VAR_ARGO_STATE)) {
		case 0:
			msgbox (BigIslandN_Text_Orpheus0, MSGBOX_DEFAULT)
		case 1:
			msgbox (BigIslandN_Text_Orpheus1, MSGBOX_DEFAULT)
		case 2:
			msgbox (BigIslandN_Text_Orpheus2, MSGBOX_DEFAULT)
		case 3:
			msgbox (BigIslandN_Text_Orpheus3, MSGBOX_DEFAULT)
		case 4:
			msgbox (BigIslandN_Text_Orpheus4, MSGBOX_DEFAULT)
		case 5:
			msgbox (BigIslandN_Text_Orpheus5, MSGBOX_DEFAULT)
		case 6:
			msgbox (BigIslandN_Text_Orpheus6, MSGBOX_DEFAULT)
		case 7:
			msgbox (BigIslandN_Text_Orpheus7, MSGBOX_DEFAULT)
		default:
			msgbox (BigIslandN_Text_Orpheus8, MSGBOX_DEFAULT)
	}
	closemessage
	release
	end
}

script BigIslandN_Script_Theseus {
	lock
	faceplayer
	msgbox (BigIslandN_Text_Theseus, MSGBOX_DEFAULT)
	closemessage
	release
	end
}

movement BigIslandN_Movement_JasonDisembark {
	walk_right
	walk_down * 5
}

movement MareS5_Movement_PlayerDisembark {
	walk_right
	walk_down * 4
}

movement BigIslandN_Movement_OrpheusDisembark {
	walk_right * 2
	walk_down * 4
	walk_right * 8
}

movement BigIslandN_Movement_TheseusDisembark {
	walk_right * 2
	walk_down * 12
}

movement BigIslandN_Movement_PolluxDisembark {
	walk_right * 2
	walk_down 
}

movement BigIslandN_Movement_CastorDisembark {
	walk_right
	walk_down
}

movement BigIslandN_Movement_JasonExitArgo {
	walk_down * 6
}

movement BigIslandN_Movement_PhiloctetesDisembark {
	walk_right * 2
	walk_down * 5
}

movement BigIslandN_Movement_JasonExploreA1 {
	walk_left
	walk_down
}

movement BigIslandN_Movement_PhiloctetesExploreA1 {
	walk_down
}

movement BigIslandN_Movement_JasonExploreB1 {
	walk_left * 2
	walk_down
}

movement BigIslandN_Movement_AmbushAll {
	walk_left * 2
	walk_down * 3
	walk_left * 5
	walk_down * 2
}

movement BigIslandN_Movement_Ambusher1 {
	walk_up
	walk_left
}

movement BigIslandN_Movement_PlayerAmbush2 {
	walk_down
	walk_left 
}

movement BigIslandN_Movement_JasonAmbush2 {
	walk_up
	face_left
}

movement BigIslandN_Movement_AmbusherExit3 {
	walk_down * 4
	walk_left
	walk_down * 2
}

movement BigIslandN_Movement_AmbusherExit4 {
	walk_down * 6
	walk_left
	walk_down * 2
}

movement BigIslandN_Movement_PhiloctetesAmbush2 {
	face_up
	face_down
	face_right
}

movement BigIslandN_Movement_AmbushPhiloctetesExit {
	walk_up * 3
	walk_right * 4
	walk_up * 4
}

movement BigIslandN_Movement_JasonSetupFollowAmbush {
	walk_left
	walk_down
}

movement BigIslandN_Movement_JasonFollowAmbush {
	walk_down * 4
	walk_left * 3
	walk_down
	walk_left * 7
	walk_up * 3
}

movement BigIslandN_Movement_JasonApproachColchisCave {
	walk_up * 2
	walk_right
	face_up
}

movement BigIslandN_Movement_PlayerExitColchis {
	walk_down
}

movement BigIslandN_Movement_ExeuntArgo1 {
	walk_down * 3
}

movement BigIslandN_Movement_ExeuntArgo2 {
	walk_right * 6
	walk_up
	walk_right * 3
	walk_up * 7
	walk_right * 6
	walk_up * 3
	walk_right * 6
	walk_up * 2
	walk_right * 28
	walk_up * 6
}

movement BigIslandN_Movement_JasonMedeaExeunt {
	walk_up * 2
}

movement BigIslandN_Movement_DioscuriExeunt {
	walk_up * 4
}

movement BigIslandN_Movement_TheseusExeunt {
	walk_right
	walk_up * 4
}

movement BigIslandN_Movement_OrpheusExeunt {
	walk_left
	walk_up * 4
}

text BigIslandN_Text_ArrivedAtColchis1 {
	format("We've landed on the north side of the big island we call Ignis. According to this map Colchis is located within a cave on the west side of the island.")
}

text BigIslandN_Text_ArrivedAtColchis2 {
	format("Orpheus, search for food and fresh water.")
}

text BigIslandN_Text_ArrivedAtColchis3 {
	format("And Theseus inquire with the town whether the locals will give us shelter and open their markets to Argonauts.")
}

text BigIslandN_Text_ArrivedAtColchis4 {
	format("Castor and Pollux you will guard the ship while {PLAYER} and I and Philoctetes go investigate the rumored Colchis cave.")
}

text BigIslandN_Text_ArrivedAtColchis5 {
	format("Meet Philoctetes and I in the woods when you're ready to find the Colchis cave.")
}

text BigIslandN_Text_Philoctetes {
	format("We've finally arrived on the island! Speak to Jason and let's go find Colchis.")
}

text BigIslandN_Text_Jason {
	format("Let us set off at once to find Colchis.")
}

text BigIslandN_Text_Ambush {
	format("On guard friends! Something wicked heads our way.")
}

text BigIslandN_Text_Ambush2 {
	format("To arms! Defend the party! Now recall the sacred oaths you swore. Now to battle!")
}

text BigIslandN_Text_ColchisGrunt1Pre {
	format("Illkay ethay invadersay anday otectpray areeppmay")
}

text BigIslandN_Text_ColchisGrunt1Lost {
	format("Ymay ifelay isay orfeitfay. Iay eavelay inay efeatday")
}

text BigIslandN_Text_ColchisGrunt1Post {
	format("Eway eednay otay arnway Aeetesay ofay isthat otplay otay ealstay Areepmay. Urryhay ackbay otay ethay avecay")
}

text BigIslandN_Text_PhiloctetesAbducted {
	format("Please help me friends! I'm lost! They're taking me! Assemble Argonauts! Jason! Please Help!")
}

text BigIslandN_Text_JasonLeaveHim {
	format("Leave him. He swore an oath that he would die in service of our quest to find Mareep. We must find the location of Colchis.")
}

text BigIslandN_Text_JasonPostAmbush {
	format("Well done your bravery has saved our quest.")
}

//Quickly! Hurry! We must warn Aeetes that ruffians are here to steal Mareep. Enter the cave, and seal it behind you!
text BigIslandN_Text_CultistsEnterCave {
	format("Uicklyquay! Urryhay! Eway ustmay arnway Aeetesay atthay uffiansray areay erehay otay ealstay Areepmay. Enteray ethay avecay anday ealsay itay ehindbay ouyay.")
}

text BigIslandN_Text_OpenColchisCave1 {
	format("This must be where Colchis Cave is hidden. The only question now is how do we open the secret door...")
}

text BigIslandN_Text_OpenColchisCave2 {
	format("OPEN SAYS ME!")
}
		
text BigIslandN_Text_OpenColchisCave3 {
	format("That didn't work, but what else could we try...")
}
//Whats that
//That Phil was taken by a witch and gone mad
//And a mysterious stone prophecy and magic bird
//By SPEAROW free? What on earth could that mean?
//Sounds like a bunch of nonesense

//Whats that? Philoctetes enchanted by
//an evil witch and a volcanic bird?
//An old prophet gave you an Ostracon
//By Spearow free? What nonesense is all this?
//We need to work on opening this cave

text BigIslandN_Text_Ostracon {
	format("What's that? Philoctetes enchanted by an evil witch?\NAnd a volcanic bird!\NAn old prophet gave you an Ostracon?\NBy SPEAROW free!\NWhat nonesense is all this? We need to work on opening this cave.")
}

text BigIslandN_Text_ReleaseSpearow {
	format("Will you release SPEAROW?")
}

text BigIslandN_Text_DeclineReleaseSpearow {
	format("Best not to release SPEAROW here and now.")
}

text BigIslandN_Text_OpenColchisCave4 {
	format("My goodness lad! What kind of magic is this where SPEAROW flies and cave doors open? Let's not waste any time. Now follow me!")
}

text BigIslandN_Text_ReleasedSpearow {
	format("{STR_VAR_1} was released!\pBye Bye {STR_VAR_1}!")
}

text BigIslandN_Text_Argo {
	format("The mighty ship of Jason:\nTHE ARGO")
}

text BigIslandN_Text_Orpheus0 {
	format("ARGO VAR STATE is 0 you should never read this. Please report bugs to CornixSenex")
}

text BigIslandN_Text_Orpheus1 {
	format("ARGO VAR STATE is 1 you should never read this. Please report bugs to CornixSenex")
}

text BigIslandN_Text_Orpheus2 {
	format("On distant shores, where sea and sorrow meet, I stand, a wanderer, with heart forlorn and beat. My quest, lost to the waves' relentless roar, and with it, all my hopes, forever on the shore.")
}

text BigIslandN_Text_Orpheus3 {
	format("The sun dips low, in fiery splendor set, amidst the waves, that gently lap and fret. The salty scent, of ocean's darkling breast, invades my senses, and my soul finds rest.")
}

text BigIslandN_Text_Orpheus4 {
	format("The cries of gulls, that wheel and soar above, echo my sighs, and share my endless love, for thee, O Ocean, vast and wild and free, that holds my dreams, in thy dark secrecy.")
}

text BigIslandN_Text_Orpheus5 {
	format("The breeze, that whispers secrets in my ear, of distant lands, and memories so dear, stirs up the depths, of sorrow and despair, and I, a shipwrecked soul, am left to stare.")
}

text BigIslandN_Text_Orpheus6 {
	format("The tide, that ebbs and flows, with soothing sound, lulls me to sleep, on this forsaken ground, yet, in my dreams, I see the shores I've left, and wake to find, my heart still bereft.")
}

text BigIslandN_Text_Orpheus7 {
	format("O, cruel fate, that brought me to this place! A poet, lost, without a friendly face. My lyre, silenced, and my voice, unheard. A castaway, with none to share my word.")
}

text BigIslandN_Text_Orpheus8 {
	format("The ocean's vastness stretches far and wide. A limitless expanse, where I am lost inside. No hope, no rescue, comes to set me free. A prisoner, of the waves' eternity.")
}

text BigIslandN_Text_Dioscuri1 {
	format("I can't believe we're stuck on guard duty!\NIt's so boring! How long have they been gone?")
}

text BigIslandN_Text_Dioscuri2 {
	format("We made friends with a couple islanders who tell us that people have been going missing recently and the rumour is they're being kidnapped by a secret cult and sacrificed to the volcano God.\NOf course it all sounds like nonesense to us.")
}

text BigIslandN_Text_Dioscuri3 {
	format("Hey {PLAYER} don't tell Jason that we snuck off.")
}

text BigIslandN_Text_Theseus {
	format("This island's home to beautiful women!")
}

//The way to Colchis is closed forever.
text BigIslandN_Text_MedeaExitColchis {
	format("Ethay ayway otay Olchiscay isay osedclay oreverfay.")
}

text BigIslandN_Text_JasonExitColchis {
	format("Let us depart this place for our new life.")
}

//Our quest is won and we are going home
//Remember to lost homies
//and an introduction to the new homie Medea
//A special thanks to PLAYER who helped
//Three cheers
//Now let us be back home
text BigIslandN_Text_JasonExeunt1 {
	format("Attention Argonauts, our quest is done. Shiny Mareep, and victory, is ours! Though grivously we lost great Heracles and Philoctetes too shall not return, that which we set out for has been achieved. A special thanks to {PLAYER} and Medea without who our objective would have failed. Three cheers from all for our new companions.")
}

text BigIslandN_Text_ThreeCheers {
	format("Hip hip horray!\pHip hip hooray!\pHip hip hooray!")
}

text BigIslandN_Text_JasonExeunt2 {
	format("Now let us return home, triumphantly.")
}

text BigIslandN_Text_JasonExeunt3 {
	format("Visit us sometime soon in Bulbus Town.")
}

