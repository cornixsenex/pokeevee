
const OBJ_EVENT_TEACHER = 1

mapscripts Ardor_Uni_LogicClass_MapScripts {

	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_LOGIC_CLASS_STATE, 1: Ardor_Uni_LogicClass_EventScript_FacePlayerLeft
		VAR_LOGIC_CLASS_STATE, 2: Ardor_Uni_LogicClass_EventScript_FacePlayerRight
	]

	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_LOGIC_CLASS_STATE, 1: Ardor_Uni_LogicClass_EventScript_Test
		VAR_LOGIC_CLASS_STATE, 2: Ardor_Uni_LogicClass_EventScript_Test
	]

}

script Ardor_Uni_LogicClass_EventScript_FacePlayerLeft {
	turnobject (OBJ_EVENT_ID_PLAYER, DIR_WEST)
	turnobject (OBJ_EVENT_TEACHER, DIR_EAST)
	end
}

script Ardor_Uni_LogicClass_EventScript_FacePlayerRight {
	turnobject (OBJ_EVENT_ID_PLAYER, DIR_EAST)
	turnobject (OBJ_EVENT_TEACHER, DIR_WEST)
	end
}

script Ardor_Uni_LogicClass_EventScript_Teacher {
	lock
	faceplayer
	if (defeated(TRAINER_UNI_LOGIC)) {
		msgbox(Ardor_Uni_LogicClass_Text_ClassFinished)
		closemessage
	} else {
		msgbox(Ardor_Uni_LogicClass_Text_BeginLogic, MSGBOX_YESNO)
		if (var(VAR_RESULT) == TRUE) {
			call (Ardor_Uni_LogicClass_EventScript_DoLogic)
		}
		msgbox(Ardor_Uni_LogicClass_Text_ReturnSoon)
		closemessage
	}
	applymovement (OBJ_EVENT_TEACHER, Common_Movement_FaceDown)
	waitmovement (0)
	release
	end
}

script Ardor_Uni_LogicClass_EventScript_DoLogic {
	
	specialvar (VAR_RESULT, GetPlayerFacingDirection)
	if (var(VAR_RESULT) == DIR_EAST) {
		setflag(FLAG_RETURN_LOGIC_PUZZLE_LEFT)
	} 
	warpsilent (MAP_LOGIC_PUZZLE, 8, 12)
	waitstate
	end
}

script Ardor_Uni_LogicClass_EventScript_Test {
	msgbox(Ardor_Uni_Text_TestIntro)
	closemessage
	trainerbattle_no_intro(TRAINER_UNI_LOGIC, Ardor_Uni_LogicClass_Text_TeacherBattleLost)
	call (Ardor_Uni_TermState_Incrementvar)
	msgbox(Ardor_Uni_LogicClass_Text_ClassFinished)
	closemessage
	return
}

text Ardor_Uni_LogicClass_Text_BeginLogic {
	format("Salve puer. Magistra Gina sum. Necesse est pingere. Praestasne pingere?")
}

text Ardor_Uni_LogicClass_Text_ReturnSoon {
	format("Reveni cum amore artis.")
}

text Ardor_Uni_LogicClass_Text_ClassFinished {
	format("Congratulatio! Iam studuisti artem.")
}

text Ardor_Uni_LogicClass_Text_TeacherBattleLost {
	format("Pictor es.")
}


