
//FLAG_TEMP_2 disables object event scripts (set during U in bed with C)

mapscripts CasaCirces_MapScripts {


    MAP_SCRIPT_ON_TRANSITION {
        //Ulysses Confront Circe scene
        if (var(VAR_ULYSSES_STATE) == 7) {
            call (CasaCirces_Script_ShowUlyssesConfrontCirce)
        }
        //Ulysses in bed w/ Circe
        if (var(VAR_ULYSSES_STATE) == 8) {
            call (CasaCirces_Script_ShowUlyssesWithCirce)
        }
        //Player confront Circe - U a pig
        if (var(VAR_ULYSSES_STATE) == 9) {
            call (CasaCirces_Script_ShowPlayerConfrontCirce)
        }
        //Circe defeated - U still a pig
        if (var(VAR_ULYSSES_STATE) == 10) {
            call (CasaCirces_Script_ShowUlyssesAfterCirce)
        }
        //default is show nobody which is after circe scene
    }

    MAP_SCRIPT_ON_RESUME {
        if (flag(FLAG_SYS_CTRL_OBJ_DELETE)) {
            hideobjectat (LOCALID_CASACIRCES_CIRCE, MAP_CASA_CIRCES)
            removeobject (LOCALID_CASACIRCES_CIRCE)
            hideobjectat (LOCALID_CASACIRCES_MISMAGIUS, MAP_CASA_CIRCES)
            removeobject (LOCALID_CASACIRCES_MISMAGIUS)
        }
    }


    MAP_SCRIPT_ON_FRAME_TABLE [
        //U confront C
        VAR_ULYSSES_STATE, 7: CasaCirces_Script_UlyssesConfrontCirce
        //P confront C
        VAR_ULYSSES_STATE, 9: CasaCirces_Script_PlayerConfrontCirce
    ]

}

script CasaCirces_Script_ShowUlyssesConfrontCirce {
    //C
    setobjectxyperm (LOCALID_CASACIRCES_CIRCE, 4, 2)
    setobjectmovementtype (LOCALID_CASACIRCES_CIRCE, MOVEMENT_TYPE_FACE_DOWN)
    //M
    setobjectxyperm (LOCALID_CASACIRCES_MISMAGIUS, 5, 2)
    setobjectmovementtype (LOCALID_CASACIRCES_MISMAGIUS, MOVEMENT_TYPE_WALK_IN_PLACE_DOWN)
    //U
    setobjectxyperm (LOCALID_CASACIRCES_ULYSSES, 4, 4)
    setobjectmovementtype (LOCALID_CASACIRCES_ULYSSES, MOVEMENT_TYPE_FACE_UP)
    return
}

script CasaCirces_Script_ShowUlyssesWithCirce {
    //disable their object scripts
    setflag (FLAG_TEMP_2)
    //C
    setobjectxyperm (LOCALID_CASACIRCES_CIRCE, 6, 3)
    setobjectmovementtype (LOCALID_CASACIRCES_CIRCE, MOVEMENT_TYPE_FACE_DOWN)
    //M
    setobjectxyperm (LOCALID_CASACIRCES_MISMAGIUS, 7, 3)
    setobjectmovementtype (LOCALID_CASACIRCES_MISMAGIUS, MOVEMENT_TYPE_WALK_IN_PLACE_DOWN)
    //U
    setobjectxyperm (LOCALID_CASACIRCES_ULYSSES, 6, 3)
    setobjectmovementtype (LOCALID_CASACIRCES_ULYSSES, MOVEMENT_TYPE_WALK_IN_PLACE_UP)
    return
}

script CasaCirces_Script_ShowPlayerConfrontCirce {
    //C
    setobjectxyperm (LOCALID_CASACIRCES_CIRCE, 4, 2)
    setobjectmovementtype (LOCALID_CASACIRCES_CIRCE, MOVEMENT_TYPE_FACE_DOWN)
    //M
    setobjectxyperm (LOCALID_CASACIRCES_MISMAGIUS, 5, 2)
    setobjectmovementtype (LOCALID_CASACIRCES_MISMAGIUS, MOVEMENT_TYPE_WALK_IN_PLACE_DOWN)
    //T
    setobjectxyperm (LOCALID_CASACIRCES_TEDDIURSA, 4, 2)
    setobjectmovementtype (LOCALID_CASACIRCES_TEDDIURSA, MOVEMENT_TYPE_LOOK_AROUND)
    return
}

script CasaCirces_Script_ShowUlyssesAfterCirce {
    //T
    setobjectxyperm (LOCALID_CASACIRCES_TEDDIURSA, 4, 2)
    setobjectmovementtype (LOCALID_CASACIRCES_TEDDIURSA, MOVEMENT_TYPE_LOOK_AROUND)
    return
}

//Event Scripts

script CasaCirces_Script_Ulysses {
    if (!flag(FLAG_TEMP_2)) {
        faceplayer
        load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
        msgbox (CasaCirces_Text_Ulysses_AfterConfrontCirce)
        destroy_field_pic (FIELD_PIC_ULYSSES)
        closemessage
        unfaceplayer
    }
}

script CasaCirces_Script_Circe {
    if (!flag(FLAG_TEMP_2)) {
        faceplayer
        load_field_pic (FIELD_PIC_CIRCE, 190, 98)
        msgbox (CasaCirces_Text_Circe_AfterConfrontUlysses)
        destroy_field_pic (FIELD_PIC_CIRCE)
        closemessage
        unfaceplayer
    }
}

script CasaCirces_Script_Mismagius {
    if (!flag(FLAG_TEMP_2)) {
        faceplayer
        playmoncry (SPECIES_MISMAGIUS, CRY_MODE_ENCOUNTER)
        waitmoncry 
        unfaceplayer
    }
}

script CasaCirces_Script_Teddiursa {
    faceplayer
    applymovement (LOCALID_CASACIRCES_TEDDIURSA, Common_Movement_ExclamationMark)
    waitmovement (0)
    playmoncry (SPECIES_MISMAGIUS, CRY_MODE_ENCOUNTER)
    waitmoncry 
    bufferspeciesname (STR_VAR_1, SPECIES_TEDDIURSA)
    msgbox (MareS6_Text_MeetsGaze)
    //No need to check you can only interact with this object after you have the fruit
    msgbox (MareS6_Text_UsePinaFruit, MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO) {
        closemessage
        unfaceplayer
        end
    } else {
        //Restore Ulysses scene
        msgbox (MareS6_Text_PlayerGaveMoly)

    }
}
//Scene Scripts

script CasaCirces_Script_UlyssesConfrontCirce {
    //player up a bit
    applymovement (LOCALID_PLAYER, Common_Movement_WalkUp)
    waitmovement (0)
    //alert P
    applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement (0)
    //U: So, you're the witch
    load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
    msgbox (CasaCirces_Text_Ulysses_ConfrontCirce1)
    destroy_field_pic (FIELD_PIC_ULYSSES)
    //C: Here I am? Who is this handsome gentleman caller?
    load_field_pic (FIELD_PIC_CIRCE, 190, 98)
    msgbox (CasaCirces_Text_Circe_ConfrontUlysses1)
    destroy_field_pic (FIELD_PIC_CIRCE)
    //U and P up to C
	applymovement (LOCALID_CASACIRCES_ULYSSES, Common_Movement_WalkUp)
	applymovement (LOCALID_PLAYER, Common_Movement_WalkUp)
	waitmovement (0)
    //U: We're here to stop you.Don't try and run
	load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
    msgbox (CasaCirces_Text_Ulysses_ConfrontCirce2)
    destroy_field_pic (FIELD_PIC_ULYSSES)
    //C: I wouldn't dream of it. Hard to get was never my style. I'm circe, I prefer physician, homemaker, and lover. Now tell me your name handsome stranger.
    load_field_pic (FIELD_PIC_CIRCE, 190, 98)
    msgbox (CasaCirces_Text_Circe_ConfrontUlysses2)
    destroy_field_pic (FIELD_PIC_CIRCE)
    //U: Im ulysses, i'm here to set free the men you've captured
	load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
    msgbox (CasaCirces_Text_Ulysses_ConfrontCirce3)
    destroy_field_pic (FIELD_PIC_ULYSSES)
    //C: Those men rather held me than I held them. But alas I cannot save them. They are beyond my powers to return. Only the Moly (Purple Pinea?) fruit, can bring them back.
    load_field_pic (FIELD_PIC_CIRCE, 190, 98)
    msgbox (CasaCirces_Text_Circe_ConfrontUlysses3)
    destroy_field_pic (FIELD_PIC_CIRCE)
    //U: Then bring out the fruit, if you're a doctor as you say and save them
	load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
    msgbox (CasaCirces_Text_Ulysses_ConfrontCirce4)
    destroy_field_pic (FIELD_PIC_ULYSSES)
    //C: Alas I cannot. The fruit is a rarity, only growing on our trees from time to time. Perhaps your companion should go out and look for it while you stay here with me. Wouldn't want to leafve me unnatended who knows what kind of mischief I could get up to.
    load_field_pic (FIELD_PIC_CIRCE, 190, 98)
    msgbox (CasaCirces_Text_Circe_ConfrontUlysses4)
    destroy_field_pic (FIELD_PIC_CIRCE)
    //U turn to player
	applymovement (LOCALID_CASACIRCES_ULYSSES, Common_Movement_FaceDown)
	waitmovement (0)
    //U: She's right, too dangerous to be left alone. Go search for this fruit I will keep hold of the witch
	load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
    msgbox (CasaCirces_Text_Ulysses_ConfrontCirce5)
    //U turn to C
	applymovement (LOCALID_CASACIRCES_ULYSSES, Common_Movement_FaceUp)
	waitmovement (0)
    //U: So be it. I will stay with you, but only to keep watch while we rescue the men
    msgbox (CasaCirces_Text_Ulysses_ConfrontCirce6)
	destroy_field_pic (FIELD_PIC_ULYSSES)
	closemessage
    //U up to position (face down left of C)
	applymovement (LOCALID_CASACIRCES_ULYSSES, Common_Movement_WalkUp)
	waitmovement (0)
	applymovement (LOCALID_CASACIRCES_ULYSSES, Common_Movement_WalkRight)
	waitmovement (0)
	applymovement (LOCALID_CASACIRCES_ULYSSES, Common_Movement_WalkUp)
	waitmovement (0)
	applymovement (LOCALID_CASACIRCES_ULYSSES, Common_Movement_FaceDown)
	waitmovement (0)
    //setvars
    setvar (VAR_ULYSSES_STATE, 8)
    //fin
    end
}

script CasaCirces_Script_PlayerConfrontCirce {
    //player up a bit
    applymovement (LOCALID_PLAYER, Common_Movement_WalkUp2)
    waitmovement (0)
    //alert player
    applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement (0)
    //C: You're too late
    load_field_pic (FIELD_PIC_CIRCE, 190, 98)
    msgbox (CasaCirces_Text_Circe_ConfrontPlayer1)
    destroy_field_pic (FIELD_PIC_CIRCE)
    //U / T mon sound
    load_field_pic (FIELD_PIC_TEDDIURSA, 190, 98)
    playmoncry (SPECIES_TEDDIURSA, CRY_MODE_ENCOUNTER)
    waitmoncry
    destroy_field_pic (FIELD_PIC_TEDDIURSA)
    //player to C
    applymovement (LOCALID_PLAYER, Common_Movement_WalkUp2)
    waitmovement (0)
    //Now I'll make you mine just like him
    load_field_pic (FIELD_PIC_CIRCE, 190, 98)
    msgbox (CasaCirces_Text_Circe_ConfrontPlayer2)
    destroy_field_pic (FIELD_PIC_CIRCE)
    //battle -> melting
    setflag (FLAG_SYS_CTRL_OBJ_DELETE) //sys flag
	trainerbattle_no_intro (TRAINER_CIRCE, CasaCirces_Text_CirceLost)
    //hide C on return from battle
    clearflag (FLAG_SYS_CTRL_OBJ_DELETE) //sys flag
    removeobject (LOCALID_CASACIRCES_CIRCE)
    setobjectxyperm (LOCALID_CASACIRCES_CIRCE, 0, 0)
    setobjectmovementtype (LOCALID_CASACIRCES_CIRCE, MOVEMENT_TYPE_INVISIBLE)
    addobject (LOCALID_CASACIRCES_CIRCE)
    removeobject (LOCALID_CASACIRCES_MISMAGIUS)
    removeobject (LOCALID_CASACIRCES_CIRCE)
    setobjectxyperm (LOCALID_CASACIRCES_MISMAGIUS, 0, 0)
    setobjectmovementtype (LOCALID_CASACIRCES_MISMAGIUS, MOVEMENT_TYPE_INVISIBLE)
    addobject (LOCALID_CASACIRCES_MISMAGIUS)
    //setvars
    setvar (VAR_ULYSSES_STATE, 10)
    //fin

}

text CasaCirces_Text_Ulysses_ConfrontCirce1 {
    format("Behold! The wicked witch of the forest!
            We've got you cornered now!")
}

text CasaCirces_Text_Circe_ConfrontUlysses1 {
    format("Hi! Here I am!
            You've really got me now, but I wonder,
            who is this handsome man who will take me?")
}

text CasaCirces_Text_Ulysses_ConfrontCirce2 {
	format("Stay back you naughty witch, your time has come.
			I'm here to punish you and put an end
			to your evil magic! Don't try and run!")
}

text CasaCirces_Text_Circe_ConfrontUlysses2 {
	format("Don't try and run? I'd never dream of it!
			Playing hard to get is not my style.
			I am Circe, the lady of the woods.
			Naughty witch is such a nasty title,
			I prefer physician, or beloved.
			Now tell me, for I am eager to learn,
			the name of my new gentleman caller.")
}

text CasaCirces_Text_Ulysses_ConfrontCirce3 {
	format("I am Ulysses, lord of Ithaca,
			and I've come to save the men you've bewitched
			on behalf of the people of the town.")
}

text CasaCirces_Text_Circe_ConfrontUlysses3 {
	format("Welcome, Lord Ulysses, to abode.
			Those men, I think, bewitched and conquered me
			more than I bewitched or ill-treated them
			but still I empathize with your concern.
			I can assure you they are all unharmed
			and close at hand. Alas they can't return,
			their restoration is beyond my skill
			alone. Only the noble Piña fruit
			can restore them to their previous forms.")
}

text CasaCirces_Text_Ulysses_ConfrontCirce4 {
	format("Enough waiting, woman, give me the fruit!")
}

text CasaCirces_Text_Circe_ConfrontUlysses4 {
	format("The Piña fruit, native to our island,
			is very rare. The Piña tree only
			flowers once in a generation we
			would need to search the entire island,
			and check every tree, and still the odds
			of finding just one fruit would be minute.
			Perhaps your companion has sharper eyes
			and better luck than we mature adults.
			Let him go out and seach the island for
			the piña fruit. Let's you and me stay here,
			you wouldn't want to leave me all alone,
			and unattended. Who knows what kind of
			mischief I might get up to by myself...
			Best stay here and make mischief together...")
}

text CasaCirces_Text_Ulysses_ConfrontCirce5 {
	format("I think, for now, we'll have to trust the witch.
			Don't worry, I think I can hold my own
			if she tries any tricks I'll pin her down
			and give her punishment for naughtiness...
			You better go start searching the island
			for the rare Piña fruit and remember
			the fate of those lost men lies in your hands.
			So, hurry up! Leave me and her alone.")
}

text CasaCirces_Text_Ulysses_ConfrontCirce6 {
	format("So be it, witch, I'll keep ahold of you
			while my companion, {PLAYER}, fetches the fruit
			you need to return the men to their homes.")
}

text CasaCirces_Text_Ulysses_AfterConfrontCirce {
	format("Let me handle Circe alone, you go
			search the island and find the Piña fruit.")
}

text CasaCirces_Text_Circe_AfterConfrontUlysses {
	format("The Piña tree only rarely bears fruit.
			You'd better hurry up and start your search
			if you have any hopes of finding some.")
}

text CasaCirces_Text_Circe_ConfrontPlayer1 {
    format("You're too late.")
}

text CasaCirces_Text_Circe_ConfrontPlayer2 {
    format("Your friend is beyond your help,
            but don't you worry. You'll be with him soon.")
}

text CasaCirces_Text_CirceLost {
    format("Look what you've done! I'm melting! I'm melting!")
}

