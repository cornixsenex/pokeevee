mapscripts Ardor_Uni_ArchaeologyClass_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		call (Ardor_Uni_ArchaeologyClass_OnTransition)
	}
}

script Ardor_Uni_ArchaeologyClass_OnTransition {
	if (var(VAR_ARDOR_UNI) >= 3) {
		call (Ardor_Uni_ArchaeologyClass_EventScript_RemoveStudents)
	} else {
		call (Ardor_Uni_ArchaeologyClass_EventScript_RemoveRebels)
	}
	end
}

script Ardor_Uni_ArchaeologyClass_EventScript_RemoveStudents {
	setflag (FLAG_ARCHAELOGY_CLASS_HIDESTUDENTS)
	removeobject (1)
	removeobject (2)
	removeobject (3)
	removeobject (4)
	removeobject (5)
	removeobject (6)
	removeobject (7)
	removeobject (8)
	removeobject (9)
	return
}	

script Ardor_Uni_ArchaeologyClass_EventScript_RemoveRebels {
	setflag (FLAG_ARCHAELOGY_CLASS_HIDEREBELS)
	removeobject (11)
	removeobject (12)
	return
}

script Ardor_Uni_ArchaeologyClass_EventScript_AddRebels {
	setvar (VAR_ARDOR_UNI, 3)
	clearflag (FLAG_ARCHAELOGY_CLASS_HIDEREBELS)
	addobject (11)
	showobjectat (11, MAP_ARDOR_UNI_ARCHAEOLOGY_CLASS)
	addobject (12)
	showobjectat (12, MAP_ARDOR_UNI_ARCHAEOLOGY_CLASS)
	return
}
	

script Ardor_Uni_ArchaeologyClass_EventScript_LeafTrigger {
	lockall
	call (Ardor_Uni_ArchaeologyClass_EventScript_Cutscene)
	releaseall
	end
}


script Ardor_Uni_ArchaeologyClass_EventScript_Cutscene {	
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
	//Sit down now class is starting soon
	load_field_pic (8, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Indy1)
	destroy_field_pic (8)
	closemessage
	//You don't understand my father sent us to enlist your help Mew has been captured we know you're a fighter
	load_field_pic (4, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Leaf1)
	closemessage
	destroy_field_pic (4)
	//Have Indy say something harsh to try and chill her out
	load_field_pic (8, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Indy2)
	destroy_field_pic (8)
	closemessage

	//Leaf "Gen Kenobi" speach ala leia
	load_field_pic (4, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Leaf2)
	closemessage
	destroy_field_pic (4)
	//have one or more of the students rush up on yall
	applymovement (2, Common_Movement_ExclamationMark)
	waitmovement (0)
	applymovement (2, Ardor_Uni_ArchaeologyClass_Movement_StudentApproach1)
	waitmovement (0)
	//She knows not what she says! She lies Teacher 
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Student1)
	applymovement (4, Common_Movement_ExclamationMark)
	waitmovement (0)
	applymovement (4, Ardor_Uni_ArchaeologyClass_Movement_StudentApproach2)
	waitmovement (0)
	//Just say the word and we'll take care of her
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Student2)
	//have indy dismiss them and announce that the time has come. Show yourselves
	//No both of you sit down
	load_field_pic (8, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Indy3)
	destroy_field_pic (8)
	closemessage
	applymovement (2, Ardor_Uni_ArchaeologyClass_Movement_StudentReturn)
	applymovement (4, Ardor_Uni_ArchaeologyClass_Movement_StudentReturn)
	waitmovement (0)
	//The time has come we must reveal ourselves and spring into action to change the world
	load_field_pic (8, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Indy4)
	destroy_field_pic (8)
	closemessage

	//transformation into magma team
	call (Ardor_Uni_ArchaeologyClass_EventScript_RebelReveal)
	//I had a strange feeling while battling you sneed I had secretly hoped to bring you into our group. This is proof that fate is on our side. 
	load_field_pic (9, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Indy5)
	destroy_field_pic (9)
	closemessage

	//Leaf blurts out "So you'll help us then!"
	load_field_pic (4, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Leaf3)
	closemessage
	destroy_field_pic (4)

	load_field_pic (9, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Indy6)
	destroy_field_pic (9)
	closemessage

	load_field_pic (4, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Leaf4)
	closemessage
	destroy_field_pic (4)

	applymovement (1, Common_Movement_ExclamationMark)
	waitmovement (0)

	load_field_pic (9, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Indy7)
	closemessage
	destroy_field_pic (9)

	load_field_pic (4, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Leaf5)
	closemessage
	destroy_field_pic (4)

	load_field_pic (9, 190, 98)
	msgbox (Ardor_Uni_ArchaeologyClass_Text_Indy8)
	closemessage
	destroy_field_pic (9)

	return
	//when you speak with him it checks if you have flash and if so you get a promtp (wanna start?) If yes he tells you to meet him at the Dark Cave entrance in the desert and exits
}

script Ardor_Uni_ArchaeologyClass_EventScript_RebelReveal {
	applymovement (2, Ardor_Uni_ArchaeologyClass_Movement_Spin)
	applymovement (1, Ardor_Uni_ArchaeologyClass_Movement_Spin)
	waitmovement (0)
	call (Ardor_Uni_ArchaeologyClass_EventScript_RemoveStudents)
	call (Ardor_Uni_ArchaeologyClass_EventScript_AddRebels)
	applymovement (12, Ardor_Uni_ArchaeologyClass_Movement_Spin)
	applymovement (11, Ardor_Uni_ArchaeologyClass_Movement_Spin)
	waitmovement (0)
	applymovement (12, Common_Movement_FaceDown)
	return 
}
	

movement Ardor_Uni_ArchaeologyClass_Movement_Spin {
	face_left
	face_up
	face_right
	face_down
	face_left
	face_up
	face_down
	face_left
	face_up
}

movement Ardor_Uni_ArchaeologyClass_Movement_StudentApproach1 {
	walk_right
	walk_up * 2
	face_right
}

movement Ardor_Uni_ArchaeologyClass_Movement_StudentApproach2 {
	walk_diag_northeast
	walk_up
	face_left
}

movement Ardor_Uni_ArchaeologyClass_Movement_StudentReturn {
	walk_down * 2
	walk_left
	face_up
}

text Ardor_Uni_ArchaeologyClass_Text_Indy1 {
	format("Now please sit down so our class may begin")
}

text Ardor_Uni_ArchaeologyClass_Text_Leaf1 {
	format("I've just told you, we're not here for the class. We need your help to...")
}

text Ardor_Uni_ArchaeologyClass_Text_Indy2 {
	format("Now is not the time! My patience is grown thin. This is a school! If you will not obey I'll get the dean.")
}

text Ardor_Uni_ArchaeologyClass_Text_Leaf2 {
	format("I'll not sit down, we need you general. Some years ago you fought with my father and now he begs your help to fight again. The Rockets have...")
}

text Ardor_Uni_ArchaeologyClass_Text_Student1 {
	format("She knows not what she says!")
}

text Ardor_Uni_ArchaeologyClass_Text_Student2 {
	format ("Just say the word and we'll take care of her!")
}

text Ardor_Uni_ArchaeologyClass_Text_Indy3 {
	format("Be quiet and both of you go sit down.")
}

text Ardor_Uni_ArchaeologyClass_Text_Indy4 {
	format("Our time has come now we must show ourselves.")
}

text Ardor_Uni_ArchaeologyClass_Text_Indy5 {
	format("I hope you will forgive us our disguise and please allow us an introduction. My name truly is Magnus Maximus, I am the leader of this rebel band. When we were forced to hide from the Rockets I took up that disguise to lie in wait. As fate would have it I had asked for {PLAYER} to meet me here with the intention of recruiting him into our rebel band but he was here for me.")
}

text Ardor_Uni_ArchaeologyClass_Text_Leaf3 {
	format("So you'll do it!")
}

text Ardor_Uni_ArchaeologyClass_Text_Indy6 {
	format("It is our only purpose to destroy the Rocket Party and retake our land. Now tell me girl who is your father and what is our need of urgent action now?")
}

text Ardor_Uni_ArchaeologyClass_Text_Leaf4 {
	format("My father served with you in the great war and we have travelled far, from Clara's Church to bring you the most tragic horrid news. The Rockets have imprisoned Mew itself")
}

text Ardor_Uni_ArchaeologyClass_Text_Indy7 {
	format("Impossible!")
}

text Ardor_Uni_ArchaeologyClass_Text_Leaf5 {
	format("{PLAYER} saw it for himself")
}

text Ardor_Uni_ArchaeologyClass_Text_Indy8 {
	format("That certainly explains the urgency. We must make our attack quickly before they end the universe with this evil. But still attacking now would be foolish. We need many allies if we shall win. Many of our best men are being held in a Rocket prison deep underground. With your help {PLAYER} we'll need to free those men. The prison is inside the labyrinth which only heroes can exit alive. Many foul creatures and a wicked maze mean certain death to most who travel there. I knew one day I'd need to free those men and in my disguise as the professor I made many daring trips to the cave. I know the way to their filthy prison and I will guide you through the labyrinth. However once we commonece the attack there will be no chance for us to turn back. When you are ready {PLAYER} we shall depart.")
}





