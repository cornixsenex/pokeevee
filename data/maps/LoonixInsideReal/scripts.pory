
const OBJ_EVENT_GRANNY = 1

const FIELD_PIC_GRANNY = 30

mapscripts LoonixInsideReal_MapScripts {

    MAP_SCRIPT_ON_TRANSITION {

		switch (var(VAR_GRANNY_STATE)) {
			case 0:
			case 1:
			case 2:
			case 3:
			case 4:
		    case 5:
				call (LoonixInsideReal_Script_ShowGrannyKitchen)
			case 6:
				call (LoonixInsideReal_Script_ShowGrannyDoorway)
		    case 7:
				call (LoonixInsideReal_Script_ShowGrannyTable)
			case 8:
				call (LoonixInsideReal_Script_ShowGrannyTV)
			case 9:
				call (LoonixInsideReal_Script_ShowGrannyBed)
			default:
				call (LoonixInsideReal_Script_HideGranny)
		}

    }

	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_GRANNY_STATE, 6: LoonixInsideReal_Script_FacePlayerUp
	]
	
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_GRANNY_STATE, 0: LoonixInsideReal_Script_GrannyScene1
		VAR_GRANNY_STATE, 6: LoonixInsideReal_Script_GrannyScene2
	]

}

script LoonixInsideReal_Script_HideGranny {
	setobjectxyperm (OBJ_EVENT_GRANNY, 12, 10)
	setobjectmovementtype (OBJ_EVENT_GRANNY, MOVEMENT_TYPE_INVISIBLE)

	end
}


script LoonixInsideReal_Script_ShowGrannyKitchen {
	setobjectxyperm (OBJ_EVENT_GRANNY, 1, 3)
	setobjectmovementtype (OBJ_EVENT_GRANNY, MOVEMENT_TYPE_FACE_UP)

	end
}

script LoonixInsideReal_Script_ShowGrannyDoorway {
	setobjectxyperm (OBJ_EVENT_GRANNY, 3, 6)
	setobjectmovementtype (OBJ_EVENT_GRANNY, MOVEMENT_TYPE_FACE_DOWN)

	end
}

script LoonixInsideReal_Script_ShowGrannyTable {
	setobjectxyperm (OBJ_EVENT_GRANNY, 9, 7)
	setobjectmovementtype (OBJ_EVENT_GRANNY, MOVEMENT_TYPE_FACE_LEFT)

	end
}

script LoonixInsideReal_Script_ShowGrannyTV {
	setobjectxyperm (OBJ_EVENT_GRANNY, 8, 6)
	setobjectmovementtype (OBJ_EVENT_GRANNY, MOVEMENT_TYPE_FACE_UP)

	end
}
script LoonixInsideReal_Script_ShowGrannyBed {
	setobjectxyperm (OBJ_EVENT_GRANNY, 11, 3)
	setobjectmovementtype (OBJ_EVENT_GRANNY, MOVEMENT_TYPE_FACE_DOWN)

	end
}

script LoonixInsideReal_Script_FacePlayerUp {
	turnobject (OBJ_EVENT_ID_PLAYER, DIR_NORTH)

	end
}

script LoonixInsideReal_Script_PrepareLeafGame {
	setvar (VAR_GRANNY_STATE, 2)
	end
}

script LoonixInsideReal_Script_ClearLeafGame {
	setvar (VAR_GRANNY_STATE, 1)
	end
}

script LoonixInsideReal_Script_GrannyScene1 {
	lockall

	setvar (VAR_GRANNY_STATE, 1)
	setflag (FLAG_RECEIVED_RUNNING_SHOES)
	setflag (FLAG_SYS_B_DASH)

	//Player walks to position
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
	waitmovement (0)
	//granny notices player come downstairs
	applymovement (OBJ_EVENT_GRANNY, Common_Movement_ExclamationMark)
	waitmovement (0)
	//face player
	applymovement (OBJ_EVENT_GRANNY, Common_Movement_FaceRight)
	waitmovement (0)
	//You're finally awake good to see you
	msgbox (LoonixInsideReal_Text_Granny1A, MSGBOX_DEFAULT)
	closemessage
	//Approach player
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	applymovement (OBJ_EVENT_GRANNY, LoonixInsideReal_Movement_Granny1_ApproachPlayer)
	waitmovement (0)
	//Stil working on breakfast, Your friend came by again, whats her name again
	msgbox (LoonixInsideReal_Text_Granny1B, MSGBOX_DEFAULT)
	closemessage
	//Name Leaf
	special (NameLeaf)
	waitstate
	//That's right she waiting outside for you. What a nice girl
	//Be safe and remember to stay away from the tall grass
	msgbox (LoonixInsideReal_Text_Granny1C, MSGBOX_DEFAULT)
	closemessage
	//Return granny to the kitchen
	applymovement (OBJ_EVENT_GRANNY, LoonixInsideReal_Movement_Granny1_ReturnToKitchen)
	waitmovement (0)
	//Fin
	releaseall
	end
}

script LoonixInsideReal_Script_GrannyScene2 {
	lockall

	setvar (VAR_GRANNY_STATE, 7)

	setflag (FLAG_SYS_POKEMON_GET)
	setflag (FLAG_SYS_POKEDEX_GET)
	special (SetUnlockedPokedexFlags)
	setflag (FLAG_SYS_NATIONAL_DEX)
	special (EnableNationalPokedex)

	load_field_pic(FIELD_PIC_GRANNY, 190, 98)
	msgbox (LoonixInsideReal_Text_Granny2A, MSGBOX_DEFAULT)
	destroy_field_pic(FIELD_PIC_GRANNY)
	closemessage

	
	//Granny approach Player

	//Your father would have wanted you to have
	//his Pokedex when you were old enough.

	//Give pokedex

	//Describe pokedex
	//It helps keep track of different pokemon
	//you meet in the wild. Also take this.

	//Give 5 pokeballs
	
	//Describe pokeballs and running shoes
	
	//I see Eevee really likes you I know he will keep you safe 
	//Your bond will grow over time but to become a serious trainer you need to go to trainer school
	//There's one in Aldeon Town near by

	//Now take a seat come get something to eat
	
	//granny and player go to table

	//Promise me you'll finish school
	//And come back home to rest up any time
	
	//FIN
	release
	end
}

script LoonixInsideReal_Script_TV {
	lock

	switch (var(VAR_GRANNY_STATE)) {
		case 0:
		case 1:
		case 2:
		case 3:
		case 4:
		case 5:
		case 6:
			msgbox(LoonixInsideReal_Text_TV1, MSGBOX_SIGN)
		case 7:
			msgbox(LoonixInsideReal_Text_TV2, MSGBOX_SIGN)
		default:
			msgbox(LoonixInsideReal_Text_TVX, MSGBOX_SIGN)
	}
	closemessage
	
	release
	end
}

script LoonixInsideReal_Script_Statue {
	lock
	msgbox(format("This is the statue they sent us after Dad left...\pGrandma must have forgotten to polish it."))
	release
	end
}

script LoonixInsideReal_Script_Junk {
	lock
	msgbox(format("Here's another pile of junk grandma left lying around."))
	release
	end
}

script LoonixInsideReal_Script_Special {
	lock
	showmonpic (SPECIES_KRABBY, 10, 3)
	msgbox(format("What's for dinner?"))
	hidemonpic
	release
	end
}

script LoonixInsideReal_Script_GrandmaTV {
	lockall
	applymovement(1, LoonixInsideReal_Movement_GrandmaUp)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_Delay48)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, LoonixInsideReal_Movement_PlayerToGma)
	waitmovement (0)
	msgbox (format("Oh {PLAYER}!"))
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
	msgbox ("Come here sweetie.")
	closemessage
	applymovement (OBJ_EVENT_ID_PLAYER, LoonixInsideReal_Movement_GrandmaTV)
	waitmovement (0)
	applymovement (1, LoonixInsideReal_Movement_GrandmaLeft)
	waitmovement (0)
	msgbox (LoonixInsideReal_Text_GrandmaTVSpeech)
	closemessage
	applymovement (1, LoonixInsideReal_Movement_GrandmaMono)
	waitmovement (0)
	msgbox (LoonixInsideReal_Text_GrandmaMono)
	closemessage
	applymovement (1, LoonixInsideReal_Movement_GrandmaLeft)
	waitmovement (0)
	msgbox (LoonixInsideReal_Text_GrandmaToBed)
	closemessage	
	applymovement (1, LoonixInsideReal_Movement_GrandmaBed)
	applymovement(OBJ_EVENT_ID_PLAYER, LoonixInsideReal_Movement_PlayerBed)
	waitmovement (0)
	addvar (VAR_GRANNY_STATE, 1)
	msgbox (LoonixInsideReal_Text_GrandmaBed)
	setrespawn (HEAL_LOCATION_GRANDMAS_CABIN)
	setflag (FLAG_RECEIVED_RUNNING_SHOES)
	setflag (FLAG_SYS_B_DASH)
	releaseall
	end
}
	

script LoonixInsideReal_Script_PreventExit {
	lockall
	msgbox (format("You don't have a Pokemon yet!"))
	applymovement (OBJ_EVENT_ID_PLAYER, LoonixInsideReal_Movement_PreventExit)
	waitmovement(0)
	releaseall
	end
}

script LoonixInsideReal_Script_Grandma {
	lockall
	faceplayer

	switch (var(VAR_GRANNY_STATE)) {
		case 0:
		case 1:
		case 2:
		case 3:
		case 4:
		case 5:
		case 6:
			call (LoonixInsideReal_Script_GrannyKitchen)
		case 7:
		case 8:
			call (LoonixInsideReal_Script_GrannyTable)
		default:
			msgbox("DEFAULT", MSGBOX_DEFAULT)
			closemessage
	}
	releaseall
	end
}

script LoonixInsideReal_Script_GrannyKitchen {

	load_field_pic(FIELD_PIC_GRANNY, 190, 98)
	msgbox (LoonixInsideReal_Text_GrannyKitchen, MSGBOX_DEFAULT)
	destroy_field_pic(FIELD_PIC_GRANNY)
	closemessage

	applymovement (OBJ_EVENT_GRANNY, Common_Movement_FaceUp)
	waitmovement (0)

	return
}

script LoonixInsideReal_Script_GrannyTable {
	msgbox (LoonixInsideReal_Text_GrannyTable1, MSGBOX_DEFAULT)
	closemessage

	call (Common_EventScript_OutOfCenterPartyHeal)

	msgbox (LoonixInsideReal_Text_GrannyTable2, MSGBOX_DEFAULT)
	closemessage

	applymovement (OBJ_EVENT_GRANNY, Common_Movement_FaceLeft)
	waitmovement (0)

	return
}


movement LoonixInsideReal_Movement_PreventExit {
	face_up
	walk_up * 4
	face_down
}

movement LoonixInsideReal_Movement_GrandmaTV {
	walk_down
	walk_right * 2
}

movement LoonixInsideReal_Movement_GrandmaBed {
	walk_right * 2
	walk_up * 3
	walk_right
	face_down
}

movement LoonixInsideReal_Movement_PlayerBed {
	walk_right * 3
	walk_up * 3
	face_right
}

movement LoonixInsideReal_Movement_GrandmaUp {
	face_up
}

movement LoonixInsideReal_Movement_GrandmaLeft {
	face_left
}

movement LoonixInsideReal_Movement_GrandmaMono {
	face_down
}

movement LoonixInsideReal_Movement_PlayerToGma {
	walk_down * 3
}

movement LoonixInsideReal_Movement_Granny1_ApproachPlayer {
	walk_right * 3
}

movement LoonixInsideReal_Movement_Granny1_ReturnToKitchen {
	walk_left * 3
	face_up
}

text LoonixInsideReal_Text_GrandmaTVSpeech {
	format("It's all over the news that Mew's returned!\n My boy, this is a God sent mircale.\pJust once...to see the living shape of Mew...there's nothing more of life that I would ask.")
}

text LoonixInsideReal_Text_GrandmaMono {
	format("But now the strength of life has left these bones, and I can hardly walk across the yard. Saeclara City is too far away for this old crow to fly.")
}	

text LoonixInsideReal_Text_GrandmaToBed {
	format("I'll have to stay.\p {PLAYER} will you help me get into bed?")
}

text LoonixInsideReal_Text_GrandmaBed {
	format("Thank you.")
}

text LoonixInsideReal_Text_Granny1A {
	format("Good morning sleepyhead!")
}

text LoonixInsideReal_Text_Granny1B {
	format("I'm still cooking breakfast but your friend came by earlier. What was her name again?")
}

//That's right {NOE}
text LoonixInsideReal_Text_Granny1C {
	format("That's right, {LEAF}! She's such a sweet and beautiful young girl. I think she's still waiting outside for you. Go out, have fun, but keep your distance from the tall grass where wild pokemon roam.")
}

text LoonixInsideReal_Text_GrannyKitchen {
	format("Go have fun with {LEAF} but please stay safe.")
}

text LoonixInsideReal_Text_GrannyTable1 {
	format("So nice to see you {PLAYER}! Sit down and rest.")
}

text LoonixInsideReal_Text_GrannyTable2 {
	format("Come back anytime and good luck at school.")
}

text LoonixInsideReal_Text_TV1 {
	format("Humans and aliens working together to explore new worlds, seek out strange new life, and boldly go where no one has ever gone before.")
}

text LoonixInsideReal_Text_TV2 {
	format("Handsome men in suits discuss transportation infrastructure with a member of the Restoration Party.")
}

text LoonixInsideReal_Text_TVX {
	format("Looks like the TV is switched off.")
}

//I told you to keep out of the tall grass! 
//Oh Sneed, what can I say? I'm worried sick,
//so scared for you, for me. I still see you
//as that rambunctious boy you used to be
//but now you're growing up and I'm afraid
//of losing you like I lost your father.
//I long to keep you here forever safe
//but as your strength flowers my own decays.
//I often asked myself if what I did
//was the right thing to do to raise you best
//but now I see that choice is yours to make.
//The time has come for me to set you free
//for even though my heart betrays these words
//I trust you and I know it's for the best.
text LoonixInsideReal_Text_Granny2A {
	format("I told you to keep out of the tall grass! Oh Sneed, what can I say? I'm worried sick, so scared for you, for me. I still see you as that rambunctious boy you used to be but now you're growing up and I'm afraid of losing you like I lost your father. I long to keep you here forever safe but as your strength flowers my own decays. I often asked myself if what I did was the right thing to do to raise you best but now I see that choice is yours to make. The time has come for me to set you free for even though my heart betrays these words I trust you and I know it's for the best.")
}

