
//VAR_TEMP_B is REGISTRATION_FEE

//Dynamic Map is  VAR_TEMP_5 :
    //1: Via Saxosa
    //2: Tranquillitas
    //3: Montes Vigiliae
    //4: Acta Tranquilla
    //5: Mare Tranquillum

mapscripts EliciusBeach_MapScripts {

    MAP_SCRIPT_ON_TRANSITION {

        //Handle Dynamic Map
        specialvar (VAR_TEMP_5, GetDynamicMapSec_EliciusBeach_F)

        //clear Via Saxosa
        if (!flag(FLAG_ROUTE9_CLEARBLOCK)) {
            setflag (FLAG_ROUTE9_CLEARBLOCK)
        }
		//Show misty if applicable
		if (!flag(FLAG_TRANQUILITAS_SURFSCHOOL_OPEN)) {
			call (Tranquilitas_Script_ShowMisty)
		}
		//Show Leaf if applicablea
		if (flag(FLAG_LEAF_ISGF) && !flag(FLAG_TRANQUILITAS_SURFSCHOOL_OPEN)) {
			call (Tranquilitas_Script_ShowLeaf)
		}
    }

}

script EliciusBeach_Script_Transition_ViaSaxosa {
    setvar (VAR_TEMP_5, 1)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script EliciusBeach_Script_Transition_Tranquillitas{
    setvar (VAR_TEMP_5, 2)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script EliciusBeach_Script_Transition_ActaTranquilla {
    setvar (VAR_TEMP_5, 4)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script EliciusBeach_Script_Transition_MareTranquillum {
    setvar (VAR_TEMP_5, 5)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script Tranquilitas_Script_ShowLeaf {
	setobjectxyperm (LOCALID_TRANQUILLITAS_LEAF, 4, 19)
	setobjectmovementtype (LOCALID_TRANQUILLITAS_LEAF, MOVEMENT_TYPE_LOOK_AROUND)
	return
}

script Tranquilitas_Script_ShowMisty {
	setobjectxyperm (LOCALID_TRANQUILLITAS_MISTY, 24, 27)
	setobjectmovementtype (LOCALID_TRANQUILLITAS_MISTY, MOVEMENT_TYPE_FACE_DOWN)
	return
}

script Tranquilitas_Script_Misty {
	lock
	faceplayer
	load_field_pic (FIELD_PIC_MISTY, 190, 98)
	msgbox (Tranquilitas_Text_MistyBlock)
	destroy_field_pic (FIELD_PIC_MISTY)
	closemessage
	applymovement (LOCALID_TRANQUILLITAS_MISTY, Common_Movement_FaceDown)
	waitmovement (0)
	release
	end
}


script Tranquilitas_Script_Leaf {
	lock
	faceplayer
	//L: Oh finally there you are ready for Surf School Y / N
	load_field_pic (FIELD_PIC_LEAF, 190, 98)
	msgbox (Tranquilitas_Text_Leaf1, MSGBOX_YESNO)
	//N: Well get ready I can't wait long
	if (var(VAR_RESULT) == FALSE) {
		msgbox(Tranquilitas_Text_LeafNotReady)
		destroy_field_pic (FIELD_PIC_LEAF)
		closemessage
		end
	//Y: This is so romantic lets go -> Begin surf school
	} else {
		msgbox (Tranquilitas_Text_Leaf2)
		destroy_field_pic (FIELD_PIC_LEAF)
		closemessage
		call (Tranquilitas_Script_MistyScene)
	}

}

script Tranquilitas_Script_MistyScene {
	//put player and leaf in correct position
	specialvar (VAR_RESULT, GetPlayerFacingDirection)
	switch (var(VAR_RESULT)) {
		case DIR_NORTH:
			applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_WalkRight)
			waitmovement (0)
			applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_WalkDown)
			waitmovement (0)
		case DIR_SOUTH:
			applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_WalkDown)
			applymovement (LOCALID_PLAYER, Common_Movement_WalkDown)
			waitmovement (0)
			applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_WalkRight)
			applymovement (LOCALID_PLAYER, Common_Movement_WalkDown)
			waitmovement (0)
		case DIR_EAST:
			applymovement (LOCALID_PLAYER, Common_Movement_WalkDown)
			applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_WalkDown)
			waitmovement (0)
			applymovement (LOCALID_PLAYER, Common_Movement_WalkRight)
			applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_WalkRight)
			waitmovement (0)
		case DIR_WEST:
			applymovement (LOCALID_PLAYER, Common_Movement_WalkLeft)
			applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_WalkDown)
			waitmovement (0)
			applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_WalkRight)
			applymovement (LOCALID_PLAYER, Common_Movement_WalkDown)
			waitmovement (0)
		default:
			msgbox("ERROR: GetPlayer\nFacingDirection OOB")
			closemessage
			end
	}
	//goto misty
	applymovement (LOCALID_TRANQUILLITAS_LEAF, Tranquilitas_Movement_ApproachMisty)
	applymovement (LOCALID_PLAYER, Tranquilitas_Movement_ApproachMisty)
	waitmovement (0)
	//M: Hello, we're full	
	load_field_pic (FIELD_PIC_MISTY, 190, 98)
	msgbox (Tranquilitas_Text_Misty1)
	destroy_field_pic (FIELD_PIC_MISTY)
	//L: It's leaf and player we have reservation
	load_field_pic (FIELD_PIC_LEAF, 190, 98)
	msgbox (Tranquilitas_Text_Leaf3)
	destroy_field_pic (FIELD_PIC_LEAF)
	//M: Forgive me, just pay the reg fee and we can proceed
	applymovement (LOCALID_TRANQUILLITAS_MISTY, Common_Movement_ExclamationMark)
	load_field_pic (FIELD_PIC_MISTY, 190, 98)
	waitmovement (0)
	//get reg fee = player gold / 3
    //Handle Broke player
	msgbox (Tranquilitas_Text_Misty2)
	destroy_field_pic (FIELD_PIC_MISTY)
    showmoneybox (18, 0, 0)
    checkmoney (500)
    if (var(VAR_RESULT) == FALSE) {
        call (Tranquilitas_Script_NoMoney)
    }

	//make player pay X money lol - X is 1/3 of player total cash I think that's funny
//	destroy_field_pic (FIELD_PIC_MISTY)
	call (Tranquilitas_Script_Registration)
	//M: Follow me your lessons begin inside
    load_field_pic (FIELD_PIC_MISTY, 190, 98)
	msgbox (Tranquilitas_Text_Misty3)
    destroy_field_pic (FIELD_PIC_MISTY)
    closemessage
	//M exit
    applymovement (LOCALID_TRANQUILLITAS_MISTY, Common_Movement_WalkUp)
    waitmovement (0)
    hideobjectat (LOCALID_TRANQUILLITAS_MISTY, MAP_BIG_ISLAND_N)
    removeobject (LOCALID_TRANQUILLITAS_MISTY)
    //Player et Leaf face
    applymovement (LOCALID_PLAYER, Common_Movement_FaceRight)
    applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_FaceLeft)
    waitmovement (0)
	//L: Lets go so excited and romantic love you blah
    load_field_pic (FIELD_PIC_LEAF, 190, 98)
    msgbox (Tranquilitas_Text_Leaf4)
    destroy_field_pic (FIELD_PIC_LEAF)
    closemessage
	//L exit	
    applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_WalkUp2)
    waitmovement (0)
    hideobjectat (LOCALID_TRANQUILLITAS_LEAF, MAP_BIG_ISLAND_N)
    removeobject (LOCALID_TRANQUILLITAS_LEAF)
	//permaremove misty et leaf
	setobjectxyperm (LOCALID_TRANQUILLITAS_MISTY, 0, 0)
	setobjectxyperm (LOCALID_TRANQUILLITAS_LEAF, 0, 0)
	setobjectmovementtype (LOCALID_TRANQUILLITAS_MISTY, MOVEMENT_TYPE_INVISIBLE)
	setobjectmovementtype (LOCALID_TRANQUILLITAS_LEAF, MOVEMENT_TYPE_INVISIBLE)
	//Set flags and prep surf school
    setflag (FLAG_TRANQUILITAS_SURFSCHOOL_OPEN)
    //fin
	release
	end
}

//If Player and Leaf goto Misty but Player is broke
script Tranquilitas_Script_NoMoney {
    //Alert Misty
    applymovement (LOCALID_TRANQUILLITAS_MISTY, Common_Movement_ExclamationMark)
    waitmovement (0)
    //Sorry but youre broke
    msgbox (Tranquilitas_Text_MistyNoMoney)
    closemessage 
//	destroy_field_pic (FIELD_PIC_MISTY)
	hidemoneybox
    //Alert Leaf
    applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_ExclamationMark)
    waitmovement (0)
    //face player leaf
    applymovement (LOCALID_PLAYER, Common_Movement_FaceRight)
    applymovement (LOCALID_TRANQUILLITAS_MISTY, Common_Movement_FaceLeft)
    waitmovement (0)
    //L: this is so embarrising how could you do this dont come back until you have more money
//    load_field_pic (FIELD_PIC_LEAF, 190, 98)
    msgbox (Tranquilitas_Text_LeafNoMoney)
    closemessage
//    destroy_field_pic (FIELD_PIC_LEAF)
    //exit leaf
    applymovement (LOCALID_TRANQUILLITAS_LEAF, Tranquilitas_Movement_LeafExitNoMoney)
    waitmovement (0)
    //reset everything
    call (Tranquilitas_Script_ShowLeaf)
    call (Tranquilitas_Script_ShowMisty)

    release
    end
}

script Tranquilitas_Script_Registration {
	copymoney (VAR_TEMP_B)
	divvar (VAR_TEMP_B, 3)
	//copy fee into string var
	buffernumberstring (STR_VAR_1, VAR_TEMP_B)
	load_field_pic (FIELD_PIC_MISTY, 190, 98)
	//tell price - already grabbed --misty pic already shown
	msgbox (Tranquilitas_Text_Registration, MSGBOX_YESNO)
	//do you agreen yn
	if (var(VAR_RESULT) == NO) {
        hidemoneybox
		closemessage
		destroy_field_pic (FIELD_PIC_MISTY)
		//alert leaf
		applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_ExclamationMark)
		waitmovement (0)
		//turn player to leaf: , show misty pic, redo
		applymovement (LOCALID_PLAYER, Common_Movement_FaceRight)
		applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_FaceLeft)
		waitmovement (0)
		//Leaf says smth
		load_field_pic (FIELD_PIC_LEAF, 190, 98)
		msgbox (Tranquilitas_Text_LeafNoRegistration)
		destroy_field_pic (FIELD_PIC_LEAF)
		applymovement (LOCALID_PLAYER, Common_Movement_FaceUp)
		applymovement (LOCALID_TRANQUILLITAS_LEAF, Common_Movement_FaceUp)
		waitmovement (0)
        showmoneybox (18, 0)
		//switch back to misty pic
		call (Tranquilitas_Script_Registration)
	} else { //YES
		//pay money
        removevarmoney(VAR_TEMP_B)
        updatemoneybox
        playse (SE_SHOP)
        waitse
		//hide coin box
        hidemoneybox
		closemessage
		destroy_field_pic (FIELD_PIC_MISTY)
        //rest of scene continues in MistyScene
		return
	}
}

script Tranquilitas_Script_TouristF {
	faceplayer
	msgbox (Tranquilitas_Text_TouristF)
	closemessage
	end
}

movement Tranquilitas_Movement_ApproachMisty {
	walk_right * 4
	walk_down * 8
	walk_right * 15
	face_up
}

//WIP
movement Tranquilitas_Movement_LeafExitNoMoney {
    walk_down
    walk_left * 10
}

text Tranquilitas_Text_TouristF {
	format("I quit my job to go on vacation.")
}

text Tranquilitas_Text_MistyBlock {
	format("I'm sorry sir, but the Surf School is booked 
			all season long. I haven't any room
			for single registration, but I might
			be able to accomodate a pair.
			If you find someone to sign up with you 
			come speak with me. I'll try to fit you in!")
}

text Tranquilitas_Text_Leaf1 {
	format("Where have you been {PLAYER}? You kept me waiting 
			for so long! Are you ready for Surf School?")
}

text Tranquilitas_Text_LeafNotReady {
	format("Ok, but hurry up! I hate waiting.")
}

text Tranquilitas_Text_Leaf2 {
	format("This is so romantic! I cannot wait
			to learn to surf the seas with you, my love.
			C'mon! Let's go meet our surf instructor.")
}

text Tranquilitas_Text_Misty1 {
	format("Hello and welcome to Tranquilitas
			Surf School. I'm Misty, the surf instructor.
			Unfortunately we are fully booked,
			I couldn't possibly allow you two
			to join...")
}

text Tranquilitas_Text_Leaf3 {
    format("But we have a reservation!
            It should be under my boyfriend's name, {PLAYER}.")
}

text Tranquilitas_Text_Misty2 {
    format("Of course, forgive me, I remember now.
            You made a reservation earlier
            for a couple's package at the Surf School.
            We've been expecting you, but first thing's first,
            you'll need to pay the registration fee.")
}

text Tranquilitas_Text_Registration {
	format("The registration fee for Surf School is a one time low payment of just {STR_VAR_1}. Will you pay the registration fee?")
}

text Tranquilitas_Text_LeafNoRegistration {
	format("But {PLAYER} you promised for our honeymooon
            that you would take us to learn how to surf!
            You still want me, don't you? This will be fun!
            Just pay the fee, and let's learn how to surf!")
}

text Tranquilitas_Text_MistyNoMoney {
    format("I'm sorry sir, but it seems you do not
            have enough cash to cover the small fee
            required to register at Surf School.
            You will have to return with more money.")
}

text Tranquilitas_Text_LeafNoMoney {
    format("This can't be real! It's so embarrising!
            How could you do something like this to me?
            You need to make more money if you want
            me to be your girlfriend. {PLAYER}, I have needs!
            Don't talk to me until you can afford
            to pay for our Surf School registration.")
}

text Tranquilitas_Text_Misty3 {
    format("Thank you. I'm sure you're excited to start
            learning to surf, so let's not waste more time.
            Follow me in, we'll begin the lesson.")
}

text Tranquilitas_Text_Leaf4 {
    format("Oh {PLAYER}, isn't this like a dream come true?
            Just you, and me, and a group of strangers,
            surrounded by a team of teenage girls
            learning how to surf in an indoor pool...
            I can't imagine any better way
            to celebrate our new relationship.
            C'mon, let's go inside and get started!")
}

