
const OBJ_EVENT_PHIL = 15
const OBJ_EVENT_MEDEA = 14
const OBJ_EVENT_HYPNO = 16

mapscripts IgnisMons_3F_MapScripts {

	MAP_SCRIPT_ON_TRANSITION {
		if (var(VAR_ARGO_STATE) == 4) {
			call (IgnisMons_3F_Script_ShowMedea)
		}
	}

	MAP_SCRIPT_ON_RESUME {
		call (IgnisMons_3F_Script_OnResume)
	
	}
}

script IgnisMons_3F_Script_OnResume {
	setstepcallback (STEP_CB_FALSE_FLOOR)
	switch (var(VAR_IGNISMONS_STATE)) {
		case 0:
			call (IgnisMons_3F_Script_SetBarriers_Init)
		case 1:
			call (IgnisMons_3F_Script_SetBarriers_Red)
		case 2:
			call (IgnisMons_3F_Script_SetBarriers_Blue)
		case 3:
			call (IgnisMons_3F_Script_SetBarriers_Green)
		default:
			call (IgnisMons_3F_Script_SetBarriers_Init)
	}
	end
}

script IgnisMons_3F_Script_ShowMedea {

	hideobjectat (OBJ_EVENT_PHIL, MAP_IGNIS_MONS_3F)
	removeobject (OBJ_EVENT_PHIL)

	hideobjectat (OBJ_EVENT_MEDEA, MAP_IGNIS_MONS_3F)
	removeobject (OBJ_EVENT_MEDEA)

	hideobjectat (OBJ_EVENT_HYPNO, MAP_IGNIS_MONS_3F)
	removeobject (OBJ_EVENT_HYPNO)

	setobjectxyperm (OBJ_EVENT_PHIL, 7, 31)
	setobjectmovementtype (OBJ_EVENT_PHIL, MOVEMENT_TYPE_FACE_UP)
	addobject (OBJ_EVENT_PHIL)
	showobjectat (OBJ_EVENT_PHIL, MAP_IGNIS_MONS_3F)

	setobjectxyperm (OBJ_EVENT_MEDEA, 8, 31)
	setobjectmovementtype (OBJ_EVENT_MEDEA, MOVEMENT_TYPE_FACE_UP)
	addobject (OBJ_EVENT_MEDEA)
	showobjectat (OBJ_EVENT_MEDEA, MAP_IGNIS_MONS_3F)

	setobjectxyperm (OBJ_EVENT_HYPNO, 9, 31)
	setobjectmovementtype (OBJ_EVENT_HYPNO, MOVEMENT_TYPE_FACE_UP)
	addobject (OBJ_EVENT_HYPNO)
	showobjectat (OBJ_EVENT_HYPNO, MAP_IGNIS_MONS_3F)

	end
}

script IgnisMons_3F_Script_HideMedea {

	setobjectxyperm (OBJ_EVENT_PHIL, 12, 30)
	setobjectmovementtype (OBJ_EVENT_PHIL, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (OBJ_EVENT_MEDEA, 13, 30)
	setobjectmovementtype (OBJ_EVENT_MEDEA, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (OBJ_EVENT_HYPNO, 14, 30)
	setobjectmovementtype (OBJ_EVENT_HYPNO, MOVEMENT_TYPE_INVISIBLE)

	hideobjectat (OBJ_EVENT_PHIL, MAP_IGNIS_MONS_3F)
	removeobject (OBJ_EVENT_PHIL)

	hideobjectat (OBJ_EVENT_MEDEA, MAP_IGNIS_MONS_3F)
	removeobject (OBJ_EVENT_MEDEA)

	hideobjectat (OBJ_EVENT_HYPNO, MAP_IGNIS_MONS_3F)
	removeobject (OBJ_EVENT_HYPNO)

	return

}



script IgnisMons_3F_Script_SetBarriers_Red {
	call (IgnisMons_3F_Script_ShowBlue)
	call (IgnisMons_3F_Script_ShowGreen)

	call (IgnisMons_3F_Script_HideRed)

	return
}

script IgnisMons_3F_Script_SetBarriers_Blue {
	call (IgnisMons_3F_Script_ShowRed)
	call (IgnisMons_3F_Script_ShowGreen)

	call (IgnisMons_3F_Script_HideBlue)

	return
}

script IgnisMons_3F_Script_SetBarriers_Green {
	call (IgnisMons_3F_Script_ShowRed)
	call (IgnisMons_3F_Script_ShowBlue)

	call (IgnisMons_3F_Script_HideGreen)

	return
}

script IgnisMons_3F_Script_SetBarriers_Init {
	call (IgnisMons_3F_Script_ShowRed)
	call (IgnisMons_3F_Script_ShowBlue)
	call (IgnisMons_3F_Script_ShowGreen)

	return
}

script IgnisMons_3F_Script_RedButton {
	lockall
	if (var(VAR_IGNISMONS_STATE) != 1) {	
		setvar (VAR_IGNISMONS_STATE, 1)
		playse (SE_FU_ZAKU)
		call (IgnisMons_3F_Script_SetBarriers_Red)
		special (DrawWholeMapView)
	}	
    releaseall
	end
}

script IgnisMons_3F_Script_BlueButton {
	lockall
	if (var(VAR_IGNISMONS_STATE) != 2) {	
		setvar (VAR_IGNISMONS_STATE, 2)
		playse (SE_FU_ZAKU)
		call (IgnisMons_3F_Script_SetBarriers_Blue)
		special (DrawWholeMapView)
	}
	releaseall
	end
}

script IgnisMons_3F_Script_GreenButton {
	lockall
	if (var(VAR_IGNISMONS_STATE) != 3) {	
		setvar (VAR_IGNISMONS_STATE, 3)
		playse (SE_FU_ZAKU)
		call (IgnisMons_3F_Script_SetBarriers_Green)
		special (DrawWholeMapView)
	}
	releaseall
	end
}

script IgnisMons_3F_Script_ShowRed {
	setmetatile (17, 22, METATILE_IgnisMons_Barrier_Red_Shadow, TRUE)
	setmetatile (18, 23, METATILE_IgnisMons_Barrier_Red, TRUE)


	setmetatile (17, 35, METATILE_IgnisMons_Button_Red, FALSE)
	return
}

script IgnisMons_3F_Script_ShowBlue {
	setmetatile (5, 6, METATILE_IgnisMons_Barrier_Blue_Shadow, TRUE)
	setmetatile (6, 5, METATILE_IgnisMons_Barrier_Blue, TRUE)
	setmetatile (7, 6, METATILE_IgnisMons_Barrier_Blue, TRUE)
	setmetatile (12, 15, METATILE_IgnisMons_Barrier_Blue_Shadow, TRUE)
	setmetatile (13, 14, METATILE_IgnisMons_Barrier_Blue_Shadow, TRUE)
	setmetatile (14, 15, METATILE_IgnisMons_Barrier_Blue, TRUE)
	setmetatile (9, 40, METATILE_IgnisMons_Barrier_Blue, TRUE)
	setmetatile (10, 39, METATILE_IgnisMons_Barrier_Blue_Shadow, TRUE)
	setmetatile (11, 40, METATILE_IgnisMons_Barrier_Blue, TRUE)
	setmetatile (10, 41, METATILE_IgnisMons_Barrier_Blue, TRUE)

	setmetatile (2, 40, METATILE_IgnisMons_Button_Blue, FALSE)

	return
}

script IgnisMons_3F_Script_ShowGreen {
	setmetatile (1, 6, METATILE_IgnisMons_Barrier_Green_Shadow, TRUE)
	setmetatile (2, 5, METATILE_IgnisMons_Barrier_Green, TRUE)
	setmetatile (12, 7, METATILE_IgnisMons_Barrier_Green_Shadow, TRUE)
	setmetatile (13, 6, METATILE_IgnisMons_Barrier_Green_Shadow, TRUE)
	setmetatile (1, 30, METATILE_IgnisMons_Barrier_Green_Shadow, TRUE)
	setmetatile (2, 29, METATILE_IgnisMons_Barrier_Green, TRUE)
	setmetatile (3, 28, METATILE_IgnisMons_Barrier_Green, TRUE)

	setmetatile (7, 17, METATILE_IgnisMons_Button_Green, FALSE)

	return
}


script IgnisMons_3F_Script_HideRed {
	setmetatile (17, 22, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (18, 23, METATILE_IgnisMons_Floor, FALSE)


	setmetatile (17, 35, METATILE_IgnisMons_Button_White, FALSE)
	return
}

script IgnisMons_3F_Script_HideBlue {
	setmetatile (5, 6, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (6, 5, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (7, 6, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (12, 15, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (13, 14, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (14, 15, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (9, 40, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (10, 39, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (11, 40, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (10, 41, METATILE_IgnisMons_Floor, FALSE)

	setmetatile (2, 40, METATILE_IgnisMons_Button_White, FALSE)

	return
}

script IgnisMons_3F_Script_HideGreen {
	setmetatile (1, 6, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (2, 5, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (12, 7, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (13, 6, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (1, 30, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (2, 29, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (3, 28, METATILE_IgnisMons_Floor, FALSE)

	setmetatile (7, 17, METATILE_IgnisMons_Button_White, FALSE)

	return
}

script IgnisMons_3F_Script_MedeaTriggerA {
	setvar (VAR_TEMP_7, 1)
	call (IgnisMons_3F_Script_Medea)
}

script IgnisMons_3F_Script_MedeaTriggerB {
	call (IgnisMons_3F_Script_Medea)
}


script IgnisMons_3F_Script_Medea {
	lockall

	setvar (VAR_ARGO_STATE, 5)

	//Move Player into place

	applymovement (OBJ_EVENT_ID_PLAYER, IgnisMons_3F_Movement_ApproachMedea)
	waitmovement (0)

	if (var(VAR_TEMP_7) == 1) {
		applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
		waitmovement (0)
	}

	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)

	//Medea Speech
	msgbox (IgnisMons_3F_Text_Medea, MSGBOX_DEFAULT)
	closemessage

	applymovement (OBJ_EVENT_PHIL, IgnisMons_3F_Movement_PhilApproach)
	applymovement (OBJ_EVENT_MEDEA, IgnisMons_3F_Movement_MedeaExit)
	applymovement (OBJ_EVENT_HYPNO, IgnisMons_3F_Movement_HypnoExit)
	waitmovement (OBJ_EVENT_PHIL)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	waitmovement (OBJ_EVENT_MEDEA)

	//Phil battle
	msgbox  (IgnisMons_3F_Text_PhilPre, MSGBOX_DEFAULT)
	closemessage

	trainerbattle_no_intro (TRAINER_PHILOCTETES, IgnisMons_3F_Text_PhilLost)

	//Post Battle
	msgbox (IgnisMons_3F_Text_PhilPost, MSGBOX_DEFAULT)
	closemessage

	applymovement (OBJ_EVENT_PHIL, IgnisMons_3F_Movement_PhilExit)
	waitmovement (0)
	
	//Persist hidden Medea and Phil and Hypno
	call (IgnisMons_3F_Script_HideMedea)
	releaseall
	end



	//applymovement (OBJ_EVENT_PHIL, IgnisMons_3F_Movement_Medea_Foe)
	//applymovement (OBJ_EVENT_MEDEA, IgnisMons_3F_Movement_Medea_Foe)
	//applymovement (OBJ_EVENT_HYPNO, IgnisMons_3F_Movement_Medea_Foe)
	//waitmovement (0)

	//msgbox (IgnisMons_3F_Text_MedeaPre, MSGBOX_DEFAULT)
	//closemessage

	//trainerbattle_two_trainers (TRAINER_PHILOCTETES, IgnisMons_3F_Text_PhiloctetesLost, TRAINER_MEDEA, IgnisMons_3F_Text_MedeaLost)

	//msgbox (IgnisMons_3F_Text_MedeaPost, MSGBOX_DEFAULT)
	//closemessage

	//applymovement (OBJ_EVENT_MEDEA, IgnisMons_3F_Movement_Medea_Exit)
	//applymovement (OBJ_EVENT_HYPNO, IgnisMons_3F_Movement_Medea_Exit)
	//waitmovement (0)

	//applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
	//applymovement (OBJ_EVENT_PHIL, IgnisMons_3F_Movement_Phil)
	//waitmovement (0)

}


movement IgnisMons_3F_Movement_ApproachMedea {
	walk_left * 5
	walk_down * 2
}

movement IgnisMons_3F_Movement_Medea_Foe {
	walk_up * 3
}

movement IgnisMons_3F_Movement_MedeaExit {
	walk_right
	walk_down * 5
}

movement IgnisMons_3F_Movement_HypnoExit {
	walk_down * 5
}

movement IgnisMons_3F_Movement_Phil {
	walk_up
	face_right
}

movement IgnisMons_3F_Movement_PhilApproach {
	walk_up * 2
	face_right
}

movement IgnisMons_3F_Movement_PhilExit {
	walk_up * 2
	walk_right * 9
}



//Impressive. There are few who come this far
//but you will not escape your present doom.
//Now join your friend as my slave. Go Hypno!
text IgnisMons_3F_Text_MedeaPre {
	format("Impressiveay. Erethay areay ewfay owhay omecay isthay arfay utbay ouyay illway otnay escapeay ouryay esentpray oomday. Ownay oinjay ouryay iendfray asay ymay aveslay. Ogay Ypnohay!")
}

//Hypno! How could you fail me?
text IgnisMons_3F_Text_MedeaLost {
	format("Ypnohay! Owhay ouldcay ouyay ailfay emay?")
}

//Get back! I do not wish to fight with you.
//Now you will pay for fouling this temple
text IgnisMons_3F_Text_PhilPre {
	format("Ownay ouyay... Get back... illway aypay... I do not wish... orfay oulingfay... to fight ... isthay...lwith you... empltay...")
}

text IgnisMons_3F_Text_PhilLost {
	format("Where am I?")
}

//Indeed you are powerful. The goddess
//gives honor to those who defeat her spells.
//I must warn Papa youve come for our fleece
text IgnisMons_3F_Text_MedeaPost {
	format("Indeeday ouyay areay owerfulpay. Ethay oddessgay ivesgay onorhay otay osethay owhay efeatday erhay ellspay. Iay ustmay arnway Apapay ouyay'evay omecay orfay ouray eeceflay.")
}

//Disbelief. Shame at defeat. Shame to face his friends after being defeated and turned against you. Traitor. Vows to live / hide in the woods. Do not come looking for me.
text IgnisMons_3F_Text_PhilPost {
	format("How could this happen? Great Philoctetes defeated by this boy, abandonded by the Argonauts and enslaved by a witch? Tell Jason and the other Argonauts that I was not the one who broke my oath, instead they nullified our fellowship when they left me for dead...Yet I was beat. I'm nothing more than a betrayed traitor, a disgrace to my family, and this earth. I'll never walk among men after this. I'll flee into the woods. Do not seek me.")
}
//Impressive. There are few who come this far
//but entry to the sacred halls under
//strict penalty of death is forbidden
//It would be right to smite where you stand
//and make you my puppet like your friend here
//Yet Hecate strongly pulls back my hand
//I sense that I am needed back at home
//This vessel no longer serves my purpose
//Destroy eachother! Forth! I must be off.
//======
//Impressive, there are few who come this far but entry to the sacred halls, under strict penalty of death, is forbidden. It would be right to smite you where you stand and make you my puppet, like your friend here, yet Hecate strongly pulls back my hand. I sense that I am needed back at home. This vessel no longer serves my purpose, destroy eachother! Forth! I must be off.
text IgnisMons_3F_Text_Medea {
	format("Ipmressiveay, erethat areay ewfay owhay omecay isthay arfay utbay entryay otay ethay acredsay allshay, underay ictstray enaltypay ofay eathday, isay orbiddenfay. Itay ouldway ebay ightray otay itesmay ouyay erewhay ouyay andstay anday akemay ouyay ymay uppetpay, ikelay ouryay iendfray erehay, etyay Ecatehay onglystray ullspay ackbay ymay andhay. Iay ensesay atthay Iay amay eedenay achbay atay omehay. Isthay esselvay onay ongerlay ervessay ymay urposepay. Estroyday eachotheray! Iay ustmay ebay offay.")
}

