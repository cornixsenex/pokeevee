
const GYMNASIUM_STATE = VAR_TEMP_5
//0: OOB
//1: Before Hector arrives
//2: Hector is preparing to fight
//3: Hector has just left
//4: Protesilaus is champion / fin

mapscripts Gymnasium_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		//set Gymansium State 
		call (Gymnasium_Script_SetGymnasiumState)
		//show Gymnasium setup based on GYMNASIUM_STATE 
		if (var(GYMNASIUM_STATE) == 1) { //Before Hector Arrives
			call (Gymnasium_Script_HideHectorEtTrainer)
		}
		//hide hector if he's gone
		if (var(GYMNASIUM_STATE) > 2) { //Only 4 (P champ) should ever be evald 
			call (Gymnasium_Script_HideHector)
			//show challenger as champion if re-enter after he left
			call (Gymnasium_Script_ShowChampionProtesilaus)
		}
	}
}

//Sets var to know gymnasium state
script Gymnasium_Script_SetGymnasiumState {
	checkitem (ITEM_WEDDING_INVITATION_HECTOR, 1)
	//Champion Protesilaus 
	if (flag(FLAG_TEMP_B)) {
		setvar (GYMNASIUM_STATE, 4)
	}
	//Hector in the building 
	elif (var(VAR_RESULT) == TRUE) {
		setvar (GYMNASIUM_STATE, 2)
	} else {
		//Hector just left
		if (flag(FLAG_GYMNASIUM_HECTORLEFT)) {
			setvar (GYMNASIUM_STATE, 3)
		} else {
			//Before Hector Arrives 
			setvar (GYMNASIUM_STATE, 1)
		}
	}
	return
}

script Gymnasium_Script_HideHector {
	setobjectxyperm (LOCALID_GYMNASIUM_HECTOR, 0, 0)
	setobjectmovementtype (LOCALID_GYMNASIUM_HECTOR, MOVEMENT_TYPE_INVISIBLE)
	return
}

script Gymnasium_Script_HideHectorEtTrainer {
	call (Gymnasium_Script_HideHector)
	setobjectxyperm (LOCALID_GYMNASIUM_HECTORTRAINER, 0, 0)
	setobjectmovementtype (LOCALID_GYMNASIUM_HECTORTRAINER, MOVEMENT_TYPE_INVISIBLE)
	return
}	

script Gymnasium_Script_ShowChampionProtesilaus {
	setobjectxyperm (LOCALID_GYMNASIUM_HECTORTRAINER, 7, 4)
	setobjectmovementtype (LOCALID_GYMNASIUM_HECTORTRAINER, MOVEMENT_TYPE_FACE_DOWN)
	setobjectxyperm (LOCALID_GYMNASIUM_PROTESILAUSTRAINER, 3, 4)
	setobjectmovementtype (LOCALID_GYMNASIUM_PROTESILAUSTRAINER, MOVEMENT_TYPE_FACE_DOWN)
	setobjectxyperm (LOCALID_GYMNASIUM_PROTESILAUS, 4, 2)
	setobjectmovementtype (LOCALID_GYMNASIUM_PROTESILAUS, MOVEMENT_TYPE_FACE_DOWN)
	return
}

//Entry dudes
script  Gymnasium_Script_BlackBelt {
    specialvar (VAR_RESULT, RandDigit)
	switch (var(VAR_RESULT)) {
		case 0:
		case 1:
		case 2:
		case 3:
			msgbox (Gymnasium_Text_BlackBelt1, MSGBOX_NPC)
		case 4:
		case 5:
		case 6:
			msgbox (Gymnasium_Text_BlackBelt2, MSGBOX_NPC)
		case 7:
		case 8:
		case 9:
		default:
			msgbox (Gymnasium_Text_BlackBelt3, MSGBOX_NPC)
	}
}

script  Gymnasium_Script_Hector {
	msgbox (Gymnasium_Text_Hector1, MSGBOX_NPC)
	checkitem (ITEM_WEDDING_INVITATION_HECTOR, 1)
	if (var(VAR_RESULT) == FALSE) {
		end
	} else {
		//alert Hector
		applymovement (LOCALID_GYMNASIUM_HECTOR, Common_Movement_ExclamationMark)
		waitmovement (0)
		//H: Whats that? You have something for me?
		msgbox (Gymnasium_Text_Hector_Wedding1)
		removeitem (ITEM_WEDDING_INVITATION_HECTOR)
		//Give H Wedding Invitation
		msgbox (Gymnasium_Text_GiveInvitation)
		msgbox (Gymnasium_Text_Hector_Wedding2)
		closemessage
		//exit hector
		specialvar (VAR_RESULT, GetPlayerFacingDirection)
		if (var(VAR_RESULT) == DIR_NORTH) {
			applymovement (LOCALID_GYMNASIUM_HECTOR, Common_Movement_WalkLeft)
			waitmovement (0)
		}
		applymovement (LOCALID_GYMNASIUM_HECTOR, Common_Movement_WalkDown7)
		waitmovement (0)
		//hide aeneas
		removeobject (LOCALID_GYMNASIUM_HECTOR)
		call (Gymnasium_Script_HideHector)
		addobject (LOCALID_GYMNASIUM_HECTOR)
		//setflag
		setflag (FLAG_GYMNASIUM_HECTORLEFT)
		//inc var to accomodate - Hector Just left stuff
		addvar (GYMNASIUM_STATE, 1)
		//fin
		end
	}
}

script  Gymnasium_Script_HectorTrainer {
	switch (var(GYMNASIUM_STATE)) {
		case 0:
			msgbox ("ERROR\n GYMNASIUM_STATE OOB", MSGBOX_NPC)
		case 1:
			msgbox("ERROR\nGYMNASIUM_STATE OOB")
			closemessage
			end
		case 2:
			msgbox (Gymnasium_Text_HectorTrainer1, MSGBOX_NPC)
		case 3:
			msgbox (Gymnasium_Text_HectorTrainer2, MSGBOX_NPC)
		case 4:
			msgbox (Gymnasium_Text_HectorTrainer3, MSGBOX_NPC)
		default:
			msgbox ("ERROR\n GYMNASIUM_STATE OOB", MSGBOX_NPC)
	}
}

script  Gymnasium_Script_Protesilaus {
	switch (var(GYMNASIUM_STATE)) {
		case 0:
			msgbox ("ERROR\n GYMNASIUM_STATE OOB", MSGBOX_NPC)
		case 1:
			msgbox (Gymnasium_Text_Protesilaus1, MSGBOX_NPC)
		case 2:
			msgbox (Gymnasium_Text_Protesilaus2, MSGBOX_NPC)
		case 3:
			msgbox (Gymnasium_Text_Protesilaus3, MSGBOX_NPC)
		case 4:
			msgbox (Gymnasium_Text_Protesilaus4, MSGBOX_NPC)
		default:
			msgbox ("ERROR\n GYMNASIUM_STATE OOB", MSGBOX_NPC)
	}
}

script  Gymnasium_Script_ProtesilausTrainer {
	switch (var(GYMNASIUM_STATE)) {
		case 0:
			msgbox ("ERROR\n GYMNASIUM_STATE OOB", MSGBOX_NPC)
		case 1:
			msgbox (Gymnasium_Text_ProtesilausTrainer1, MSGBOX_NPC)
		case 2:
			msgbox (Gymnasium_Text_ProtesilausTrainer2, MSGBOX_NPC)
		case 3:
			msgbox (Gymnasium_Text_ProtesilausTrainer3, MSGBOX_NPC)
		case 4:
			msgbox (Gymnasium_Text_ProtesilausTrainer4, MSGBOX_NPC)
		default:
			msgbox ("ERROR\n GYMNASIUM_STATE OOB", MSGBOX_NPC)
	}
}

script  Gymnasium_Script_Chiron {
	switch (var(GYMNASIUM_STATE)) {
		case 0:
			msgbox ("ERROR\n GYMNASIUM_STATE OOB", MSGBOX_NPC)
		case 1:
			msgbox (Gymnasium_Text_Chiron1, MSGBOX_NPC)
		case 2:
			msgbox (Gymnasium_Text_Chiron2, MSGBOX_NPC)
		case 3:
			msgbox (Gymnasium_Text_Chiron3, MSGBOX_NPC)
		case 4:
			msgbox (Gymnasium_Text_Chiron4, MSGBOX_NPC)
		default:
			msgbox ("ERROR\n GYMNASIUM_STATE OOB", MSGBOX_NPC)
	}

}

text Gymnasium_Text_BlackBelt1 {
	format("This is the Gymnasium, home of the Grand Tournanment.")
}

text Gymnasium_Text_BlackBelt2 {
	format("I'm honored just to guard the door of this place.")
}

text Gymnasium_Text_BlackBelt3 {
	format("You don't look like most of the men who come to the Gymnasium to train under Chiron and fight in the Grand Tournament.")
}

text Gymnasium_Text_Hector1 {
	format("I've trained all my life for this tournanment.
			I'm ready. I will be the champion.")
}

text Gymnasium_Text_Hector_Wedding1 {
	format("What's that? You brought something to give to me?")
}

text Gymnasium_Text_Hector_Wedding2 {
	format("Wonderful news! My young brother, Paris,
			is staying as a guest at Nestor's house
			and is one of two candidates to wed
			Nestor's lovely daughter, Helen! I must
			attend. As much as I would like to stay,
			to win, to become the champion I
			have worked so hard to be, my family
			comes first. I will not act dishonorably
			by chasing vain glory and scorning the
			pious obligation to attend my
			brother's wedding. Even if he is not
			the groom, an invitation to the house
			of Nestor is honorable as well.
			Therefore, I lay down my bid for the wreath
			of laurel and my fight for victory.
			Farewell, all! I must be off for the house
			of Nestor to be with my dear Paris.")
}

text Gymnasium_Text_GiveInvitation {
    format("{PLAYER} gave the Wedding Invitation to Hector")
}

text Gymnasium_Text_Protesilaus1 {
	format("I'll beat anyone, any challenger!")
}

text Gymnasium_Text_Protesilaus2 {
	format("That man over there, Hector, he is much
			larger and confident than I assumed.")
}

text Gymnasium_Text_Protesilaus3 {
	format("Is he serious? Did he just withdraw?")
}

text Gymnasium_Text_Protesilaus4 {
	format("My years of training have finally paid off!
			Look here, kid, if you work hard all your life
			perhaps one day you too can be the champ.")
}

text Gymnasium_Text_Chiron1 {
	format("Hello, my name is Chiron I am the
			the grandmaster of the Gymnasium
			and preside over the Grand Tournement.")
}

text Gymnasium_Text_Chiron2 {
	format("The championship fight will soon begin.
			I have seen dozens of champions crowned
			since the days of my youth when I myself
			crowned champion with the laurel wreath.
			In all those years no fight has been so far
			mismatched as today between Hector and
			Protesilaus. Hector is by far
			stronger, faster, more noble, and more calm.
			This fight will likely not last two whole rounds.
			I'm sure Hector will win, but still they must
			compete to determine the champion.")
}

text Gymnasium_Text_Chiron3 {
	format("Never in all my days...Where did he go?")
}

text Gymnasium_Text_Chiron4 {
	format("The champion, Protesilaus, has
			not earned his laurel wreath by victory
			but on a technicality. Without
			an opponent to challenge him he is
			the champion by default. If I were
			only fifteen years younger, a far cry
			from the figher I was back in my youth,
			I'd challenge this default champion and win!")
}

text Gymnasium_Text_ProtesilausTrainer1 {
	format("Keep on your toes! Focus!")
}

text Gymnasium_Text_ProtesilausTrainer2 {
	format("Stay loose! We'll need agility to beat Hector.")
}

text Gymnasium_Text_ProtesilausTrainer3 {
	format("This must be some kind of ruse. Keep getting ready for battle!")
}

text Gymnasium_Text_ProtesilausTrainer4 {
	format("It's been a long journey with Protesilaus but I am so proud to see all his hard work pay off. Being a champion's trainer will make my services command a much higher premium as well.")
}
	
text Gymnasium_Text_HectorTrainer1 {
	format("We've trained all this time for this moment! You're ready! You're going to win!")
}

text Gymnasium_Text_HectorTrainer2 {
	format("What? Where is he going? Hector!")
}

text Gymnasium_Text_HectorTrainer3 {
	format("I can't believe it...disqualified...on a technicality... and now all that work just... gone...")
}
