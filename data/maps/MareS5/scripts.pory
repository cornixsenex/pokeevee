
//DYNAMIC MAP TEMP_VAR_5

//1: Mare Tropicum
//2: Acta Echona
//3: Isla Pina

const OBJ_EVENT_ARGO = 1

mapscripts MareS5_MapScripts {

    MAP_SCRIPT_ON_TRANSITION {

        //Handle Dynamic Map Trigger Setup
        specialvar (VAR_TEMP_5, GetDynamicMapSec_MareS5_F)
    }

	MAP_SCRIPT_ON_LOAD {
		if (var(VAR_ARGO_STATE) == 1) {
			call (MareS5_Script_SupressSurf)
		}
	}

	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_ARGO_STATE, 1: MareS5_Script_HidePlayer
	]

	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_ARGO_STATE, 1: MareS5_OnFrame_SailToColchis
	]

}

script MareS5_Script_Transition_IslaPina {
    setvar (VAR_TEMP_5, 3)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS5_Script_MareTropicumTransition {
    setvar (VAR_TEMP_5, 1)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS5_Script_ActaEchonaTransition {
    setvar (VAR_TEMP_5, 2)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS5_Script_SupressSurf {
	setmetatile (45, 45, METATILE_Slateport_CalmWater_False, FALSE)
	end
}

script MareS5_Script_HidePlayer {
	setobjectsubpriority (OBJ_EVENT_ID_PLAYER, MAP_MARE_S5, 0)
	hideobjectat (OBJ_EVENT_ID_PLAYER, MAP_MARE_S5)
	end
}

script MareS5_EventScript_Argo {
	lockall
	applymovement (OBJ_EVENT_ARGO, Common_Movement_ExclamationMark)
	waitmovement (0)
	msgbox(MareS5_Text_ArgoIntro1, MSGBOX_YESNO)
	closemessage
	if (var(VAR_RESULT) == YES) {
		warpsilent (MAP_ARGO, 16, 9)
		waitstate
	} else {
		msgbox(MareS5_Text_ArgoFarewell)
		closemessage
	}
	releaseall
	end
}

script MareS5_OnFrame_SailToColchis {
	lockall
	call (Common_EventScript_PlayBrineysBoatMusic)
	applymovement (OBJ_EVENT_ARGO, MareS5_Movement_SailToColchis1)
	applymovement (OBJ_EVENT_ID_PLAYER, MareS5_Movement_SailToColchis1)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
	msgbox (MareS5_Text_Scylla, MSGBOX_DEFAULT)
	closemessage
	showobjectat (OBJ_EVENT_ARGO, MAP_MARE_S4)
	hideobjectat (OBJ_EVENT_ARGO, MAP_MARE_S5)
	applymovement (OBJ_EVENT_ARGO, MareS5_Movement_SailToColchis2)
	applymovement (OBJ_EVENT_ID_PLAYER, MareS5_Movement_SailToColchis2)
	waitmovement (0)
	delay (15)
	call (BigIslandN_Script_ArgoArrival)
	releaseall
	end
}

movement MareS5_Movement_SailToColchisDirect {
	walk_faster_right * 293
	walk_faster_down * 58
}

movement MareS5_Movement_SailToColchis1 {
	walk_faster_right * 111
	walk_faster_down * 5
	walk_right
}

movement MareS5_Movement_SailToColchis2Totals {
	walk_faster_right * 180		
	walk_faster_down *  53
}

movement MareS5_Movement_SailToColchis2 {
	walk_faster_up * 8
	walk_faster_left * 3
	walk_faster_up * 30
	walk_faster_right * 184
	walk_faster_down * 91
}

movement MareS5_Movement_PlayerExitsBoat1 {
	walk_right
}

movement MareS5_Movement_PlayerExitsBoat2 {
	walk_down * 4
}

text MareS5_Text_ArgoIntro1 {
	format("Ahoy matey! If you be lost at sea feel free to join us!\nWill you board Argo?")
}

text MareS5_Text_ArgoFarewell {
	format("Farewell! Calm winds and gentle seas to you.")
}

text MareS5_Text_ArrivedAtColchis {
	format("We've landed on the north side of the big island we call Ignis. According to this map Colchis is located within a cave on the west side of the island. Orpheus, search for food and fresh water And Theseus inquire with the town whether the locals will give us shelter and open their markets to Argonauts. Castor and Pollux you will guard the ship while {PLAYER} and I and Philoctetes go investigate the rumored Colchis cave.")
}

text MareS5_Text_Scylla {
	format("Ahoy mateys! Weigh anchor! Drop your oars! A mighty monster, whirlpool, clashing rocks, lie in our path ahead. Turn back lest Argo wreck and we perish!")
}



