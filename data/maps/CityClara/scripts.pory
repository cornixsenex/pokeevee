
const FIELD_PIC_LEAF = 4

const OBJ_EVENT_JESSIE = 14
const OBJ_EVENT_JAMES = 15
const OBJ_EVENT_MEOWTH = 16
const OBJ_EVENT_MAN2 = 4
const OBJ_EVENT_FATMAN = 5
const OBJ_EVENT_WOMAN1 = 6
const OBJ_EVENT_GENTLEMAN = 9
const OBJ_EVENT_WOMAN5 = 8
const OBJ_EVENT_WOMAN3 = 10
const OBJ_EVENT_MANIAC = 11
const OBJ_EVENT_OLDWOMAN = 12
const OBJ_EVENT_LASS = 13

const OBJ_EVENT_GARDENER = 21


const OBJ_EVENT_ROCKETGRUNTM = 1
const OBJ_EVENT_ROCKETGRUNTF = 3

const OBJ_EVENT_SCOTT = 26
const OBJ_EVENT_LEAF = 18
const OBJ_EVENT_JIGGLYPUFF = 27

//DYNMAMIC MAP
//VAR_TEMP_5
//1: Acta Echona
//2: Silva Profunda
//3: Via Fidelium
//4: Sinus Clarus
//5: Castalia
//6: Delta Draci


mapscripts CityClara_MapScripts {

	MAP_SCRIPT_ON_TRANSITION {
		//Handle Dynamic Map Triggers
		specialvar (VAR_RESULT, GetDynamicMapSec_CityClara_F)
		if (var(VAR_RESULT) == 1) {
			setvar (VAR_TEMP_5, 1)
		} elif (var(VAR_RESULT) == 2) {
			setvar (VAR_TEMP_5, 2)
		} elif (var(VAR_RESULT) == 3) {
			setvar (VAR_TEMP_5, 3)
		} elif (var(VAR_RESULT) == 4) {
			setvar (VAR_TEMP_5, 4)
		} elif (var(VAR_RESULT) == 5) {
			setvar (VAR_TEMP_5, 5)
		} elif (var(VAR_RESULT) == 6) {
			setvar (VAR_TEMP_5, 6)
		}

        //Show Mewscene
        if (var(VAR_CITYCLARA_STATE) < 2) {
            call (CityClara_Script_ShowMewScene)
        }
   
        //Show Leaf et Scott
        if (var(VAR_CITYCLARA_STATE) == 0) {
            call (CityClara_Script_ShowLeafEtScott)
        } elif (var(VAR_CITYCLARA_STATE) == 1) {
            call (CityClara_Script_ShowLeaf)
        }

		//Open Garden
		if (flag(FLAG_CASTALIA_GARDEN_COMPLETE)) {
			call (CityClara_Script_OpenGarden)
		}
		if (flag(FLAG_TEMP_B)) {
			call (CityClara_Script_DuringGarden)
		}

		//Show Leaf after Church
		if (var(VAR_LEAF_ROAD_TO_ARDOR) == 1) {
			call (CityClara_Script_ShowLeafOnTheRoad)
		}

	}

	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_TEMP_9)) {
			removeobject (31)
		}
		if (flag(FLAG_TEMP_3)) {
			removeobject (32)
		}
		if (flag(FLAG_TEMP_A)) {
			removeobject (33)
		}
		if (flag(FLAG_TEMP_12)) {
			removeobject (34)
		}
		if (flag(FLAG_TEMP_D)) {
			removeobject (35)
		}
		if (flag(FLAG_TEMP_C)) {
			removeobject (36)
		}
	}
}

script CityClara_Script_ActaEchonaTransition {
	setvar (VAR_TEMP_5, 1)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script CityClara_Script_SilvaTransition {
	setvar (VAR_TEMP_5, 2)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script CityClara_Script_ViaFideliumTransition {
	setvar (VAR_TEMP_5, 3)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script CityClara_Script_SinusClarusTransition {
	setvar (VAR_TEMP_5, 4)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script CityClara_Script_CastaliaTransition {
	setvar (VAR_TEMP_5, 5)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script CityClara_Script_DeltaDraciTransition {
	setvar (VAR_TEMP_5, 6)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script  CityClara_Script_ShowMewScene {

    //Rockets
    setobjectxyperm (OBJ_EVENT_ROCKETGRUNTM, 62, 10)
    setobjectmovementtype (OBJ_EVENT_ROCKETGRUNTM, MOVEMENT_TYPE_FACE_DOWN)

    setobjectxyperm (OBJ_EVENT_ROCKETGRUNTF, 65, 10)
    setobjectmovementtype (OBJ_EVENT_ROCKETGRUNTF, MOVEMENT_TYPE_FACE_DOWN)
    //Jesse et James
    setobjectxyperm (OBJ_EVENT_JESSIE, 34, 22)
    setobjectmovementtype (OBJ_EVENT_JESSIE, MOVEMENT_TYPE_FACE_DOWN)

    setobjectxyperm (OBJ_EVENT_JAMES, 36, 22)
    setobjectmovementtype (OBJ_EVENT_JAMES, MOVEMENT_TYPE_FACE_DOWN)

    setobjectxyperm (OBJ_EVENT_MEOWTH, 35, 22)
    setobjectmovementtype (OBJ_EVENT_MEOWTH, MOVEMENT_TYPE_FACE_DOWN)
    //City Line
    setobjectxyperm (OBJ_EVENT_MAN2, 35, 23)
    setobjectmovementtype (OBJ_EVENT_MAN2, MOVEMENT_TYPE_FACE_UP)
    
    setobjectxyperm (OBJ_EVENT_FATMAN, 35, 24)
    setobjectmovementtype (OBJ_EVENT_FATMAN, MOVEMENT_TYPE_FACE_UP)
    
    setobjectxyperm (OBJ_EVENT_WOMAN1, 35, 25)
    setobjectmovementtype (OBJ_EVENT_WOMAN1, MOVEMENT_TYPE_FACE_UP)

    setobjectxyperm (OBJ_EVENT_GENTLEMAN, 35, 26)
    setobjectmovementtype (OBJ_EVENT_GENTLEMAN, MOVEMENT_TYPE_FACE_UP)

    setobjectxyperm (OBJ_EVENT_WOMAN5, 35, 27)
    setobjectmovementtype (OBJ_EVENT_WOMAN5, MOVEMENT_TYPE_FACE_UP)

    setobjectxyperm (OBJ_EVENT_WOMAN3, 35, 28)
    setobjectmovementtype (OBJ_EVENT_WOMAN3, MOVEMENT_TYPE_FACE_UP)
    
    return
}

script CityClara_Script_ShowLeafEtScott {
    setobjectxyperm (LOCALID_CASTALIA_SCOTT, 30, 37)
    setobjectmovementtype (LOCALID_CASTALIA_SCOTT, MOVEMENT_TYPE_FACE_DOWN)

    setobjectxyperm (LOCALID_CASTALIA_LEAF, 31, 37)
    setobjectmovementtype (LOCALID_CASTALIA_LEAF, MOVEMENT_TYPE_FACE_DOWN)

    setobjectxyperm (LOCALID_CASTALIA_JIGGLYPUFF, 32, 37)
    setobjectmovementtype (LOCALID_CASTALIA_JIGGLYPUFF, MOVEMENT_TYPE_FACE_DOWN)

    return
}

script CityClara_Script_ShowLeaf {
    setobjectxyperm (LOCALID_CASTALIA_LEAF, 32, 31)
    setobjectmovementtype (LOCALID_CASTALIA_LEAF, MOVEMENT_TYPE_FACE_DOWN)

    setobjectxyperm (LOCALID_CASTALIA_JIGGLYPUFF, 31, 31)
    setobjectmovementtype (LOCALID_CASTALIA_JIGGLYPUFF, MOVEMENT_TYPE_FACE_DOWN)

    return
}

script CityClara_Script_OpenGarden {	
	//move gardener
	setobjectxyperm (OBJ_EVENT_GARDENER, 47, 40)
	setobjectmovementtype (OBJ_EVENT_GARDENER, MOVEMENT_TYPE_FACE_UP)

	//remove pests
	setobjectxyperm (28, 0, 0)
	setobjectmovementtype (28, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (29, 0, 0)
	setobjectmovementtype (28, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (30, 0, 0)
	setobjectmovementtype (28, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (31, 0, 0)
	setobjectmovementtype (28, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (32, 0, 0)
	setobjectmovementtype (28, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (33, 0, 0)
	setobjectmovementtype (28, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (34, 0, 0)
	setobjectmovementtype (28, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (35, 0, 0)
	setobjectmovementtype (28, MOVEMENT_TYPE_INVISIBLE)

	setobjectxyperm (36, 0, 0)
	setobjectmovementtype (28, MOVEMENT_TYPE_INVISIBLE)

	return
}

script CityClara_Script_ShowLeafOnTheRoad {
	setobjectxyperm (LOCALID_CASTALIA_LEAF, 64, 13)
	setobjectmovementtype (LOCALID_CASTALIA_LEAF, MOVEMENT_TYPE_FACE_DOWN)

	setobjectxyperm (LOCALID_CASTALIA_JIGGLYPUFF, 63, 13)
	setobjectmovementtype (LOCALID_CASTALIA_JIGGLYPUFF, MOVEMENT_TYPE_FACE_DOWN)

	return
}



script CityClara_Script_DuringGarden {
	setobjectxyperm (OBJ_EVENT_GARDENER, 48, 40)
	setobjectmovementtype (OBJ_EVENT_GARDENER, MOVEMENT_TYPE_FACE_UP)
	
	return
}

script CityClara_Script_LeafEtScottSceneA {
    lockall
    applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement (0)
    call (CityClara_Script_LeafEtScottScene)

    releaseall
    end
}

script CityClara_Script_LeafEtScottSceneB {
    lockall
    applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
    waitmovement (0)
    applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
    waitmovement (0)
    call (CityClara_Script_LeafEtScottScene)

    releaseall
    end
}

script CityClara_Script_LeafEtScottScene {
    //Move player to position
    applymovement (OBJ_EVENT_ID_PLAYER, CityClara_Movement_LeafEtScottScene_Player)
    waitmovement (0)
    //Leaf: Long lines
	load_field_pic (FIELD_PIC_LEAF, 190, 98)
	msgbox (CityClara_Text_Leaf1)
	destroy_field_pic (FIELD_PIC_LEAF)
    //Scott: No use waiting around all day come find me at the bar I'll buy you a drink and we'll wait this out in comfort
	load_field_pic (FIELD_PIC_SCOTT, 190, 98)
	msgbox (CityClara_Text_Scott1)
	destroy_field_pic (FIELD_PIC_SCOTT)
    closemessage
    //Exit Scott
    applymovement (LOCALID_CASTALIA_SCOTT, CityClara_Movement_LeafEtScottScene_ScottExit)
    waitmovement (0) 
    //remove scott
    removeobject (LOCALID_CASTALIA_SCOTT)
    setobjectxyperm (LOCALID_CASTALIA_SCOTT, 0, 0)
    setobjectmovementtype (LOCALID_CASTALIA_SCOTT, MOVEMENT_TYPE_INVISIBLE)
    addobject (LOCALID_CASTALIA_SCOTT)
    //L: I won't wait we need to find a way inside the church to see Mew
	load_field_pic (FIELD_PIC_LEAF, 190, 98)
	msgbox (CityClara_Text_Leaf2)
	destroy_field_pic (FIELD_PIC_LEAF)
    closemessage
    //Exit Leaf
    applymovement (LOCALID_CASTALIA_LEAF, CityClara_Movement_LeafEtScottScene_LeafExit)
    applymovement (LOCALID_CASTALIA_JIGGLYPUFF, CityClara_Movement_LeafEtScottScene_LeafExit)
    waitmovement (0) 
    //reset Leaf et Jig location
    removeobject (LOCALID_CASTALIA_LEAF)
    setobjectxyperm (LOCALID_CASTALIA_LEAF, 32, 31)
    setobjectmovementtype (LOCALID_CASTALIA_LEAF, MOVEMENT_TYPE_FACE_DOWN)
    addobject (LOCALID_CASTALIA_LEAF)
    showobjectat (LOCALID_CASTALIA_LEAF, MAP_CITY_CLARA)

    removeobject (LOCALID_CASTALIA_JIGGLYPUFF)
    setobjectxyperm (LOCALID_CASTALIA_JIGGLYPUFF, 31, 31)
    setobjectmovementtype (LOCALID_CASTALIA_JIGGLYPUFF, MOVEMENT_TYPE_FACE_DOWN)
    addobject (LOCALID_CASTALIA_JIGGLYPUFF)
    showobjectat (LOCALID_CASTALIA_JIGGLYPUFF, MAP_CITY_CLARA)
    //setup vars
    setvar (VAR_CITYCLARA_STATE, 1)

    releaseall
    end
}


script CityClara_EventScript_ClubSign {	
	lock
	msgbox (CityClara_Text_ClubSign)
	closemessage
	release
	end
}

script CityClara_EventScript_CannaWoman {
	lockall
	faceplayer
	msgbox (CityClara_Text_CannaWoman)
	closemessage
	releaseall
	end
}

script CityClara_EventScript_Man2 {
	lock
	faceplayer
	if (var(VAR_CITYCLARA_STATE) > 2) {
		msgbox (CityClara_Text_GreenSuit2)
	} 
	else {
		msgbox (CityClara_Text_GreenSuit)
	}
	closemessage
	release
	end
}

script CityClara_EventScript_FatMan {
	lock
	faceplayer
	if (var(VAR_CITYCLARA_STATE) > 2) {
		msgbox (CityClara_Text_FatMan2)
	} 
	else {
		msgbox (CityClara_Text_FatMan)
	}
	closemessage
	release
	end
}

script CityClara_EventScript_Woman1 {
	lock
	faceplayer
	if (var(VAR_CITYCLARA_STATE) > 2) {
		msgbox (CityClara_Text_WomanA2)
	} 
	else {
		msgbox (CityClara_Text_WomanA)
	}
	closemessage
	release
	end
}

script CityClara_EventScript_Woman5 {
	lock
	faceplayer
	if (var(VAR_CITYCLARA_STATE) > 2) {
		msgbox (CityClara_Text_RedWoman2)
	} 
	else {
		msgbox (CityClara_Text_RedWoman)
	}
	closemessage
	release
	end
}

script CityClara_EventScript_Woman3 {
	lock
	faceplayer
	if (var(VAR_CITYCLARA_STATE) > 2) {
		msgbox (CityClara_Text_Woman3_2)
	} 
	else {
		msgbox (CityClara_Text_Woman3_1)
	}
	closemessage
	release
	end
}

script CityClara_EventScript_Gentleman {
	lock
	faceplayer
	if (var(VAR_CITYCLARA_STATE) > 2) {
		msgbox (CityClara_Text_OldMan2)
	} 
	else {
		msgbox (CityClara_Text_OldMan)
	}
	closemessage
	release
	end
}

script CityClara_EventScript_OldWoman {
	lock
	faceplayer
	if (var(VAR_CITYCLARA_STATE) > 2) {
		msgbox (CityClara_Text_OldWoman2)
	} 
	else {
		msgbox (CityClara_Text_OldWoman)
	}
	closemessage
	release
	end
}

//NOTE: Fix to accomodate the garden thing
script CityClara_EventScript_Lass {
	lock
	faceplayer
	if (var(VAR_CITYCLARA_STATE) > 2) {
		msgbox (CityClara_Text_Lass2)
	} 
	else {
		msgbox (CityClara_Text_Lass)
	}
	closemessage
	release
	end
}

//TEMP_FLAG_B = Inside the garden / during garden scene
script CityClara_Script_Gardener {
    lock
    faceplayer
    //during garden scene
    if (flag(FLAG_TEMP_B)) {
        //Check if prerequisites complete -> finish garden scene
		if (var(VAR_TEMP_9) == 9) {
			call (CityClara_Script_FinishGarden)
		} else {
			msgbox (CityClara_Text_Gardener_DuringGarden)
			closemessage 
			applymovement (OBJ_EVENT_GARDENER, Common_Movement_FaceUp)
			waitmovement (0)
		}
	}
    //After garden scene
    elif (flag(FLAG_RECEIVED_SCYTHE)) {
        msgbox (CityClara_Text_Gardener_AfterGarden)
        closemessage
    //Before Garden Scene
    } else {
        msgbox (CityClara_Text_Gardener_BeforeGardenIntro)
        applymovement (OBJ_EVENT_GARDENER, Common_Movement_ExclamationMark)
        waitmovement (0)
        msgbox (CityClara_Text_Gardener_BeforeGardenBeginYN, MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO) {
            msgbox (CityClara_Text_Gardener_BeforeGardenDecline)
            closemessage
        } else {
            //setflag to show during garden
            setflag (FLAG_TEMP_B)
            //Begin Garden scene - G: Come with me
            msgbox (CityClara_Text_Gardener_BeforeGardenBegin)
            //Move into position: Player facing left at gardener in garden
            applymovement (OBJ_EVENT_ID_PLAYER, CityClara_Movement_PlayerBeginGarden1)
            applymovement (OBJ_EVENT_GARDENER, CityClara_Movement_GardenerBeginGarden1)
            waitmovement (0)
            //G: Here take this to cut down the trees and be careful of the bugs
            msgbox (CityClara_Text_Gardener_BeginGarden1)
            closemessage
            //Give player scythe
			playfanfare (MUS_OBTAIN_ITEM)
			giveitem (ITEM_SCYTHE, 1)
            //Set flag has scythe
            setflag (FLAG_RECEIVED_SCYTHE)
            //G: I know you can do it
            msgbox (CityClara_Text_Gardener_BeginGarden2)
            closemessage
            //Move into block
            applymovement (OBJ_EVENT_GARDENER, Common_Movement_WalkRight)
            applymovement (OBJ_EVENT_ID_PLAYER, CityClara_Movement_PlayerBeginGarden2)
            waitmovement (0)
            applymovement (OBJ_EVENT_GARDENER, Common_Movement_FaceUp)
            waitmovement (0)
			//persist block	
			call (CityClara_Script_DuringGarden)
        }
    }
    release
    end
}

script CityClara_Script_FinishGarden {
	//Set gardener to not during garden state temp flag
	clearflag (FLAG_TEMP_B)
	//She reacts
	applymovement (OBJ_EVENT_GARDENER, Common_Movement_ExclamationMark)
	waitmovement (0)
	//G: You did it thank you as a reward please have my Scythe
	msgbox (CityClara_Text_Gardener_FinishGarden1)
	closemessage
	//Switch on player direction to exit player
	specialvar (VAR_RESULT, GetPlayerFacingDirection)
	switch (var(VAR_RESULT)) {
		case DIR_NORTH:
			msgbox("VAR_RESULT DIR_NORTH")
			closemessage
		case DIR_WEST:
			applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
		    applymovement (OBJ_EVENT_GARDENER, Common_Movement_WalkLeft)
			waitmovement (0)
			applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
		    applymovement (OBJ_EVENT_GARDENER, Common_Movement_WalkRight)
			waitmovement (0)
			applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
		    applymovement (OBJ_EVENT_GARDENER, Common_Movement_FaceDown)
			waitmovement (0)
		case DIR_SOUTH:
			applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
		    applymovement (OBJ_EVENT_GARDENER, Common_Movement_WalkLeft)
			waitmovement (0)
			applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
		    applymovement (OBJ_EVENT_GARDENER, Common_Movement_WalkRight)
			waitmovement (0)
			applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
		    applymovement (OBJ_EVENT_GARDENER, Common_Movement_FaceDown)
			waitmovement (0)
		case DIR_EAST:
			applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
		    applymovement (OBJ_EVENT_GARDENER, Common_Movement_WalkRight)
			waitmovement (0)
			applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
		    applymovement (OBJ_EVENT_GARDENER, Common_Movement_WalkLeft)
			waitmovement (0)
			applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
		    applymovement (OBJ_EVENT_GARDENER, Common_Movement_FaceDown)
			waitmovement (0)
		default:
			msgbox("VAR_RESULT OOB")
			closemessage
	}
	//She blocking facing down: Come back any time
	msgbox (CityClara_Text_Gardener_FinishGarden2)
	closemessage
	//She move into position
	applymovement (OBJ_EVENT_GARDENER, CityClara_Movement_GardenerFinishGarden)
	waitmovement (0)
	//set clear garden flag
	setflag (FLAG_CASTALIA_GARDEN_COMPLETE)
	//Persist new location
	call (CityClara_Script_OpenGarden)
	release
	end
}

script CityClara_EventScript_Leaf {
	lock
	faceplayer
	if (var(VAR_LEAF_ROAD_TO_ARDOR) < 1) {
		call (CityClara_EventScript_LeafBridge)
	} else {
		call (CityClara_EventScript_LeafOnTheRoad)
	}
	release
	end
}

script CityClara_EventScript_LeafBridge {
	load_field_pic (4, 190, 98)
	msgbox (CityClara_Text_LeafBridge)
	destroy_field_pic (4)
	closemessage
	applymovement (LOCALID_CASTALIA_LEAF, Common_Movement_FaceDown)
	waitmovement (0)
	releaseall
	end
}

script CityClara_EventScript_LeafOnTheRoad {
	setvar (VAR_LEAF_ROAD_TO_ARDOR, 2)
	load_field_pic (4, 190, 98)
	msgbox (CityClara_Text_LeafOnTheRoad)
	destroy_field_pic (4)
	closemessage
	applymovement (LOCALID_CASTALIA_LEAF, Common_Movement_WalkUp6)
	applymovement (LOCALID_CASTALIA_JIGGLYPUFF, Common_Movement_WalkUp6)
	waitmovement (0)
	//remove Leaf et Jig
	removeobject (LOCALID_CASTALIA_LEAF)
	setobjectxyperm (LOCALID_CASTALIA_LEAF, 0, 0)
	setobjectmovementtype (LOCALID_CASTALIA_LEAF, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_CASTALIA_LEAF)
	showobjectat (LOCALID_CASTALIA_LEAF, MAP_CITY_CLARA)
	removeobject (LOCALID_CASTALIA_JIGGLYPUFF)
	setobjectxyperm (LOCALID_CASTALIA_JIGGLYPUFF, 0, 0)
	setobjectmovementtype (LOCALID_CASTALIA_JIGGLYPUFF, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_CASTALIA_JIGGLYPUFF)
	showobjectat (LOCALID_CASTALIA_JIGGLYPUFF, MAP_CITY_CLARA)
	releaseall
	end
}

script CityClara_EventScript_TownSign {	
	lock
	msgbox (CityClara_Text_TownSign)
	closemessage
	release
	end
}

script CityClara_EventScript_Senex {
	lockall
	faceplayer
	msgbox (CityClara_Text_Senex1, MSGBOX_YESNO)
	if (var(VAR_RESULT) == YES) {
		msgbox (CityClara_Text_Senex2)
	}
	else {
		msgbox (CityClara_Text_Senex3)
	}
	closemessage
	releaseall
	end
}

script CityClara_EventScript_Jessie {
	lockall
	faceplayer
	msgbox (CityClara_Text_Jessie)
	closemessage	
	applymovement (14, Common_Movement_FaceDown)
	waitmovement (0)
	releaseall
	end
}

script CityClara_EventScript_James {
	lockall
	faceplayer
	msgbox (CityClara_Text_James)
	closemessage	
	applymovement (15, Common_Movement_FaceDown)
	waitmovement (0)
	releaseall
	end
}

script CityClara_EventScript_RocketBlockA {
	lock
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	msgbox (CityClara_Text_RocketBlock1)
	closemessage
	waitmovement (0)
	applymovement (1, CityClara_Movement_RocketBlock1A)
	applymovement (3, CityClara_Movement_RocketBlock2A)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, CityClara_Movement_PlayerFaceLeft)
	waitmovement (0)
	msgbox (CityClara_Text_RocketBlock2)
	applymovement (OBJ_EVENT_ID_PLAYER, CityClara_Movement_PlayerFaceRight)
	waitmovement (0)
	msgbox (CityClara_Text_RocketBlock3)
	closemessage
	applymovement (1, CityClara_Movement_RocketReturn1A)
	applymovement (3, CityClara_Movement_RocketReturn2A)
	applymovement (OBJ_EVENT_ID_PLAYER, CityClara_Movement_PlayerReturn)
	waitmovement (0)
	release
}

script CityClara_EventScript_RocketBlockB {
	lock
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	msgbox (CityClara_Text_RocketBlock1)
	closemessage
	waitmovement (0)
	applymovement (1, CityClara_Movement_RocketBlock1B)
	applymovement (3, CityClara_Movement_RocketBlock2B)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, CityClara_Movement_PlayerFaceRight)
	waitmovement (0)
	msgbox (CityClara_Text_RocketBlock2)
	applymovement (OBJ_EVENT_ID_PLAYER, CityClara_Movement_PlayerFaceLeft)
	waitmovement (0)
	msgbox (CityClara_Text_RocketBlock3)
	closemessage
	applymovement (1, CityClara_Movement_RocketReturn1B)
	applymovement (3, CityClara_Movement_RocketReturn2B)
	applymovement (OBJ_EVENT_ID_PLAYER, CityClara_Movement_PlayerReturn)
	waitmovement (0)
	release
}

script CityClara_EventScript_Rocket1 {
	lock
	faceplayer
	msgbox(CityClara_Text_Rocket1)
	closemessage
	release
}

script CityClara_EventScript_Rocket2 {
	lock
	faceplayer
	msgbox(CityClara_Text_Rocket2)
	closemessage
	release
}

script CityClara_Script_GardenTree {
	call (CityClara_Script_IncrementGardenVar)
    call (EventScript_ScytheTree)
	end
}

script CityClara_Script_Nincada {
	lock
	faceplayer
	call (CityClara_Script_IncrementGardenVar)
	playmoncry (SPECIES_NINCADA, CRY_MODE_ENCOUNTER)
	waitmoncry
	setwildbattle (SPECIES_NINCADA, 23)
	setflag (FLAG_TEMP_9)
	dowildbattle
	release
	end
}

script CityClara_Script_Weedle {
	lock
	faceplayer
	call (CityClara_Script_IncrementGardenVar)
	playmoncry (SPECIES_WEEDLE, CRY_MODE_ENCOUNTER)
	waitmoncry
	setwildbattle (SPECIES_WEEDLE, 23)
	setflag (FLAG_TEMP_C)
	dowildbattle
	release
	end
}

script CityClara_Script_Wurmple{
	lock
	faceplayer
	call (CityClara_Script_IncrementGardenVar)
	playmoncry (SPECIES_WURMPLE, CRY_MODE_ENCOUNTER)
	waitmoncry
	setwildbattle (SPECIES_WURMPLE, 23)
	setflag (FLAG_TEMP_3)
	dowildbattle
	release
	end
}

script CityClara_Script_Caterpie {
	lock
	faceplayer
	call (CityClara_Script_IncrementGardenVar)
	playmoncry (SPECIES_CATERPIE, CRY_MODE_ENCOUNTER)
	waitmoncry
	setwildbattle (SPECIES_CATERPIE, 23)
	setflag (FLAG_TEMP_12)
	dowildbattle
	release
	end
}

script CityClara_Script_Paras {
	lock
	faceplayer
	call (CityClara_Script_IncrementGardenVar)
	playmoncry (SPECIES_PARAS, CRY_MODE_ENCOUNTER)
	waitmoncry
	setwildbattle (SPECIES_PARAS, 23)
	setflag (FLAG_TEMP_A)
	dowildbattle
	release
	end
}

script CityClara_Script_Venonat {
	lock
	faceplayer
	call (CityClara_Script_IncrementGardenVar)
	playmoncry (SPECIES_VENONAT, CRY_MODE_ENCOUNTER)
	waitmoncry
	setwildbattle (SPECIES_VENONAT, 23)
	setflag (FLAG_TEMP_D)
	dowildbattle
	release
	end
}

//Track Garden Progress - VAR_TEMP_9
script CityClara_Script_IncrementGardenVar {
	switch (var(VAR_TEMP_9)) {
		case 0:
			setvar (VAR_TEMP_9, 1)
		case 1:
			setvar (VAR_TEMP_9, 2)
		case 2:
			setvar (VAR_TEMP_9, 3)
		case 3:
			setvar (VAR_TEMP_9, 4)
		case 4:
			setvar (VAR_TEMP_9, 5)
		case 5:
			setvar (VAR_TEMP_9, 6)
		case 6:
			setvar (VAR_TEMP_9, 7)
		case 7:
			setvar (VAR_TEMP_9, 8)
		case 8:
			setvar (VAR_TEMP_9, 9)
		default:
			msgbox("VAR_TEMP_9 OOB")
			closemessage
	}
	return
}	

movement CityClara_Movement_RocketBlock1A {
	walk_down
	face_right
}

movement CityClara_Movement_RocketBlock2A {
	walk_down
	walk_left
}

movement CityClara_Movement_PlayerFaceLeft {
	face_left
}

movement CityClara_Movement_PlayerFaceRight {
	face_right
}

movement CityClara_Movement_RocketReturn1A {
	walk_up
	face_down
}

movement CityClara_Movement_RocketReturn2A {
	walk_right
	walk_up
	face_down
}

movement CityClara_Movement_PlayerReturn {
	walk_down * 2
}

movement CityClara_Movement_RocketBlock1B {
	walk_down
	walk_right
}

movement CityClara_Movement_RocketBlock2B {
	walk_down
	face_left
}

movement CityClara_Movement_RocketReturn1B {
	walk_left
	walk_up
	face_down
}

movement CityClara_Movement_RocketReturn2B {
	walk_up
	face_down
}

movement CityClara_Movement_LeafEtScottScene_Player {
    walk_right * 3
    walk_up
}

movement CityClara_Movement_LeafEtScottScene_ScottExit {
    walk_down * 3
    walk_left * 7
}

movement CityClara_Movement_LeafEtScottScene_LeafExit {
    walk_up * 5
}

movement CityClara_Movement_PlayerBeginGarden1 {
    walk_up * 2
    face_left
}

movement CityClara_Movement_GardenerBeginGarden1 {
    walk_up
    walk_left
    face_right
}

movement CityClara_Movement_PlayerBeginGarden2 {
    walk_right
    face_left
}

movement CityClara_Movement_GardenerFinishGarden {
	walk_left
	face_up
}

text CityClara_Text_ClubSign {
	format("The Chronic\nPremium Cannabis Club")
}

text CityClara_Text_TownSign {
	format("City Saeclara built upon the lake")
}

text CityClara_Text_CannaWoman {
	format("You see this building here? The Chronic Club? They got me so high I can barely walk!")
}

text CityClara_Text_LeafBridge {
	format("This line is so long and it's not moving! We need to find a way inside, and fast.")
}

text CityClara_Text_GreenSuit {
	format("I'm first in line because I payed for it.")
}

text CityClara_Text_GreenSuit2 {
	format("I trust the Party to take care of Mew.")
}

text CityClara_Text_FatMan {
	format("I'm first in line because I payed for it.")
}

text CityClara_Text_FatMan2 {
	format("I trust the Party to take care of Mew.")
}

text CityClara_Text_WomanA {
	format("I'm first in line because I payed for it.")
}

text CityClara_Text_WomanA2 {
	format("I trust the Party to take care of Mew.")
}

text CityClara_Text_RedWoman {
	format("I'm first in line because I payed for it.")
}

text CityClara_Text_RedWoman2 {
	format("I trust the Party to take care of Mew.")
}

text CityClara_Text_OldMan {
	format("I'm first in line because I payed for it.")
}

text CityClara_Text_OldMan2 {
	format("I trust the Party to take care of Mew.")
}

text CityClara_Text_OldWoman {
	format("Is Mew really here? Am I really here? This life feels like reflections on water...")
}

text CityClara_Text_OldWoman2 {
	format("Am I still here, or am I an echo?")
}

text CityClara_Text_Lass {
	format("I want to go play in the garden but the gardener says it's dangerous now.")
}

text CityClara_Text_Lass2 {
	format("I trust the Party to take care of Mew.")
}

text CityClara_Text_Senex1 {
	format("My boy come here, or no, are you a girl? Forgive me for my eyes are old as I have grown. Tell me would you ask me to speak and tell to you the tale of this city?")
}

text CityClara_Text_Senex2 {
	format("This place is ancient, older than I am. They say that people first lived on the lake thousands of years ago. Escpaing drought the elder race escaped across the sea and here observed a certain sign of fate. Upon the crystal clear basin surface a Charizard devoured it's own tail. The elders knew their fate: To build a town upon the lake in reverence of their god. And now thousands of years in the future is it coincedance that Mew is here?")
}

text CityClara_Text_Senex3 {
	format("No one has time for history these days...")
}

text CityClara_Text_RocketBlock1 {
	format("Hey You!")
}

text CityClara_Text_RocketBlock2 {
	format("Stop right there! Take not one more stop. This road is closed, we shall let no one pass.")
}

text CityClara_Text_RocketBlock3 {
	format("By order of the Rocket High Command only party business will be allowed until our mission has been completed. Now get thee gone, noone wants more trouble")
}

text CityClara_Text_Rocket1 {
	format("The Rocket Party helped me buy a home! Standing watch on the road's the very least I can do to pay back our dear leader.")
}

text CityClara_Text_Rocket2 {
	format("The party only wants the very best people to represent their interests. Perhaps with time, and practice, and some luck, you could one day wear our great uniform. Never forget the party's here for you.")
}

text CityClara_Text_Jessie {
	format("This church is being occupied by the Rocket Party and we're watching the door. You'll have to get in line and wait your turn then pay the entrance fee to step inside.")
}

text CityClara_Text_James {
	format("This town is so antique it's comical. Look at all these people waiting in line just for a glimpse of some old pokemon. The city is a better place to live and party people are superior.")
}

text CityClara_Text_Leaf1 {
    format("Looks like we weren't the only ones who came to Castalia to catch a glimpse of Mew. The line to enter church is super long!")
}

text CityClara_Text_Scott1 {
    format("It's not moving either. Those government stooges have it blocked up! If you ask me it's no use waiting in that line right now. We're better off sitting in comfort at the Lotus Club until the line dies down. That's where I'm going! Why don't you join me? I'll buy us all Lotus and let time pass.")
}

text CityClara_Text_Leaf2 {
    format("There's no way after traveling out here that we're going to go sit in a club while Mew herself awaits us in that Church! Come with me {PLAYER} we need to find a way to get into that Church ASAP!")
}

text CityClara_Text_Woman3_1 {
    format("The line is long, but the wait is worth it.")
}

text CityClara_Text_Woman3_2 {
    format("I can't believe how fast time's flying by! I still feel like that same little school girl...")
}

text CityClara_Text_Gardener_BeforeGardenIntro {
    format("Oh no! My garden has been overrun by pests and weeds! I don't know what to do!")
}

text CityClara_Text_Gardener_BeforeGardenBeginYN {
    format("Oh hey! You're a young, fit, capable boy. Will you help me clean up the garden? Please?")
}

text CityClara_Text_Gardener_BeforeGardenDecline {
    format("I understand. It's not your job. Take care.")
}

text CityClara_Text_Gardener_BeforeGardenBegin {
    format("Thank you so much! Let me show you inside.")
}

text CityClara_Text_Gardener_BeginGarden1 {
    format("Welcome to the Castalia Garden! In better times we grow beautiful things but now the garden looks like such a mess. Please get rid of the pests that have moved in and use this Scythe to cut down the small trees.")
}

text CityClara_Text_Gardener_BeginGarden2 {
    format("Now get to work! Good luck! You can do it!")
}

text CityClara_Text_Gardener_DuringGarden {
    format("You're not done yet. The garden's still a mess!")
}

text CityClara_Text_Gardener_AfterGarden {
    format("Thank you again for cleaining the garden.")
}

text CityClara_Text_Gardener_FinishGarden1 {
	format("You did it! You cut down every weed and fully resolved the infestation! As thanks I want you to keep that Sharp Scythe.")
}

text CityClara_Text_Gardener_FinishGarden2 {
	format("Come back and visit the garden later.")
}

text CityClara_Text_LeafOnTheRoad {
	format("The way to Deseret is not direct. The fastest was is north: Fidelium up to Robustica. The road curves around the Pacific Sine and then diverges at Litoralis. Take the western path and stay to the right and you should arrive in Robustica. Once there we will arrange transportation along Via Magna to Deseret. There is no time to lose. C'mon. Let's go!")
}
