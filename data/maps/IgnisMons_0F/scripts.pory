
const OBJ_EVENT_PHIL = 1
const OBJ_EVENT_DRIFBLIM_A = 8
const OBJ_EVENT_DRIFBLIM_B = 9


mapscripts IgnisMons_0F_MapScripts {


	MAP_SCRIPT_ON_TRANSITION {
		if (var(VAR_ARGO_STATE) < 4) {
			call (IgnisMons_0F_Script_ShowAmbush)
		} else {
			call (IgnisMons_0F_Script_HideAmbush)
		}
	}
	
	MAP_SCRIPT_ON_RESUME {
		call (IgnisMons_0F_Script_OnResume)

	}
}

script IgnisMons_0F_Script_OnResume {
	setstepcallback (STEP_CB_FALSE_FLOOR)
	switch (var(VAR_IGNISMONS_STATE)) {
		case 0:
			call (IgnisMons_0F_Script_SetBarriers_Init)
		case 1:
			call (IgnisMons_0F_Script_SetBarriers_Red)
		case 2:
			call (IgnisMons_0F_Script_SetBarriers_Blue)
		case 3:
			call (IgnisMons_0F_Script_SetBarriers_Green)
		default:
			call (IgnisMons_0F_Script_SetBarriers_Init)
	}
	end
}

script IgnisMons_0F_Script_ShowAmbush {
	setobjectxyperm (OBJ_EVENT_PHIL, 15, 20)
	setobjectmovementtype (OBJ_EVENT_PHIL, MOVEMENT_TYPE_FACE_LEFT)
	addobject (OBJ_EVENT_PHIL)
	showobjectat (OBJ_EVENT_PHIL, MAP_IGNIS_MONS_0F)
	end
}

script IgnisMons_0F_Script_HideAmbush {
	setobjectxyperm (OBJ_EVENT_DRIFBLIM_A, 8, 15)
	setobjectmovementtype (OBJ_EVENT_DRIFBLIM_A, MOVEMENT_TYPE_WANDER_AROUND)
	addobject (OBJ_EVENT_DRIFBLIM_A)
	showobjectat (OBJ_EVENT_DRIFBLIM_A, MAP_IGNIS_MONS_0F)

	setobjectxyperm (OBJ_EVENT_DRIFBLIM_B, 12, 20)
	setobjectmovementtype (OBJ_EVENT_DRIFBLIM_B, MOVEMENT_TYPE_WANDER_AROUND)
	addobject (OBJ_EVENT_DRIFBLIM_B)
	showobjectat (OBJ_EVENT_DRIFBLIM_B, MAP_IGNIS_MONS_0F)

	end
}

script IgnisMons_0F_Script_SetBarriers_Init {
	call (IgnisMons_0F_Script_ShowRed)
	call (IgnisMons_0F_Script_ShowBlue)
	call (IgnisMons_0F_Script_ShowGreen)

	return
}

script IgnisMons_0F_Script_RedButton {
	lockall
	if (var(VAR_IGNISMONS_STATE) != 1) {	
		setvar (VAR_IGNISMONS_STATE, 1)
		playse (SE_FU_ZAKU)
		call (IgnisMons_0F_Script_SetBarriers_Red)
		special (DrawWholeMapView)
	}	
    releaseall
	end
}

script IgnisMons_0F_Script_BlueButton {
	lockall
	if (var(VAR_IGNISMONS_STATE) != 2) {	
		setvar (VAR_IGNISMONS_STATE, 2)
		playse (SE_FU_ZAKU)
		call (IgnisMons_0F_Script_SetBarriers_Blue)
		special (DrawWholeMapView)
	}
	releaseall
	end
}

script IgnisMons_0F_Script_GreenButton {
	lockall
	if (var(VAR_IGNISMONS_STATE) != 3) {	
		setvar (VAR_IGNISMONS_STATE, 3)
		playse (SE_FU_ZAKU)
		call (IgnisMons_0F_Script_SetBarriers_Green)
		special (DrawWholeMapView)
	}
	releaseall
	end
}

script IgnisMons_0F_Script_SetBarriers_Red {
	call (IgnisMons_0F_Script_ShowBlue)
	call (IgnisMons_0F_Script_ShowGreen)

	call (IgnisMons_0F_Script_HideRed)

	return
}

script IgnisMons_0F_Script_SetBarriers_Blue {
	call (IgnisMons_0F_Script_ShowRed)
	call (IgnisMons_0F_Script_ShowGreen)

	call (IgnisMons_0F_Script_HideBlue)

	return
}

script IgnisMons_0F_Script_SetBarriers_Green {
	call (IgnisMons_0F_Script_ShowRed)
	call (IgnisMons_0F_Script_ShowBlue)

	call (IgnisMons_0F_Script_HideGreen)

	return
}

script IgnisMons_0F_Script_ShowRed {
	setmetatile (3, 8, METATILE_IgnisMons_Barrier_Red_Shadow, TRUE)
	setmetatile (4, 9, METATILE_IgnisMons_Barrier_Red, TRUE)
	setmetatile (9, 10, METATILE_IgnisMons_Barrier_Red_Shadow, TRUE)
	setmetatile (10, 11, METATILE_IgnisMons_Barrier_Red, TRUE)
	setmetatile (3, 12, METATILE_IgnisMons_Barrier_Red_Shadow, TRUE)
	setmetatile (4, 13, METATILE_IgnisMons_Barrier_Red, TRUE)
	setmetatile (17, 36, METATILE_IgnisMons_Barrier_Red, TRUE)
	setmetatile (18, 37, METATILE_IgnisMons_Barrier_Red_Shadow, TRUE)

	setmetatile (12, 8, METATILE_IgnisMons_Button_Red, FALSE)
	setmetatile (1, 11, METATILE_IgnisMons_Button_Red_Shadow, FALSE)
	setmetatile (17, 21, METATILE_IgnisMons_Button_Red, FALSE)
	setmetatile (2, 40, METATILE_IgnisMons_Button_Red, FALSE)

	return
}

script IgnisMons_0F_Script_ShowBlue {
	setmetatile (11, 16, METATILE_IgnisMons_Barrier_Blue, TRUE)
	setmetatile (12, 15, METATILE_IgnisMons_Barrier_Blue_Shadow, TRUE)
	setmetatile (13, 16, METATILE_IgnisMons_Barrier_Blue, TRUE)

	setmetatile (2, 15, METATILE_IgnisMons_Button_Blue, FALSE)

	return
}

script IgnisMons_0F_Script_ShowGreen {
	setmetatile (6, 2, METATILE_IgnisMons_Barrier_Green_Shadow, TRUE)
	setmetatile (6, 3, METATILE_IgnisMons_Barrier_Green, TRUE)
	setmetatile (1, 19, METATILE_IgnisMons_Barrier_Green_Shadow, TRUE)
	setmetatile (2, 18, METATILE_IgnisMons_Barrier_Green, TRUE)

	setmetatile (17, 8, METATILE_IgnisMons_Button_Green, FALSE)
	setmetatile (14, 34, METATILE_IgnisMons_Button_Green, FALSE)

	return
}

script IgnisMons_0F_Script_HideRed {
	setmetatile (3, 8, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (4, 9, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (9, 10, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (10, 11, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (3, 12, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (4, 13, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (17, 36, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (18, 37, METATILE_IgnisMons_Floor_Shadow, FALSE)

	setmetatile (12, 8, METATILE_IgnisMons_Button_White, FALSE)
	setmetatile (1, 11, METATILE_IgnisMons_Button_White_Shadow, FALSE)
	setmetatile (17, 21, METATILE_IgnisMons_Button_White, FALSE)
	setmetatile (2, 40, METATILE_IgnisMons_Button_White, FALSE)

	return
}

script IgnisMons_0F_Script_HideBlue {
	setmetatile (11, 16, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (12, 15, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (13, 16, METATILE_IgnisMons_Floor, FALSE)

	setmetatile (2, 15, METATILE_IgnisMons_Button_White, FALSE)

	return
}

script IgnisMons_0F_Script_HideGreen {
	setmetatile (6, 2, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (6, 3, METATILE_IgnisMons_Floor, FALSE)
	setmetatile (1, 19, METATILE_IgnisMons_Floor_Shadow, FALSE)
	setmetatile (2, 18, METATILE_IgnisMons_Floor, FALSE)

	setmetatile (17, 8, METATILE_IgnisMons_Button_White, FALSE)
	setmetatile (14, 34, METATILE_IgnisMons_Button_White, FALSE)

	return
}

//Philoctetes ambush
script IgnisMons_0F_Script_PhilAmbush {
	lockall
	setvar (VAR_ARGO_STATE, 4)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, IgnisMons_0F_Movement_PhilAmbush1)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	waitmovement (0)

	msgbox (IgnisMons_0F_Text_PhilAmbush, MSGBOX_DEFAULT)
	closemessage

	applymovement (OBJ_EVENT_PHIL, IgnisMons_0F_Movement_PhilAmbushExit)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
	waitmovement (0)

	removeobject (OBJ_EVENT_DRIFBLIM_A)
	setobjectxyperm (OBJ_EVENT_DRIFBLIM_A, 11, 17)
	setobjectmovementtype (OBJ_EVENT_DRIFBLIM_A, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_DRIFBLIM_A)
	showobjectat (OBJ_EVENT_DRIFBLIM_A, MAP_IGNIS_MONS_0F)

	removeobject (OBJ_EVENT_DRIFBLIM_B)
	setobjectxyperm (OBJ_EVENT_DRIFBLIM_B, 12, 17)
	setobjectmovementtype (OBJ_EVENT_DRIFBLIM_B, MOVEMENT_TYPE_FACE_DOWN)
	addobject (OBJ_EVENT_DRIFBLIM_B)
	showobjectat (OBJ_EVENT_DRIFBLIM_B, MAP_IGNIS_MONS_0F)

	applymovement (OBJ_EVENT_DRIFBLIM_A, IgnisMons_0F_Movement_PhilAmbusHDrifblim)
	applymovement (OBJ_EVENT_DRIFBLIM_B, IgnisMons_0F_Movement_PhilAmbusHDrifblim)

	hideobjectat (OBJ_EVENT_PHIL, MAP_IGNIS_MONS_0F)
	removeobject (OBJ_EVENT_PHIL)


	call (IgnisMons_1F_CollisionDrifblim)

	end
}
	

movement IgnisMons_0F_Movement_PhilAmbush1 {
	walk_up * 4 
}

movement IgnisMons_0F_Movement_PhilAmbusHDrifblim {
	walk_down * 2
}

movement IgnisMons_0F_Movement_PhilAmbushExit {
	walk_up * 5
	walk_right
	walk_up
}

//turn back there is no hope for you turn back
//Medea's spell will overcome you too
//and trap us both in this dance of darkness
//
//Foolish outsider you should not have come
//The path you tread leads only to downfall
//be gone or you too will become my slave
text IgnisMons_0F_Text_PhilAmbush {
	format("Turn back there is... Oolishfay outsideray ouyay ouldshay otnay avehay omecay... no hope for you turn back! Medea's spell...Ethay athpay ouyay eadtray eadslay onlyay otay ownfallday...Will overcome you too and...Ebay onegay oray ouyay ootay...Trap us both in this dance of darkness...Illway ecomebay ymay aveslay.")
}


