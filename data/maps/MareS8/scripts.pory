
//VAR_TEMP_5 Dynamic Map

//1: Isla Ithaca
//2: Mare Subtropicum - Default
//3: Mare Tropicum
//4: Isla Hesperia
//5: Isla Tropica
//6: Isla Elenna

//VAR_TEMP_7 Trigger Ulysses Scene
//VAR_TEMP_A - VAR_TEMP_F setup corresponding Kecleon Chase scenes 1-6
//Note: 7 is never needed because Kec7 only appears during Ulysses scene

//FLAG_TEMP_A switches player position for Ulysses Scene

mapscripts MareS8_MapScripts {

    MAP_SCRIPT_ON_TRANSITION {
		specialvar (VAR_TEMP_5, GetDynamicMapSec_MareS8_F)
		//Show Rutulian Block if Pristis still on Tropica
		if (var(VAR_AENEID_STATE) < 3) {
			call (MareS8_Script_ShowRutuliBlock)
		}
		//Handle Aeneid Setup
		if (var(VAR_AENEID_STATE) < 9 && var(VAR_AENEID_STATE) > 2) { //Obviously update this to the right number
			call (MareS8_Script_ShowAeneidDuringWar)
		}
		if (var(VAR_AENEID_STATE) < 7 && var(VAR_AENEID_STATE) > 2) {
			call (MareS8_Script_ShowRutulianWar)
		}
		//RetR search
		if (var(VAR_AENEID_STATE) == 10) {
			setobjectxyperm (LOCALID_MARES8_BEROE, 19, 22)
			setobjectmovementtype (LOCALID_MARES8_BEROE, MOVEMENT_TYPE_FACE_UP)
		}
		//Setup Kecleons
		call (MareS8_Script_SetupKecleonChase)
		//Setup Ulysses Scene
		if (var(VAR_ULYSSES_STATE) == 3 && flag(FLAG_ULYSSES_POST_POLYPHEMUS)) {
			call (MareS8_Script_SetupUlysses)
		}
    }

	MAP_SCRIPT_ON_RESUME {
		if (flag(FLAG_SYS_CTRL_OBJ_DELETE)) {
			//just hide them all
			hideobjectat (LOCALID_MARES8_KECLEON1, MAP_MARE_S8)
			hideobjectat (LOCALID_MARES8_KECLEON2, MAP_MARE_S8)
			hideobjectat (LOCALID_MARES8_KECLEON3, MAP_MARE_S8)
			hideobjectat (LOCALID_MARES8_KECLEON4, MAP_MARE_S8)
			hideobjectat (LOCALID_MARES8_KECLEON5, MAP_MARE_S8)
			hideobjectat (LOCALID_MARES8_KECLEON6, MAP_MARE_S8)
			hideobjectat (LOCALID_MARES8_KECLEON7, MAP_MARE_S8)
		}
	}

}

script MareS8_Script_ShowRutulianWar {
	setobjectxyperm (LOCALID_MARES8_RUTULIAN3, 45, 4)
	setobjectmovementtype (LOCALID_MARES8_RUTULIAN3, MOVEMENT_TYPE_FACE_RIGHT)
	if (var(VAR_AENEID_STATE) < 6) {
		setobjectxyperm (LOCALID_MARES8_RUTULIAN4, 48, 4)
		setobjectmovementtype (LOCALID_MARES8_RUTULIAN4, MOVEMENT_TYPE_FACE_LEFT)
	}
	if (var(VAR_AENEID_STATE) < 5) {
		setobjectxyperm (LOCALID_MARES8_RUTULIAN2, 35, 8)
		setobjectmovementtype (LOCALID_MARES8_RUTULIAN2, MOVEMENT_TYPE_FACE_UP)
	}
	if (var(VAR_AENEID_STATE) < 4) {
		setobjectxyperm (LOCALID_MARES8_RUTULIAN1, 30, 11)
		setobjectmovementtype (LOCALID_MARES8_RUTULIAN1, MOVEMENT_TYPE_FACE_LEFT)
	}
	return
}

//Show party on beach
script MareS8_Script_ShowAeneidDuringWar {
	setobjectxyperm (LOCALID_MARES8_AENEAS, 20, 19)
	setobjectxyperm (LOCALID_MARES8_FAUSTULUS, 18, 19)
	setobjectxyperm (LOCALID_MARES8_BEROE, 19, 22)
	setobjectxyperm (LOCALID_MARES8_ROMULUS, 15, 24)
	setobjectxyperm (LOCALID_MARES8_ASCANIUS, 16, 24)
	setobjectxyperm (LOCALID_MARES8_REMUS, 17, 24)
	setobjectmovementtype (LOCALID_MARES8_AENEAS, MOVEMENT_TYPE_FACE_DOWN)
	setobjectmovementtype (LOCALID_MARES8_FAUSTULUS, MOVEMENT_TYPE_FACE_RIGHT)
	setobjectmovementtype (LOCALID_MARES8_BEROE, MOVEMENT_TYPE_FACE_UP)
	setobjectmovementtype (LOCALID_MARES8_ROMULUS, MOVEMENT_TYPE_FACE_UP)
	setobjectmovementtype (LOCALID_MARES8_ASCANIUS, MOVEMENT_TYPE_FACE_UP)
	setobjectmovementtype (LOCALID_MARES8_REMUS, MOVEMENT_TYPE_FACE_UP)

	return
}

script MareS8_Script_Transition_MareTropicum {
    setvar (VAR_TEMP_5, 3)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS8_Script_Transition_MareSubtropicum {
    setvar (VAR_TEMP_5, 2)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS8_Script_Transition_IslaIthaca {
    setvar (VAR_TEMP_5, 1)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS8_Script_Transition_IslaElenna {
    setvar (VAR_TEMP_5, 6)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS8_Script_Transition_IslaHesperia {
    setvar (VAR_TEMP_5, 4)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS8_Script_Transition_IslaTropica {
    setvar (VAR_TEMP_5, 5)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS8_Script_ArriveHesperia {
        //reset music
        //setup the boat
        removeobject (LOCALID_MARES8_PRISTIS)
        setobjectxyperm (LOCALID_MARES8_PRISTIS, 22, 19)
        setobjectmovementtype (LOCALID_MARES8_PRISTIS, MOVEMENT_TYPE_FACE_UP)
        addobject (LOCALID_MARES8_PRISTIS)
        hideobjectat (LOCALID_MARES9_PRISTIS, MAP_MARE_S9)
        //disembark boat
        //Beroe
        removeobject (LOCALID_MARES8_BEROE)
        setobjectxyperm (LOCALID_MARES8_BEROE, 22, 19)
        setobjectmovementtype (LOCALID_MARES8_BEROE, MOVEMENT_TYPE_FACE_LEFT)
        addobject (LOCALID_MARES8_BEROE)
        applymovement (LOCALID_MARES8_BEROE, Common_Movement_WalkLeft3)
        waitmovement (0)
        applymovement (LOCALID_MARES8_BEROE, Common_Movement_WalkDown3)
        waitmovement (0)
        applymovement (LOCALID_MARES8_BEROE, Common_Movement_FaceUp)
        waitmovement (0)
        //Romulus, Ascanius et Remus
        removeobject (LOCALID_MARES8_ROMULUS)
        setobjectxyperm (LOCALID_MARES8_ROMULUS, 22, 19)
        setobjectmovementtype (LOCALID_MARES8_ROMULUS, MOVEMENT_TYPE_FACE_LEFT)
        addobject (LOCALID_MARES8_ROMULUS)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_WalkLeft)
        waitmovement (0)
        removeobject (LOCALID_MARES8_ASCANIUS)
        setobjectxyperm (LOCALID_MARES8_ASCANIUS, 22, 19)
        setobjectmovementtype (LOCALID_MARES8_ASCANIUS, MOVEMENT_TYPE_FACE_LEFT)
        addobject (LOCALID_MARES8_ASCANIUS)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_WalkLeft)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_WalkLeft)
        waitmovement (0)
        removeobject (LOCALID_MARES8_REMUS)
        setobjectxyperm (LOCALID_MARES8_REMUS, 22, 19)
        setobjectmovementtype (LOCALID_MARES8_REMUS, MOVEMENT_TYPE_FACE_LEFT)
        addobject (LOCALID_MARES8_REMUS)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_WalkLeft2)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_WalkLeft2)
        applymovement (LOCALID_MARES8_REMUS, Common_Movement_WalkLeft2)
        waitmovement (0)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_WalkDown)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_WalkLeft)
        applymovement (LOCALID_MARES8_REMUS, Common_Movement_WalkLeft)
        waitmovement (0)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_WalkDown)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_WalkDown)
        applymovement (LOCALID_MARES8_REMUS, Common_Movement_WalkLeft)
        waitmovement (0)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_WalkDown2)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_WalkDown2)
        applymovement (LOCALID_MARES8_REMUS, Common_Movement_WalkDown2)
        waitmovement (0)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_WalkLeft)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_WalkDown)
        applymovement (LOCALID_MARES8_REMUS, Common_Movement_WalkDown)
        waitmovement (0)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_WalkLeft)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_WalkLeft)
        applymovement (LOCALID_MARES8_REMUS, Common_Movement_WalkDown)
        waitmovement (0)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_WalkLeft)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_WalkLeft)
        applymovement (LOCALID_MARES8_REMUS, Common_Movement_WalkLeft)
        waitmovement (0)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_WalkDown)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_WalkDown)
        applymovement (LOCALID_MARES8_REMUS, Common_Movement_WalkDown)
        waitmovement (0)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_FaceUp)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_FaceUp)
        applymovement (LOCALID_MARES8_REMUS, Common_Movement_FaceUp)
        waitmovement (0)
        //Faustulus
        removeobject (LOCALID_MARES8_FAUSTULUS)
        setobjectxyperm (LOCALID_MARES8_FAUSTULUS, 22, 19)
        setobjectmovementtype (LOCALID_MARES8_FAUSTULUS, MOVEMENT_TYPE_FACE_LEFT)
        addobject (LOCALID_MARES8_FAUSTULUS)
        applymovement (LOCALID_MARES8_FAUSTULUS, Common_Movement_WalkLeft4)
        waitmovement (0)
        applymovement (LOCALID_MARES8_FAUSTULUS, Common_Movement_FaceDown)
        waitmovement (0)
        //Player
        fadenewbgm (MUS_SOOTOPOLIS)
        clearflag (FLAG_DONT_TRANSITION_MUSIC)
        resetobjectsubpriority (OBJ_EVENT_ID_PLAYER, MAP_MARE_S9)
        showobjectat (LOCALID_PLAYER, MAP_MARE_S8)
        applymovement (LOCALID_PLAYER, Common_Movement_WalkLeft3)
        waitmovement (0)
        applymovement (LOCALID_PLAYER, Common_Movement_FaceDown)
        waitmovement (0)
        //Aeneas
        removeobject (LOCALID_MARES8_AENEAS)
        setobjectxyperm (LOCALID_MARES8_AENEAS, 22, 19)
        setobjectmovementtype (LOCALID_MARES8_AENEAS, MOVEMENT_TYPE_FACE_LEFT)
        addobject (LOCALID_MARES8_AENEAS)
        applymovement (LOCALID_MARES8_AENEAS, Common_Movement_WalkLeft2)
        waitmovement (0)
        applymovement (LOCALID_MARES8_AENEAS, Common_Movement_FaceDown)
        waitmovement (0)
        //alert all
        call (MareS8_Script_AlertParty)
        //sink boat
        applymovement (LOCALID_MARES8_PRISTIS, Common_Movement_ExclamationMark)
        waitmovement (0)
        playse (SE_M_DIVE)
        delay (60)
        applymovement (LOCALID_MARES8_PRISTIS, Movement_SetInvisible)
        waitmovement (0)
        playse (SE_M_DIVE)
        delay (60)
        removeobject (LOCALID_MARES8_PRISTIS)
        setobjectxyperm (LOCALID_MARES8_PRISTIS, 0, 0)
        setobjectmovementtype (LOCALID_MARES8_PRISTIS, MOVEMENT_TYPE_INVISIBLE)
        addobject (LOCALID_MARES8_PRISTIS)
        //B: Now where have you gotten us, we lost our ship this cant get much worse
        load_field_pic (FIELD_PIC_BEROE, 190, 98)
        msgbox (MareS8_Text_Beroe1)
        destroy_field_pic (FIELD_PIC_BEROE)
        //alert party
        call (MareS8_Script_AlertParty)
        //R: Intruders be gone!
        load_field_pic (FIELD_PIC_RUTULIAN, 190, 98)
        msgbox (MareS8_Text_RutulianAmbush1)
        destroy_field_pic (FIELD_PIC_RUTULIAN)
        //A: On guard it may be about to get worse 
        load_field_pic (FIELD_PIC_AENEAS, 190, 98)
        msgbox (MareS8_Text_Aeneas_GetWorse)
        destroy_field_pic (FIELD_PIC_AENEAS)
		playbgm (MUS_ENCOUNTER_AQUA, TRUE)
        //face player et aeneas
        applymovement (LOCALID_PLAYER, Common_Movement_FaceUp)
        applymovement (LOCALID_MARES8_AENEAS, Common_Movement_FaceUp)
        waitmovement (0)
        //show rutuli
        removeobject (LOCALID_MARES8_RUTULIAN1)
        removeobject (LOCALID_MARES8_RUTULIAN2)
        setobjectxyperm (LOCALID_MARES8_RUTULIAN1, 27, 16)
        setobjectxyperm (LOCALID_MARES8_RUTULIAN2, 28, 16)
        setobjectmovementtype (LOCALID_MARES8_RUTULIAN1, MOVEMENT_TYPE_FACE_LEFT)
        setobjectmovementtype (LOCALID_MARES8_RUTULIAN2, MOVEMENT_TYPE_FACE_LEFT)
        addobject (LOCALID_MARES8_RUTULIAN1)
        addobject (LOCALID_MARES8_RUTULIAN2)
        //Enter 2 Rs
        applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkLeft6)
        applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkLeft6)
        waitmovement (0)
        applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkSouthWest)
        applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkLeft)
        waitmovement (0)
        applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkLeft)
        applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkSouthWest)
        waitmovement (0)
		applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_FaceDown)
		applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_FaceDown)
		waitmovement (0)
        //Setup Multi Battle
        applymovement (LOCALID_PLAYER, Common_Movement_FaceRight)
        applymovement (LOCALID_MARES8_AENEAS, Common_Movement_FaceLeft)
        waitmovement (0)
        load_field_pic (FIELD_PIC_AENEAS, 190, 98)
        msgbox (MareS8_Text_AeneasSetup)
        destroy_field_pic (FIELD_PIC_AENEAS)
        call (Common_Script_SelectPartyForMultiBattle)
        applymovement (LOCALID_PLAYER, Common_Movement_WalkUp)
        applymovement (LOCALID_MARES8_AENEAS, Common_Movement_WalkUp)
        waitmovement (0)
        //R:So long as King Turnus lives and Rutulians breath air none shall take our land!
        load_field_pic (FIELD_PIC_RUTULIAN, 190, 98)
        msgbox (MareS8_Text_RutulianAmbushPre)
        destroy_field_pic (FIELD_PIC_RUTULIAN)
		closemessage
		playbgm (MUS_SOOTOPOLIS, TRUE)
        //R attacks
        multi_2_vs_2 (TRAINER_RUTULIAN, MareS8_Text_RutulianAmbushLost1, TRAINER_RUTULIAN, MareS8_Text_RutulianAmbushLost2, PARTNER_AENEAS)
        //R post - we will drive these invaders from the sea
		applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_FaceRight)
		applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_FaceLeft)
		waitmovement (0)
        load_field_pic (FIELD_PIC_RUTULIAN, 190, 98)
        msgbox (MareS8_Text_RutulianAmbushPost)
        destroy_field_pic (FIELD_PIC_RUTULIAN)
		closemessage
		//exeunt Rutuli
        applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkUp6)
        applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkUp6)
        waitmovement (0)
        //B: Well I can't believe it. It's now much worse - stranded savages... 
        load_field_pic (FIELD_PIC_BEROE, 190, 98)
        msgbox (MareS8_Text_Beroe2)
        destroy_field_pic (FIELD_PIC_BEROE)
        //Move A et Player down again
        applymovement (LOCALID_MARES8_AENEAS, Common_Movement_WalkDown)
        applymovement (LOCALID_PLAYER, Common_Movement_WalkDown)
        waitmovement (0)
        //A: No you heard him this is Hesperia we've arrived! B watch the boys. Quickly let's defeat the savagges and take what is ours!
        load_field_pic (FIELD_PIC_AENEAS, 190, 98)
        msgbox (MareS8_Text_AeneasHesperia)
		//A et player face again
        applymovement (LOCALID_MARES8_AENEAS, Common_Movement_FaceLeft)
        applymovement (LOCALID_PLAYER, Common_Movement_FaceRight)
        waitmovement (0)
		//A: I'll keep watch over the women and children. I'm trusting you to scout ahead and clear the island of the savage menace. Go win glory for yourself and Aeneas' men!
		msgbox (MareS8_Text_AeneasGoToWar)
		destroy_field_pic (FIELD_PIC_AENEAS)
        closemessage
        applymovement (LOCALID_MARES8_AENEAS, Common_Movement_FaceDown)
        waitmovement (0)
		//set vars
		setvar (VAR_AENEID_STATE, 3)
		//setup for Rutulian War section
		removeobject (LOCALID_MARES8_RUTULIAN1)
		removeobject (LOCALID_MARES8_RUTULIAN2)
		removeobject (LOCALID_MARES8_RUTULIAN3)
		removeobject (LOCALID_MARES8_RUTULIAN4)
		call (MareS8_Script_ShowRutulianWar)
		addobject (LOCALID_MARES8_RUTULIAN1)
		addobject (LOCALID_MARES8_RUTULIAN2)
		addobject (LOCALID_MARES8_RUTULIAN3)
		addobject (LOCALID_MARES8_RUTULIAN4)
		//persist party location
		call (MareS8_Script_ShowAeneidDuringWar)
        //fin
        releaseall
        end

}

script MareS8_Script_Remus {
    faceplayer
    msgbox (MareS8_Text_RemusDuringWar)
    closemessage
    call (Common_Script_Unfaceplayer)
    end
}

script MareS8_Script_Beroe {
    faceplayer
	load_field_pic (FIELD_PIC_BEROE, 190, 98)
	if (var(VAR_AENEID_STATE) < 9) {
		msgbox (MareS8_Text_BeroeDuringWar)
	} else {
		msgbox (MareS8_Text_BeroeDuringSearch)
	}
	destroy_field_pic (FIELD_PIC_BEROE)
    closemessage
    applymovement (LOCALID_MARES8_BEROE, Common_Movement_FaceUp)
    waitmovement (0)
    end
}

script MareS8_Script_Aeneas {
    faceplayer
	load_field_pic (FIELD_PIC_AENEAS, 190, 98)
    msgbox (MareS8_Text_AeneasDuringWar)
    closemessage
	destroy_field_pic (FIELD_PIC_AENEAS)
    call (Common_Script_Unfaceplayer)
    end
}

script MareS8_Script_Faustulus {
    faceplayer
	load_field_pic (FIELD_PIC_FAUSTULUS, 190, 98)
    msgbox (MareS8_Text_FaustulusDuringWar)
    closemessage
	destroy_field_pic (FIELD_PIC_FAUSTULUS)
    call (Common_Script_Unfaceplayer)
    end
}

script MareS8_Script_Ascanius {
    faceplayer
	load_field_pic (FIELD_PIC_ASCANIUS, 190, 98)
    msgbox (MareS8_Text_AscaniusDuringWar)
    closemessage
	destroy_field_pic (FIELD_PIC_ASCANIUS)
    call (Common_Script_Unfaceplayer)
    end
}

script MareS8_Script_Romulus {
    faceplayer
    msgbox (MareS8_Text_RomulusDuringWar)
    closemessage
    call (Common_Script_Unfaceplayer)
    end
}

script MareS8_Script_AlertParty {
        applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
        applymovement (LOCALID_MARES8_AENEAS, Common_Movement_ExclamationMark)
        applymovement (LOCALID_MARES8_BEROE, Common_Movement_ExclamationMark)
        applymovement (LOCALID_MARES8_FAUSTULUS, Common_Movement_ExclamationMark)
        applymovement (LOCALID_MARES8_ASCANIUS, Common_Movement_ExclamationMark)
        applymovement (LOCALID_MARES8_ROMULUS, Common_Movement_ExclamationMark)
        applymovement (LOCALID_MARES8_REMUS, Common_Movement_ExclamationMark)
        waitmovement (0)
}

script MareS8_Script_Trigger_Rutulian1A {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceRight)
	waitmovement (0)
	//if needed move Rutulian into position
    applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkLeft2)
    waitmovement (0)
	//do battle
	call (Hesperia_Script_RutulianBattle)
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkRight)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkUp4)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkRight6)
	waitmovement (0)
	//remove Rutulian
	call (MareS8_Script_RemoveRutulian)
}

script MareS8_Script_Trigger_Rutulian1B {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceRight)
	waitmovement (0)
	//if needed move Rutulian into position
    applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkLeft)
    waitmovement (0)
	//do battle
	call (Hesperia_Script_RutulianBattle)	
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkUp4)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkRight7)
	waitmovement (0)
	//remove Rutulian
	call (MareS8_Script_RemoveRutulian)
}

script MareS8_Script_Trigger_Rutulian1C {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceRight)
	waitmovement (0)
	//if needed move Rutulian into position
	//do battle
	call (Hesperia_Script_RutulianBattle)	
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkUp4)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN1, Common_Movement_WalkRight7)
	waitmovement (0)
	//remove Rutulian
	call (MareS8_Script_RemoveRutulian)
}

script MareS8_Script_Trigger_Rutulian2A {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceDown)
	waitmovement (0)
	//if needed move Rutulian into position
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkUp4)
	waitmovement (0)
	//do battle
	call (Hesperia_Script_RutulianBattle)	
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkDown3)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkRight8)
	waitmovement (0)
	//remove Rutulian et inc var
	call (MareS8_Script_RemoveRutulian)
}

script MareS8_Script_Trigger_Rutulian2B {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceDown)
	waitmovement (0)
	//if needed move Rutulian into position
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkUp3)
	waitmovement (0)
	//do battle
	call (Hesperia_Script_RutulianBattle)	
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkDown2)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkRight8)
	waitmovement (0)
	//remove Rutulian et inc var
	call (MareS8_Script_RemoveRutulian)
}

script MareS8_Script_Trigger_Rutulian2C {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceDown)
	waitmovement (0)
	//if needed move Rutulian into position
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkUp2)
	waitmovement (0)
	//do battle
	call (Hesperia_Script_RutulianBattle)	
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkDown)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkRight8)
	waitmovement (0)
	//remove Rutulian et inc var
	call (MareS8_Script_RemoveRutulian)
}

script MareS8_Script_Trigger_Rutulian2D {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceDown)
	waitmovement (0)
	//if needed move Rutulian into position
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkUp)
	waitmovement (0)
	//do battle
	call (Hesperia_Script_RutulianBattle)	
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkRight8)
	waitmovement (0)
	//remove Rutulian et inc var
	call (MareS8_Script_RemoveRutulian)
}

script MareS8_Script_Trigger_Rutulian2E {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceDown)
	waitmovement (0)
	//if needed move Rutulian into position
	//do battle
	call (Hesperia_Script_RutulianBattle)	
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN2, Common_Movement_WalkRight8)
	waitmovement (0)
	//remove Rutulian et inc var
	call (MareS8_Script_RemoveRutulian)
}
	
script Hesperia_Script_RutulianBattle {
	clearflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Pick random pre message
    specialvar (VAR_RESULT, RandDigit)
    switch (var(VAR_RESULT)) {
        case 0:
        case 1:
            msgbox (Hesperia_Text_RutulianPre1)
        case 2:
        case 3:
            msgbox (Hesperia_Text_RutulianPre2)
        case 4:
        case 5:
            msgbox (Hesperia_Text_RutulianPre3)
        case 6:
        case 7:
            msgbox (Hesperia_Text_RutulianPre4)
        case 8:
        case 9:
            msgbox (Hesperia_Text_RutulianPre5)
        default:
                msgbox("ERROR RandDigit OOB")
                closemessage
                releaseall
                end
    }
    closemessage
	//pick random battle lost text (ie actual battle)
    specialvar (VAR_RESULT, RandDigit)
    switch (var(VAR_RESULT)) {
        case 0:
        case 1:
            trainerbattle_no_intro (TRAINER_RUTULIAN, Hesperia_Text_RutulianLost1)
        case 2:
        case 3:
            trainerbattle_no_intro (TRAINER_RUTULIAN, Hesperia_Text_RutulianLost2)
        case 4:
        case 5:
            trainerbattle_no_intro (TRAINER_RUTULIAN, Hesperia_Text_RutulianLost3)
        case 6:
        case 7:
            trainerbattle_no_intro (TRAINER_RUTULIAN, Hesperia_Text_RutulianLost4)
        case 8:
        case 9:
            trainerbattle_no_intro (TRAINER_RUTULIAN, Hesperia_Text_RutulianLost5)
        default:
                msgbox("ERROR RandDigit OOB")
                closemessage
                releaseall
                end
    }
	//pick random post message
	specialvar (VAR_RESULT, RandDigit)
    switch (var(VAR_RESULT)) {
        case 0:
        case 1:
            msgbox (Hesperia_Text_RutulianPost1)
        case 2:
        case 3:
            msgbox (Hesperia_Text_RutulianPost2)
        case 4:
        case 5:
            msgbox (Hesperia_Text_RutulianPost3)
        case 6:
        case 7:
            msgbox (Hesperia_Text_RutulianPost4)
        case 8:
        case 9:
            msgbox (Hesperia_Text_RutulianPost5)
        default:
                msgbox("ERROR RandDigit OOB")
                closemessage
                releaseall
                end
    }
    closemessage
	//return for exit
	return
}

script MareS8_Script_RemoveRutulian {
	switch (var(VAR_AENEID_STATE)) {
		//3: R1
		case 3:
			removeobject (LOCALID_MARES8_RUTULIAN1)
			setobjectxyperm (LOCALID_MARES8_RUTULIAN1, 0, 0)
			setobjectmovementtype (LOCALID_MARES8_RUTULIAN1, MOVEMENT_TYPE_INVISIBLE)
			addobject (LOCALID_MARES8_RUTULIAN1)
		//4: R2
		case 4:
			removeobject (LOCALID_MARES8_RUTULIAN2)
			setobjectxyperm (LOCALID_MARES8_RUTULIAN2, 0, 0)
			setobjectmovementtype (LOCALID_MARES8_RUTULIAN2, MOVEMENT_TYPE_INVISIBLE)
			addobject (LOCALID_MARES8_RUTULIAN2)
		//5: R4
		case 5:
			removeobject (LOCALID_MARES8_RUTULIAN4)
			setobjectxyperm (LOCALID_MARES8_RUTULIAN4, 0, 0)
			setobjectmovementtype (LOCALID_MARES8_RUTULIAN4, MOVEMENT_TYPE_INVISIBLE)
			addobject (LOCALID_MARES8_RUTULIAN4)
		//6: R3
		case 6:
			removeobject (LOCALID_MARES8_RUTULIAN3)
			setobjectxyperm (LOCALID_MARES8_RUTULIAN3, 0, 0)
			setobjectmovementtype (LOCALID_MARES8_RUTULIAN3, MOVEMENT_TYPE_INVISIBLE)
			addobject (LOCALID_MARES8_RUTULIAN3)
		default:
			msgbox("ERROR: AENEID_STATE OOB")
			closemessage
			releaseall
			end
	}
	//incvar
	addvar (VAR_AENEID_STATE, 1)
	//fin
	releaseall
	end
}

script MareS8_Script_ShowRutuliBlock {
	setobjectxyperm (LOCALID_MARES8_RUTULIAN1, 29, 9)
	setobjectxyperm (LOCALID_MARES8_RUTULIAN2, 30, 9)
	setobjectmovementtype (LOCALID_MARES8_RUTULIAN1, MOVEMENT_TYPE_FACE_DOWN)
	setobjectmovementtype (LOCALID_MARES8_RUTULIAN2, MOVEMENT_TYPE_FACE_DOWN)
	return
}

script MareS8_Script_RutuliBlock {
	msgbox (MareS8_Text_RutuliBlock)
	closemessage
}

//potential double battle R on the right.
script MareS8_Script_Rutulian4 {
	applymovement (LOCALID_MARES8_RUTULIAN4, Common_Movement_FaceDown)
	waitmovement (0)
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	applymovement (LOCALID_MARES8_RUTULIAN4, Common_Movement_ExclamationMark)
	waitmovement (0)
	call (Hesperia_Script_RutulianBattle)
	//Exit
	applymovement (LOCALID_MARES8_RUTULIAN4, Common_Movement_WalkUp4)
	waitmovement (0)
	//Remove
	call (MareS8_Script_RemoveRutulian)
}

script MareS8_Script_Trigger_Rutuli34A {
	//setup
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//alert rutuli
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_ExclamationMark)
	applymovement (LOCALID_MARES8_RUTULIAN4, Common_Movement_ExclamationMark)
	waitmovement (0)
	delay (30)
	//face player to approaching R
	applymovement (LOCALID_PLAYER, Common_Movement_FaceRight)
	applymovement (LOCALID_MARES8_RUTULIAN4, Common_Movement_WalkLeft)
	waitmovement (0)
	//RD Pre1
	msgbox (MareS8_Text_RutuliDoublePre1)
	//face player to L
	applymovement (LOCALID_PLAYER, Common_Movement_FaceLeft)
	waitmovement (0)
	//RD Pre2
	msgbox (MareS8_Text_RutuliDoublePre2)
	closemessage
	//do double battle - RL1, RL2
	trainerbattle_two_trainers (TRAINER_RUTULIAN, MareS8_Text_RutuliDoubleLost1, TRAINER_RUTULIAN, MareS8_Text_RutuliDoubleLost2)
	//Rd post
	msgbox (MareS8_Text_RutuliDoublePost)
	closemessage
	//exeunt rutuli
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkUp)
	applymovement (LOCALID_MARES8_RUTULIAN4, Common_Movement_WalkUp)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkRight)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkUp5)
	applymovement (LOCALID_MARES8_RUTULIAN4, Common_Movement_WalkUp5)
	waitmovement (0)
	//remove them both
	removeobject (LOCALID_MARES8_RUTULIAN3)
	removeobject (LOCALID_MARES8_RUTULIAN4)
	setobjectxyperm (LOCALID_MARES8_RUTULIAN3, 0, 0)
	setobjectxyperm (LOCALID_MARES8_RUTULIAN4, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_RUTULIAN3, MOVEMENT_TYPE_INVISIBLE)
	setobjectmovementtype (LOCALID_MARES8_RUTULIAN4, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_RUTULIAN3)
	addobject (LOCALID_MARES8_RUTULIAN4)
	//set var to 7
	setvar (VAR_AENEID_STATE, 7)
	//fin
	releaseall
	end
}

script MareS8_Script_Trigger_Rutuli34B {
	//setup
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//alert rutuli
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_ExclamationMark)
	applymovement (LOCALID_MARES8_RUTULIAN4, Common_Movement_ExclamationMark)
	waitmovement (0)
	delay (30)
	//face player to approaching R
	applymovement (LOCALID_PLAYER, Common_Movement_FaceLeft)
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkRight)
	waitmovement (0)
	//RD Pre1
	msgbox (MareS8_Text_RutuliDoublePre1)
	//face player to L
	applymovement (LOCALID_PLAYER, Common_Movement_FaceRight)
	waitmovement (0)
	//RD Pre2
	msgbox (MareS8_Text_RutuliDoublePre2)
	closemessage
	//do double battle - RL1, RL2
	trainerbattle_two_trainers (TRAINER_RUTULIAN, MareS8_Text_RutuliDoubleLost1, TRAINER_RUTULIAN, MareS8_Text_RutuliDoubleLost2)
	//Rd post
	msgbox (MareS8_Text_RutuliDoublePost)
	closemessage
	//exeunt rutuli
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkUp)
	applymovement (LOCALID_MARES8_RUTULIAN4, Common_Movement_WalkUp)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkRight)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkUp5)
	applymovement (LOCALID_MARES8_RUTULIAN4, Common_Movement_WalkUp5)
	waitmovement (0)
	//remove them both
	removeobject (LOCALID_MARES8_RUTULIAN3)
	removeobject (LOCALID_MARES8_RUTULIAN4)
	setobjectxyperm (LOCALID_MARES8_RUTULIAN3, 0, 0)
	setobjectxyperm (LOCALID_MARES8_RUTULIAN4, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_RUTULIAN3, MOVEMENT_TYPE_INVISIBLE)
	setobjectmovementtype (LOCALID_MARES8_RUTULIAN4, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_RUTULIAN3)
	addobject (LOCALID_MARES8_RUTULIAN4)
	//set var to 7
	setvar (VAR_AENEID_STATE, 7)
	//fin
	releaseall
	end
}

script MareS8_Script_Trigger_Rutulian3A {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceLeft)
	waitmovement (0)
	//if needed move Rutulian into position
	//do battle
	call (Hesperia_Script_RutulianBattle)	
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkUp)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkRight)
	waitmovement (0)
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkUp5)
	waitmovement (0)
	removeobject (LOCALID_MARES8_RUTULIAN3)
	setobjectxyperm (LOCALID_MARES8_RUTULIAN3, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_RUTULIAN3, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_RUTULIAN3)
	//remove Rutulian et inc var
	call (MareS8_Script_RemoveRutulian)
}

script MareS8_Script_Trigger_Rutulian3B {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceLeft)
	waitmovement (0)
	//if needed move Rutulian into position
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkRight)
	waitmovement (0)
	//do battle
	call (Hesperia_Script_RutulianBattle)	
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkUp6)
	waitmovement (0)
	removeobject (LOCALID_MARES8_RUTULIAN3)
	setobjectxyperm (LOCALID_MARES8_RUTULIAN3, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_RUTULIAN3, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_RUTULIAN3)
	//remove Rutulian et inc var
	call (MareS8_Script_RemoveRutulian)
}

script MareS8_Script_Trigger_Rutulian3C {
	lockall
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	//Music
	playbgm (MUS_ENCOUNTER_AQUA, FALSE)
	//Alert Rutulian 
    applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_ExclamationMark)
    waitmovement (0)
	//Turn et face player
	delay (30)
	applymovement (LOCALID_PLAYER, Common_Movement_FaceLeft)
	waitmovement (0)
	//if needed move Rutulian into position
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkRight2)
	waitmovement (0)
	//do battle
	call (Hesperia_Script_RutulianBattle)	
	//do exit
	applymovement (LOCALID_MARES8_RUTULIAN3, Common_Movement_WalkUp6)
	waitmovement (0)
	removeobject (LOCALID_MARES8_RUTULIAN3)
	setobjectxyperm (LOCALID_MARES8_RUTULIAN3, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_RUTULIAN3, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_RUTULIAN3)
	//remove Rutulian et inc var
	call (MareS8_Script_RemoveRutulian)
}

//Mare Elenna Kecleon et Ulysses

script MareS8_Script_SetupKecleonChase {
	//1-4 always
	setvar (VAR_TEMP_A, 1)
	setvar (VAR_TEMP_B, 1)
	setvar (VAR_TEMP_C, 1)
	setvar (VAR_TEMP_D, 1)
	//5 only once
	if (!flag(FLAG_MARES8_KECLEON5)) {
		setvar (VAR_TEMP_E, 1)
	}
	//6 - just setup normal and remove in setupulysses called after
	setvar (VAR_TEMP_F, 1)
	return
}

script MareS8_Script_SetupUlysses {
	setobjectxyperm (LOCALID_MARES8_ULYSSES, 33, 76)
	setobjectmovementtype (LOCALID_MARES8_ULYSSES, MOVEMENT_TYPE_WALK_SEQUENCE_RIGHT_DOWN_LEFT_UP)
	//remove Kecleon 6
	setobjectxyperm (LOCALID_MARES8_KECLEON6, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_KECLEON6, MOVEMENT_TYPE_INVISIBLE)
	//disable Kecleon6 Chase
	setvar (VAR_TEMP_F, 0)
	//setup Ulysses Scene trigger
	setvar (VAR_TEMP_7, 1)
	return
}

script MareS8_Script_UlyssesSceneA {
	setflag (FLAG_TEMP_A)
	call (MareS8_Script_UlyssesScene)
}

script MareS8_Script_UlyssesSceneB {
	call (MareS8_Script_UlyssesScene)
}

script MareS8_Script_UlyssesScene {
	lockall
	applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
	applymovement (LOCALID_MARES8_ULYSSES, Common_Movement_ExclamationMark)
	waitmovement (0)
	applymovement (LOCALID_MARES8_ULYSSES, Common_Movement_FaceRight)
	waitmovement (0)
    load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
	msgbox (MareS8_Text_Ulysses1)
	destroy_field_pic (FIELD_PIC_ULYSSES)
	closemessage
	//setup Kecleon
	removeobject (LOCALID_MARES8_KECLEON6)
	setobjectxyperm (LOCALID_MARES8_KECLEON6, 34, 77)
	addobject (LOCALID_MARES8_KECLEON6)

	removeobject (LOCALID_MARES8_KECLEON7)
	setobjectxyperm (LOCALID_MARES8_KECLEON7, 34, 78)
	addobject (LOCALID_MARES8_KECLEON7)

	applymovement (LOCALID_MARES8_KECLEON6, Common_Movement_WalkRight)
	applymovement (LOCALID_MARES8_KECLEON7, Common_Movement_WalkRight)
	waitmovement (0)
	//Cry Kecleon
	playmoncry (SPECIES_KECLEON, CRY_MODE_ENCOUNTER)
	waitmoncry
	//Show kecleon
	showobjectat (LOCALID_MARES8_KECLEON6, MAP_MARE_S8)
	showobjectat (LOCALID_MARES8_KECLEON7, MAP_MARE_S8)
	//Battle Kecleon
	setflag (FLAG_SYS_CTRL_OBJ_DELETE)
	setwildbattle (SPECIES_KECLEON, 13, SPECIES_KECLEON, 13)
	setobjectxyperm (LOCALID_MARES8_ULYSSES, 34, 77)
	setobjectmovementtype (LOCALID_MARES8_ULYSSES, MOVEMENT_TYPE_FACE_RIGHT)
	dowildbattle
	clearflag (FLAG_SYS_CTRL_OBJ_DELETE)
	//remove kecleon
	removeobject (LOCALID_MARES8_KECLEON6)
	setobjectxyperm (LOCALID_MARES8_KECLEON6, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_KECLEON6, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_KECLEON6)
	
	removeobject (LOCALID_MARES8_KECLEON7)
	setobjectxyperm (LOCALID_MARES8_KECLEON7, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_KECLEON7, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_KECLEON7)
	//Ulysses to player
	if (!flag(FLAG_TEMP_A)) {
		applymovement (LOCALID_MARES8_ULYSSES, Common_Movement_WalkDown)
		waitmovement (0)
	}
	applymovement (LOCALID_MARES8_ULYSSES, Common_Movement_WalkRight)
	waitmovement (0)
	//U: Thanks
    load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
	msgbox (MareS8_Text_Ulysses2)
	destroy_field_pic (FIELD_PIC_ULYSSES)
	closemessage
	//U exit
	if (flag(FLAG_TEMP_A)) {
		applymovement (LOCALID_MARES8_ULYSSES, Common_Movement_WalkDown)
	} else {
		applymovement (LOCALID_MARES8_ULYSSES, Common_Movement_WalkUp)
	}
	waitmovement (0)
	applymovement (LOCALID_MARES8_ULYSSES, Common_Movement_WalkRight3)
	waitmovement (0)
	applymovement (LOCALID_MARES8_ULYSSES, Common_Movement_WalkUp2)
	waitmovement (0)
	applymovement (LOCALID_MARES8_ULYSSES, Common_Movement_WalkRight2)
	waitmovement (0)
	applymovement (LOCALID_MARES8_ULYSSES, Common_Movement_WalkUp5)
	waitmovement (0)
	//Remove U
	removeobject (LOCALID_MARES8_ULYSSES)
	setobjectxyperm (LOCALID_MARES8_ULYSSES, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_ULYSSES, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_ULYSSES)
	//setvars
	setvar (VAR_ULYSSES_STATE, 4)
	end
}

script MareS8_Script_KecleonChase1 {
	setobjectmovementtypeghoul (LOCALID_MARES8_KECLEON1, MOVEMENT_TYPE_CHASE_PLAYER)
	return
}

script MareS8_Script_KecleonChase2 {
	setobjectmovementtypeghoul (LOCALID_MARES8_KECLEON2, MOVEMENT_TYPE_CHASE_PLAYER)
	return
}

script MareS8_Script_KecleonChase3 {
	setobjectmovementtypeghoul (LOCALID_MARES8_KECLEON3, MOVEMENT_TYPE_CHASE_PLAYER)
	return
}

script MareS8_Script_KecleonChase4 {
	setobjectmovementtypeghoul (LOCALID_MARES8_KECLEON4, MOVEMENT_TYPE_CHASE_PLAYER)
	return
}

script MareS8_Script_KecleonChase5 {
	setobjectmovementtypeghoul (LOCALID_MARES8_KECLEON5, MOVEMENT_TYPE_CHASE_PLAYER)
	return
}

script MareS8_Script_KecleonChase6 {
	setobjectmovementtypeghoul (LOCALID_MARES8_KECLEON6, MOVEMENT_TYPE_CHASE_PLAYER)
	return
}

script MareS8_Script_KecleonSetup {
	lockall
	applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
playmoncry (SPECIES_KECLEON, CRY_MODE_ENCOUNTER)
	waitmoncry
	return
}

script MareS8_Script_KecleonBattle {
	setflag (FLAG_SYS_CTRL_OBJ_DELETE)
	setwildbattle (SPECIES_KECLEON, 13)
	dowildbattle
	clearflag (FLAG_SYS_CTRL_OBJ_DELETE)
	return
}

script MareS8_Script_Kecleon1 {
	call (MareS8_Script_KecleonSetup)
	//set not invisible
	showobjectat (LOCALID_MARES8_KECLEON1, MAP_MARE_S8)
	//battle
	call (MareS8_Script_KecleonBattle)
	//Remove specific Kecleon
	removeobject (LOCALID_MARES8_KECLEON1)
	setobjectxyperm (LOCALID_MARES8_KECLEON1, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_KECLEON1, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_KECLEON1)
	//clear Kecleon
	setvar (VAR_TEMP_A, 0)
	releaseall
	end
}

script MareS8_Script_Kecleon2 {
	call (MareS8_Script_KecleonSetup)
	//set not invisible
	showobjectat (LOCALID_MARES8_KECLEON2, MAP_MARE_S8)
	//battle
	call (MareS8_Script_KecleonBattle)
	//Remove specific Kecleon
	removeobject (LOCALID_MARES8_KECLEON2)
	setobjectxyperm (LOCALID_MARES8_KECLEON2, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_KECLEON2, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_KECLEON2)
	//clear Kecleon
	setvar (VAR_TEMP_B, 0)
	releaseall
	end
}

script MareS8_Script_Kecleon3 {
	call (MareS8_Script_KecleonSetup)
	//set not invisible
	showobjectat (LOCALID_MARES8_KECLEON3, MAP_MARE_S8)
	//battle
	call (MareS8_Script_KecleonBattle)
	//Remove specific Kecleon
	removeobject (LOCALID_MARES8_KECLEON3)
	setobjectxyperm (LOCALID_MARES8_KECLEON3, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_KECLEON3, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_KECLEON3)
	//clear Kecleon
	setvar (VAR_TEMP_C, 0)
	releaseall
	end
}

script MareS8_Script_Kecleon4 {
	call (MareS8_Script_KecleonSetup)
	//set not invisible
	showobjectat (LOCALID_MARES8_KECLEON4, MAP_MARE_S8)
	//battle
	call (MareS8_Script_KecleonBattle)
	//Remove specific Kecleon
	removeobject (LOCALID_MARES8_KECLEON4)
	setobjectxyperm (LOCALID_MARES8_KECLEON4, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_KECLEON4, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_KECLEON4)
	//clear Kecleon
	setvar (VAR_TEMP_D, 0)
	releaseall
	end
}

//Beach - Only Once
script MareS8_Script_Kecleon5 {
	call (MareS8_Script_KecleonSetup)
	//set not invisible
	showobjectat (LOCALID_MARES8_KECLEON5, MAP_MARE_S8)
	//battle
	call (MareS8_Script_KecleonBattle)
	//Remove specific Kecleon
	removeobject (LOCALID_MARES8_KECLEON5)
	setobjectxyperm (LOCALID_MARES8_KECLEON5, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_KECLEON5, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_KECLEON5)
	//clear Kecleon
	setflag (FLAG_MARES8_KECLEON5) //Perm
	setvar (VAR_TEMP_E, 0)
	releaseall
	end
}

script MareS8_Script_Kecleon6 {
	call (MareS8_Script_KecleonSetup)
	//set not invisible
	showobjectat (LOCALID_MARES8_KECLEON6, MAP_MARE_S8)
	//battle
	call (MareS8_Script_KecleonBattle)
	//Remove specific Kecleon
	removeobject (LOCALID_MARES8_KECLEON6)
	setobjectxyperm (LOCALID_MARES8_KECLEON6, 0, 0)
	setobjectmovementtype (LOCALID_MARES8_KECLEON6, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_MARES8_KECLEON6)
	//clear Kecleon
	setvar (VAR_TEMP_F, 0)
	releaseall
	end
}

text MareS8_Text_Beroe1 {
    format("Leave it to Aeneas to find a way
            to put us in a worse situation.
            We lost our ship and still have no idea
            where we are or where Heperia is.
            I can't see how this could get any worse!")
}

//Death to the foemen!
text MareS8_Text_RutulianAmbush1 {
    format("Foemen sadeathya!")
}

text MareS8_Text_Aeneas_GetWorse {
    format("On guard! I think it's about to get worse.")
}

text MareS8_Text_AeneasSetup {
    format("We're being attacked by two savages!
            Let's unite and defeat them as a team.
            Select three Pokemon you will fight with.")
}

//Lets drive the enemy into the sea
text MareS8_Text_RutulianAmbushPre {
    format("Into sea safoe wadrive.")
}

//We need more men!
text MareS8_Text_RutulianAmbushLost1 {
    format("More Rutulians waneed.")
}

//Nasty defeat!
text MareS8_Text_RutulianAmbushLost2 {
    format("Nasty defeatya!")
}

//We can't let Hesperia fall to these foes! Let's warn Turnus and rouse the Rutulian Army!
text MareS8_Text_RutulianAmbushPost {
    format("Hesperia to safoe fall waletna.\pKa-Turnus wawarn and wa-army warouseya!")
}


text MareS8_Text_Beroe2 {
	format("Well, Aeneas, you're right. It's now much worse.
            We're lost without a ship and stranded on
            some hostile island.")
}

text MareS8_Text_AeneasHesperia {
	format("Didn't you hear?
            That savage said this is Hesperia!
            At long last we have arrived in that land
            where I'm destined to found a great city!
            Rejoice, for our long voyage has at last
            reached its destination! All that remains
            for us to do is clear this island of
            its heathen natives and then start building.")
}

text MareS8_Text_AeneasGoToWar {
	format("I will wait here, and guard the party from
            any ambush those primitives might launch.
            You go ahead, make war on the natives,
            bring down their chief, expel the savages,
            and make the island safe for construction.
            Go forth! Win glory for Aeneas' men!")
}

text MareS8_Text_AscaniusDuringWar {
    format("While I'm nervous of a savage ambush
            I'm quite relieved my boys finally calmed down.")
}

text MareS8_Text_AeneasDuringWar {
    format("What are you waiting for? Go win glory!")
}

//I'm not scared. I know master will win. He always wins.
text MareS8_Text_FaustulusDuringWar {
    format("Mascarena! Kawin maknow. Alwaysya kawin.")
}

text MareS8_Text_RomulusDuringWar {
    format("I think we would be safer if we built
            a mighty wall to keep out the natives.")
}

text MareS8_Text_RemusDuringWar {
    format("If I were older, I'd be fighting too!")
}

text MareS8_Text_BeroeDuringWar {
    format("I hope the natives don't attack again.")
}

//Death to the nonbeliever!
text Hesperia_Text_RutulianPre1 {
	format("Death taheathen mado.")
}

//Coming here was your last mistake
text Hesperia_Text_RutulianPre2 {
	format("Isla Hesperia tacome tamistake last isya.")
}

//Outsiders are not allowed on Isla Hesperia
text Hesperia_Text_RutulianPre3 {
	format("Outsiders Isla Hesperia on taallowna.")
}

//King Turnus will reward me for killing you
text Hesperia_Text_RutulianPre4 {
	format("Takill Ka-Turnus mareward Kado.")
}

//We Rutulians will feast on your corpse
text Hesperia_Text_RutulianPre5 {
	format("Tacorpse Wa-Rutuli waeatya.")
}

//Turnus!
text Hesperia_Text_RutulianLost1 {
	format("Ka-Turnus!")
}

//What black magic is this?
text Hesperia_Text_RutulianLost2 {
	format("Tablack tamagic howya?")
}

//Impossible!
text Hesperia_Text_RutulianLost3 {
	format("Uhu!")
}

//Have mercy!
text Hesperia_Text_RutulianLost4 {
	format("Taforebearya!")
}

//Laran! Why have you forsaken me?
text Hesperia_Text_RutulianLost5 {
	format("Ka-Laran! Maforsake whyya?")
}

//Losing to an outsider is worse than losing to a woman.
text Hesperia_Text_RutulianPost1 {
	format("Malose taoutsider worse dame malose beya.")
}

//I must warn Turnus about you
text Hesperia_Text_RutulianPost2 {
	format("Ka-Turnus ta kawarn maneed!")
}

//We will still stop you
text Hesperia_Text_RutulianPost3 {
	format("Wa-Rutuli tastopya.")
}

//This is only minor setback
text Hesperia_Text_RutulianPost4 {
	format("Small blow onlyya be.")
}

//This isn't over. We Rutulians don't know the word surrender
text Hesperia_Text_RutulianPost5 {
	format("Overna! Wa-Rutuli yield waknowna.")
}

//Your invasion of our island stops here
text MareS8_Text_RutuliDoublePre1 {
	format("Ta-attack waisland stopya.")
}

//How can you expect to defeat two of us at once?
text MareS8_Text_RutuliDoublePre2 {
	format("Watwo Rutuli tadefeatna.")
}

text MareS8_Text_RutuliDoubleLost1 {
	format("Wafall backya!")
}

text MareS8_Text_RutuliDoubleLost2 {
	format("Wabeat!")
}

text MareS8_Text_RutuliDoublePost {
	format("Waneed more Rutuli here fastya!")
}

text MareS8_Text_RutuliBlock {
	format("Isla holy Hesperia beya.\p Taoutsider welcomena. Tago!")
}

text MareS8_Text_BeroeDuringSearch {
	format("The boys, Romulus and Remus, are gone!
			I hope they turn up soon...It's terrible!")
}

text MareS8_Text_Ulysses1 {
	format("Please, help me {PLAYER}! Help me! I'm being chased
			by some kind of invisible monsters!")
}

text MareS8_Text_Ulysses2 {
	format("Thank you, {PLAYER}. Those monsters had me trapped
			on this island with no hope of escape.
			Now I must be gone. I cannot delay
			my journey to Nestor's house where my friend
			Menelaus will celebrate marriage.")
}
