
//Dynamic map VAR_TEMP_5

//1: Isla Ithaca
//2: Mare Subtropicum
//3: Isla Herba Bona
//4: Isla Arida
//5: Isla Ignifera

//VAR_TEMP_A et VAR_TEMP_B used to hold values during Tauros Push


mapscripts MareS7_MapScripts {

	MAP_SCRIPT_ON_TRANSITION {
		//Handle Dynamic Map
		specialvar (VAR_TEMP_5, GetDynamicMapSec_MareS7_F)
		//Hide Telemachus if either not ready or already done
		//Setup Ulysses Stand / Chase
		if (var(VAR_ULYSSES_STATE) == 0) {
			call (MareS7_Script_CheckUlyssesChase)
		}
		//Setup Ulysses save Telemachus scene
		if (var(VAR_ULYSSES_STATE) == 1) {
			call (MareS7_Script_ShowUlyssesSaveTelemachus)
		}
    }

	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		//Turn Player for Ulysses Save Telemachus scene
		VAR_ULYSSES_STATE, 1: Common_Script_FacePlayerUp
	]
	
	MAP_SCRIPT_ON_FRAME_TABLE [
		//Do Ulysses save telemachus sceen
		VAR_ULYSSES_STATE, 1: MareS7_Script_UlyssesSaveTelemachus
	]

}

//Dynamic Map Scripts

script MareS7_Script_Transition_IslaIthaca {
    setvar (VAR_TEMP_5, 1)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS7_Script_Transition_MareSubtropicum {
    setvar (VAR_TEMP_5, 2)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS7_Script_Transition_IslaHerbaBona {
    setvar (VAR_TEMP_5, 3)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS7_Script_Transition_IslaArida {
    setvar (VAR_TEMP_5, 4)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS7_Script_Transition_IslaIgnifera {
    setvar (VAR_TEMP_5, 5)
	special (TransitionMapMusic)
    special (ShowMapNamePopup)
}

script MareS7_Script_CheckUlyssesChase {
	checkitem (ITEM_WEDDING_INVITATION_ULYSSES)
	if (var(VAR_RESULT) == YES) {
		call (MareS7_Script_SetupUlyssesChase)
	}
	return
}

script MareS7_Script_SetupUlyssesChase {
    //U Wander
	setobjectxyperm (LOCALID_MARES7_ULYSSES, 11, 30)
	setobjectmovementtype (LOCALID_MARES7_ULYSSES, MOVEMENT_TYPE_WANDER_LEFT_AND_RIGHT)
    //T1 
	setobjectxyperm (LOCALID_MARES7_TAUROS1, 9, 28)
	setobjectmovementtype (LOCALID_MARES7_TAUROS1, MOVEMENT_TYPE_WALK_RIGHT_AND_LEFT)
    //T2
	setobjectxyperm (LOCALID_MARES7_TAUROS2, 13, 32)
	setobjectmovementtype (LOCALID_MARES7_TAUROS2, MOVEMENT_TYPE_WALK_LEFT_AND_RIGHT)
    //P
	setobjectxyperm (LOCALID_MARES7_PENELOPE, 11, 34)
	setobjectmovementtype (LOCALID_MARES7_PENELOPE, MOVEMENT_TYPE_FACE_UP)
    //Telemachus
	setobjectxyperm (LOCALID_MARES7_TELEMACHUS, 10, 34)
	setobjectmovementtype (LOCALID_MARES7_TELEMACHUS, MOVEMENT_TYPE_FACE_UP)
	return
}

script MareS7_Script_ShowUlyssesSaveTelemachus {
	//T
    setobjectxyperm (LOCALID_MARES7_TELEMACHUS, 10, 33)
    setobjectmovementtype (LOCALID_MARES7_TELEMACHUS, MOVEMENT_TYPE_FACE_RIGHT)
	//U
    setobjectxyperm (LOCALID_MARES7_ULYSSES, 11, 33)
    setobjectmovementtype (LOCALID_MARES7_ULYSSES, MOVEMENT_TYPE_FACE_LEFT)
	//P
    setobjectxyperm (LOCALID_MARES7_PENELOPE, 12, 33)
    setobjectmovementtype (LOCALID_MARES7_PENELOPE, MOVEMENT_TYPE_FACE_DOWN)
	return
}

//Ulysses feigns madness
script MareS7_Script_Ulysses {
load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
    specialvar (VAR_RESULT, RandDigit)
    switch (var(VAR_RESULT)) {
        case 0:
        case 1:
            msgbox (MareS7_Text_UlyssesMadness1)
        case 2:
        case 3:
            msgbox (MareS7_Text_UlyssesMadness2)
        case 4:
        case 5:
            msgbox (MareS7_Text_UlyssesMadness3)
        case 6:
        case 7:
            msgbox (MareS7_Text_UlyssesMadness4)
        case 8:
        case 9:
            msgbox (MareS7_Text_UlyssesMadness5)
        default:
            msgbox (MareS7_Text_UlyssesMadness6)
    }
    destroy_field_pic (FIELD_PIC_ULYSSES)
    closemessage
    end
}

script MareS7_Script_Penelope {
	applymovement (LOCALID_MARES7_PENELOPE, Common_Movement_ExclamationMark)
	waitmovement (0)
    load_field_pic (FIELD_PIC_PENELOPE, 190, 98)
    msgbox (MareS7_Text_Penelope1)
    faceplayer
    msgbox (MareS7_Text_Penelope2)
    destroy_field_pic (FIELD_PIC_PENELOPE)
    closemessage
    unfaceplayer
    end
}

script MareS7_Script_Telemachus {
    faceplayer
    msgbox (MareS7_Text_Telemachus)
    closemessage
    unfaceplayer
    end
}

script MareS7_Script_TaurosPush {
	specialvar (VAR_TEMP_A, CheckWhichTaurosPush)
	switch (var(VAR_TEMP_A)) {
		//OOB
		case 0:
			msgbox("ERROR CheckWhichTaurosPush OOB")
		//T1
		case 1:
			applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
			applymovement (LOCALID_MARES7_TAUROS1, Common_Movement_ExclamationMark)
			waitmovement (0)
		//T2
		case 2:
			applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
			applymovement (LOCALID_MARES7_TAUROS2, Common_Movement_ExclamationMark)
			waitmovement (0)
		default:
			msgbox("ERROR CheckWhichTaurosPush OOB")
	}
    //tauros sound
	playmoncry (SPECIES_TAUROS, CRY_MODE_ENCOUNTER)
	waitmoncry
    //push player - check tauros direction, push player in that direction unless collision with telemachus in which case just move player down
	specialvar (VAR_TEMP_B, CheckTaurosPushDirection)
	switch (var(VAR_TEMP_B)) {
		//handle error code
		case DIR_EAST:
			if (var(VAR_TEMP_A) == 1) {
				applymovement (LOCALID_MARES7_TAUROS1, Common_Movement_WalkRight)
				applymovement (LOCALID_PLAYER, Common_Movement_WalkRight)
				waitmovement (0)
			} else {
				applymovement (LOCALID_MARES7_TAUROS2, Common_Movement_WalkRight)
				applymovement (LOCALID_PLAYER, Common_Movement_WalkRight)
				waitmovement (0)
			}
		case DIR_WEST:
			if (var(VAR_TEMP_A) == 1) {
				applymovement (LOCALID_MARES7_TAUROS1, Common_Movement_WalkLeft)
				applymovement (LOCALID_PLAYER, Common_Movement_WalkLeft)
				waitmovement (0)
			} else {
				applymovement (LOCALID_MARES7_TAUROS2, Common_Movement_WalkLeft)
				applymovement (LOCALID_PLAYER, Common_Movement_WalkLeft)
				waitmovement (0)
			}
        case DIR_NORTH:
				applymovement (LOCALID_PLAYER, Common_Movement_WalkUp)
				waitmovement (0)
        case DIR_SOUTH:
				applymovement (LOCALID_PLAYER, Common_Movement_WalkDown)
				waitmovement (0)
        case 67:
            msgbox("ERROR 67 \nNo Valid Push Direction sin collision")
		case 69:
			msgbox("ERROR 69 CheckTaurosPushDirection")
        default:
                msgbox("ERROR VAR_TEMP_B default switch")
	}
    //fin
    release
    end
}

script MareS7_Script_TaurosTelemachus {
	//alert Telemachus et relevant Tauros
	specialvar (VAR_TEMP_A, CheckWhichTaurosTelemachusPush)
	switch (var(VAR_TEMP_A)) {
		//OOB
		case 0:
			applymovement (LOCALID_MARES7_TAUROS1, Common_Movement_ExclamationMark)
			applymovement (LOCALID_MARES7_TAUROS2, Common_Movement_ExclamationMark)
			waitmovement (0)
			msgbox("ERROR CheckWhichTaurosPush OOB")
		//T1
		case 1:
			applymovement (LOCALID_MARES7_TELEMACHUS, Common_Movement_ExclamationMark)
			applymovement (LOCALID_MARES7_TAUROS1, Common_Movement_ExclamationMark)
			waitmovement (0)
		//T2
		case 2:
			applymovement (LOCALID_MARES7_TELEMACHUS, Common_Movement_ExclamationMark)
			applymovement (LOCALID_MARES7_TAUROS2, Common_Movement_ExclamationMark)
			waitmovement (0)
		default:
			msgbox("ERROR CheckWhichTaurosPush OOB")
	}
	//tauros sound
	playmoncry (SPECIES_TAUROS, CRY_MODE_ENCOUNTER)
	waitmoncry
	//alerty U
	applymovement (LOCALID_MARES7_ULYSSES, Common_Movement_ExclamationMark)
	waitmovement (0)
	//turn U
	if (var(VAR_TEMP_A) == 1) {
		applymovement (LOCALID_MARES7_ULYSSES, Common_Movement_FaceUp)
		waitmovement (0)
	} elif (var(VAR_TEMP_A) == 2) {
		applymovement (LOCALID_MARES7_ULYSSES, Common_Movement_FaceDown)
		waitmovement (0)
	} else {
		msgbox("ERROR VAR_TEMP_A OOB in Ulysses Face")
		closemessage
		end
	}
	//U: No! Telemachus!
    load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
	msgbox(MareS7_Text_UlyssesSaveTelemachus1)
	setvar (VAR_ULYSSES_STATE, 1)
	warpsilent (MAP_MARE_S7, 11, 34)
}

script MareS7_Script_UlyssesSaveTelemachus {
	//U: Are you OK? That was so close
    load_field_pic (FIELD_PIC_ULYSSES, 190, 98)
	msgbox(MareS7_Text_UlyssesSaveTelemachus2)
	//U face down
    applymovement (LOCALID_MARES7_ULYSSES, Common_Movement_FaceDown)
    applymovement (LOCALID_MARES7_TELEMACHUS, Common_Movement_FaceDown)
    waitmovement (0)
	//U: Oh! Sorry I uhh didn't see you there, I uhh...Wow I've been miraculously cured. Hello, guest, welcome to my home Ithaca. We should probably head insdie now
	msgbox(MareS7_Text_UlyssesSaveTelemachus3)
	//warp into Casa
    warpsilent (MAP_CASA_ULYSSIS, 7, 8)
}

text MareS7_Text_Penelope1 {
    format("What ever shall I do? My poor husband,
            the resourceful and brilliant Ulysses,
            has gone insane! Look how he wanders round
            without sense while Tauros destroy our fields!")
}

text MareS7_Text_Penelope2 {
    format("I'm so sorry but I must ask you to
            respect our privacy during this time
            of tragedy. Please leave, my husband is
            not well. Curse the madness of Ulysses!")
}

text MareS7_Text_UlyssesMadness6 {
    format("...and viddy films I would! Where I was taken to, brothers, was like no cine I'd ever viddied before. I was bound up in a straightjacket, and my guliver was strapped to a headrest...")
}

text MareS7_Text_UlyssesMadness2 {
    format("The spy coast on the deep road toward that narrow north...")
}

text MareS7_Text_UlyssesMadness3 {
    format("Jar women, tomb of sand, the bitter summer guests, the mighty bread...")
}

text MareS7_Text_UlyssesMadness4 {
    format("I am the eggman. You are the eggman. I am the walrus. Goo goo g'joob!")
}

text MareS7_Text_UlyssesMadness5 {
    format("A is B and C is B and D is B but A is not C and A is not D and C is not D.")
}

text MareS7_Text_UlyssesMadness1 {
    format("Jingle bells! Mermaid Man smells! Barnacle Boy laid an egg! The invisible boat mobile lost a wheel and...")
}

text MareS7_Text_Telemachus {
    format("I don't get it! What's wrong with pa? Why can't he stop the Tauros from hurting our farm?")
}

text MareS7_Text_UlyssesSaveTelemachus1 {
	format("No! Telemachus!")
}

text MareS7_Text_UlyssesSaveTelemachus2 {
    format("My darling son, are you OK? You're not
            injured? That was nearly catastrophe!")
}

text MareS7_Text_UlyssesSaveTelemachus3 {
    format("Oh! Hello...uhh I uhh...didn't see you there. Wow! I uhh.. I'm feeling just like myself again! Yeah, that's right!
            I've been miraculously healed of my
            former condition, now I'm thinking straight
            and seeing clearly. Praise to the divine!
            But where are my manners, you are our guest
            on out family home, Isla Ithaca.
            We should all head inside. Please, come with me.")
}
