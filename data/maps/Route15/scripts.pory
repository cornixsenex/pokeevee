
//VAR_TEMP_3: Check sandstorm block
//VAR_TEMP_4: Which direction to move during block
//VAR_TEMP_5: DYNAMIC MAP

mapscripts Route15_MapScripts {

	MAP_SCRIPT_ON_LOAD {
		if (flag(FLAG_ROUTE15_DESERTCAVE_OPEN)) {
			call (Route15_EventScript_ShowCaveDoor)
		}
	}

}

script Route15_EventScript_ShowCaveDoor {
	setmetatile (43, 38, METATILE_General_CaveEntrance_Bottom, 0)
	setmetatile (43, 37, METATILE_General_CaveEntrance_Top, 1)
	end
}

script Route15_EventScript_DesertTempleWarp {
	lockall
	delay (20)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, Movement_SetInvisible)
	waitmovement (0)
	playse (SE_FALL)
	delay (60)
	warpsilent (MAP_DESERT_TEMPLE_B1F, 9, 9)
	waitstate
	end
}

script Route15_Script_SunTrigger {
    setweather (WEATHER_SUNNY)
    doweather
    end
}

script Route15_Script_SandstormTriggerUp {
    setvar (VAR_TEMP_4, 2)
    call (Route15_Script_SandstormTrigger)
}

script Route15_Script_SandstormTriggerLeft {
    setvar (VAR_TEMP_4, 1)
    call (Route15_Script_SandstormTrigger)
}

script Route15_Script_SandstormTriggerRight {
    setvar (VAR_TEMP_4, 3)
    call (Route15_Script_SandstormTrigger)
}

script Route15_Script_SandstormTrigger {
    setweather (WEATHER_SANDSTORM)
    doweather
    checkitem (ITEM_GO_GOGGLES)
    if (var(VAR_RESULT) == NO) {
        applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_ExclamationMark)
        waitmovement (0)
        msgbox (gText_SandstormIsVicious)
        closemessage
        if (var(VAR_TEMP_4) == 1) {
            applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft2)
        } elif (var(VAR_TEMP_4) == 2) {
            applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp2)
        } elif (var(VAR_TEMP_4) == 3) {
			applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight2)
		} else {
			msgbox("ERROR: SandstormTrigger\nVAR_TEMP_4 OOB")
			closemessage
			release
			end
		}
   			waitmovement (0)
    } else {
        setvar (VAR_TEMP_3, 1)
    }
	release
    end
}
