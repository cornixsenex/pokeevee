mapscripts AedesAqua_MiniBossRoom_MapScripts {

    MAP_SCRIPT_ON_TRANSITION {
        //Setup Dark Bendan if not defeated
        if (flag(FLAG_AEDES_AQUA_CRYSTAL6) && !defeated(TRAINER_DARKBRENDAN)) {
            setobjectxyperm (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN, 10, 11)
            setobjectmovementtype (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN, MOVEMENT_TYPE_CHASE_PLAYER)
        }
    }

    MAP_SCRIPT_ON_LOAD {
        //Show pressed crystal tile
        if (flag(FLAG_AEDES_AQUA_CRYSTAL6)) {
			setmetatile (10, 4, METATILE_Cave_ShoalCave_BlueStone_Small, TRUE)
        }
    }

    MAP_SCRIPT_ON_RESUME {
        //Hide Dark Brendan on return from battle
        if (flag(FLAG_SYS_CTRL_OBJ_DELETE)) {
            hideobjectat (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN, MAP_AEDES_AQUA_MINI_BOSS_ROOM)
            removeobject (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN)
        }
    }
}

script AedesAqua_MiniBossRoom_Script_Crystal6 {
	if (!flag(FLAG_AEDES_AQUA_CRYSTAL6)) {
		setflag (FLAG_AEDES_AQUA_CRYSTAL6)
		msgbox (AedesAqua_Text_CrystalPress)
		playse (SE_SWITCH)
		waitse
		closemessage
		setmetatile (10, 4, METATILE_Cave_ShoalCave_BlueStone_Small, TRUE)
		special (DrawWholeMapView)
		//Setup Dark Brendan 
		removeobject (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN)
		setobjectxyperm (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN, 10, 11)
		setobjectmovementtype (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN, MOVEMENT_TYPE_CHASE_PLAYER)
		addobject (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN)
	} else {
		msgbox (AedesAqua_Text_CrystalPressed)
		closemessage
	}
	end
}

script AedesAqua_MiniBossRoom_Script_DarkBrendan {
	applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
    setflag (FLAG_SYS_CTRL_OBJ_DELETE) //Sys flag
	trainerbattle_no_intro (TRAINER_DARKBRENDAN, AedesAqua_MiniBossRoom_Text_DarkBrendanLost)
    clearflag (FLAG_SYS_CTRL_OBJ_DELETE) //Sys flag
    //remove him after battle
    removeobject (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN)
    setobjectxyperm (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN, 0, 0)
    setobjectmovementtype (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN, MOVEMENT_TYPE_INVISIBLE)
    addobject (LOCALID_AEDESAQUA_MINIBOSSROOM_DARKBRENDAN)
	end
}

text AedesAqua_MiniBossRoom_Text_DarkBrendanLost {
	format("")
}
