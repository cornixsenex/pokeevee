
const OBJ_EVENT_ARGO = 1

mapscripts MareS5_MapScripts {

	MAP_SCRIPT_ON_LOAD {
		if (var(VAR_ARGO_BOAT_STATE) == 1) {
			call (MareS5_Script_SupressSurf)
		}
	}

	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_ARGO_BOAT_STATE, 1: Mares5_OnFrame_SailToColchis
	]

}

script MareS5_Script_SupressSurf {
	setmetatile (45, 45, METATILE_Slateport_CalmWater_False, FALSE)
	end
}

script MareS5_EventScript_Argo {
	lockall
	applymovement (OBJ_EVENT_ARGO, Common_Movement_ExclamationMark)
	waitmovement (0)
	msgbox(MareS5_Text_ArgoIntro1, MSGBOX_YESNO)
	closemessage
	if (var(VAR_RESULT) == YES) {
		warpsilent (MAP_ARGO, 16, 9)
		waitstate
	} else {
		msgbox(MareS5_Text_ArgoFarewell)
		closemessage
	}
	releaseall
	end
}

script Mares5_OnFrame_SailToColchis {
	lockall
	setobjectsubpriority (OBJ_EVENT_ID_PLAYER, MAP_MARE_S5, 0)
	hideobjectat (OBJ_EVENT_ID_PLAYER, MAP_MARE_S5)
	call (Common_EventScript_PlayBrineysBoatMusic)
	applymovement (OBJ_EVENT_ARGO, MareS5_Movement_SailToColchis)
	applymovement (OBJ_EVENT_ID_PLAYER, MareS5_Movement_SailToColchis)
	waitmovement (0)
	delay (50)
	applymovement (OBJ_EVENT_ID_PLAYER, MareS5_Movement_PlayerExitsBoat)
	waitmovement (0)
	call (Common_EventScript_StopBrineysBoatMusic)
	showobjectat (OBJ_EVENT_ID_PLAYER, MAP_BIG_ISLAND_N)
	setflag (FLAG_MARES5_HIDE_ARGO)
	resetobjectsubpriority (OBJ_EVENT_ID_PLAYER, MAP_MARE_S5)
	setvar (VAR_ARGO_BOAT_STATE, 2)
	msgbox (MareS5_Text_ArrivedAtColchis, MSGBOX_DEFAULT)
	releaseall
	end
}

movement MareS5_Movement_SailToColchis {
	walk_faster_right * 293
	walk_faster_down * 58
}

movement MareS5_Movement_PlayerExitsBoat {
	walk_right
	walk_down * 4
}

text MareS5_Text_ArgoIntro1 {
	format("Ahoy matey! If you be lost at sea feel free to join us!\nWill you board Argo?")
}

text MareS5_Text_ArgoFarewell {
	format("Farewell! Calm winds and gentle seas to you.")
}

text MareS5_Text_ArrivedAtColchis {
	format("We've landed on the north side of the big island we call Ignis. According to this map Colchis is located within a cave on the west side of the island. Orpheus, search for food and fresh water And Theseus inquire with the town whether the locals will give us shelter and open their markets to Argonauts. Castor and Pollux you will guard the ship while {PLAYER} and I and Philoctetes go investigate the rumored Colchis cave.")
}



