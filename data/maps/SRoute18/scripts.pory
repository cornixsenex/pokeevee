
//DYNAMIC MAP VAR_TEMP_5
//1: Sinus Pacificus Top Left
//2: Castrum Bottom Right
//3: Sinus Clarus Bottom Left
//4: Mare Internum Default

mapscripts SRoute18_MapScripts {

    MAP_SCRIPT_ON_TRANSITION {
		//Handle Dynamic Map Triggers
		specialvar (VAR_TEMP_5, GetDynamicMapSec_SRoute18_F)
		//setup door block
		if (var(VAR_ULYSSES_STATE) < 22) {
			call (SRoute18_Script_SetupDoorBlock)
		}
		//Show Ulysses if stranded - Alcinous Block
		if (var(VAR_ULYSSES_STATE) == 21) {
			call (SRoute18_Script_ShowUlysses)
		}
	}
}

script SRoute18_Script_SetupDoorBlock {
	setobjectxyperm (LOCALID_SROUTE18_DOORBLOCK, 15, 98)
	return
}

script SRoute18_Script_ShowUlysses {
	setobjectxyperm (LOCALID_SROUTE18_ULYSSES, 11, 100)
	setobjectmovementtype (LOCALID_SROUTE18_ULYSSES, MOVEMENT_TYPE_INVISIBLE)
	setobjectxyperm (LOCALID_SROUTE18_NAUSICAA, 12, 100)
	setobjectmovementtype (LOCALID_SROUTE18_NAUSICAA, MOVEMENT_TYPE_FACE_LEFT)
	return
}

script SRoute18_Script_SinusPacificusTransition {
	setvar (VAR_TEMP_5, 1)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script SRoute18_Script_CastrumTransition {
	setvar (VAR_TEMP_5, 2)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script SRoute18_Script_SinusClarusTransition {
	setvar (VAR_TEMP_5, 3)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script SRoute18_Script_MareInternumTransition {
	setvar (VAR_TEMP_5, 4)
	special (TransitionMapMusic)
	special (ShowMapNamePopup)
}

script SRoute18_Script_UlyssesScene {
	//alert player
	applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
	waitmovement (0)
	//N: OMG! lets get you inside to my father
	load_field_pic (FIELD_PIC_NAUSICAA, 190, 98)
	msgbox (SRoute18_Text_Nausicaa)
	destroy_field_pic (FIELD_PIC_NAUSICAA)
	closemessage
	//just do a fade
	fadescreenswapbuffers (FADE_TO_BLACK)
	//remove door block
	removeobject (LOCALID_SROUTE18_DOORBLOCK)
	setobjectxyperm (LOCALID_SROUTE18_DOORBLOCK, 7, 74)
	setobjectmovementtype (LOCALID_SROUTE18_DOORBLOCK, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_SROUTE18_DOORBLOCK)
	//remove N
	removeobject (LOCALID_SROUTE18_NAUSICAA)
	setobjectxyperm (LOCALID_SROUTE18_NAUSICAA, 7, 74)
	setobjectmovementtype (LOCALID_SROUTE18_NAUSICAA, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_SROUTE18_NAUSICAA)
	//remove U
	removeobject (LOCALID_SROUTE18_ULYSSES)
	setobjectxyperm (LOCALID_SROUTE18_ULYSSES, 7, 74)
	setobjectmovementtype (LOCALID_SROUTE18_ULYSSES, MOVEMENT_TYPE_INVISIBLE)
	addobject (LOCALID_SROUTE18_ULYSSES)
	//set var
	setvar (VAR_ULYSSES_STATE, 22)
	//refade
	fadescreenswapbuffers (FADE_FROM_BLACK)
	//fin
	end
}

script Common_Script_LockedDoor {
	msgbox (Common_Text_LockedDoor)
	closemessage
}

text Common_Text_LockedDoor {
	format("The door is locked.")
}

text SRoute18_Text_Nausicaa {
	format("Hello? Mister, can you hear me? You don't look too good... Oh no! I better take you to Papa right away!")
}
